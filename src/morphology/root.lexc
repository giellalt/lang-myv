! Divvun & Giellatekno - open source grammars for Erzya
! Copyright © 2012 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! Morphology
!! ----------
!! INTRODUCTION TO MORPHOLOGICAL ANALYSER OF ERZYA.


Multichar_Symbols
!! Analysis symbols
!! ----------------
!! The morphological analyses of wordforms of ERZYA are presented
!! in this system in terms of following symbols.
!! (It is highly suggested to follow existing standards when adding new tags).

!! The parts-of-speech are:
 +N +A +Adv +V
 +Pron
 +CS ! subordinating conjunction
 +CC ! coordinating conjunction
 +Po +Pr +Interj +Pcle
 +Qnt ! Quantifier

!! The parts of speech are further split up into:
! Nouns
  +Prop
! Pronouns
  +Interr +Rel
  +Pers +Dem +Refl +Recipr
  +Indef ! 
+CollN ! used with paired nouns _collective nouns_
+Dep ! dependent word requiring the presence of another, e.g. _мень_
! Quantifiers (numerals)
 +Num
 +AssocColl ! -ne- ; avide-
 +Assoc ! +мезть
 +Coll ! Collective
 +NCrd ! +Card
 +NOrd ! +Ord
 +Distr ! Distributive
 +Iter ! Iterative form expressing number of times [myv: кавксть] [kpv: кыкысь]
 +Appr ! Approximative numeral кавто-колмо, колмошка 'two or three'
    ! NB! do not confuse with Komi case +Apr

! Adjectives
 +Adn ! Adnominal (modifier) !! This is not an NP head like +Pron
 +Bahuvrihi ! This is a nominative-case NP used as an adjective

! Adverbs
 +Adv-Ideoph ! These are ideophonic descriptors used to modify the verb
            ! вырк ливтясь '_flit_ and it flew off'
 +AdA ! Ad-adjective
 +Manner ! with reference to type of adverb
 +Spat ! spatial
 +Temp ! temporal

! Postpositions
! _+ Spat_, _+ Temp_

! Interjections
 +Formulaic

!! The Usage extents are marked using following tags:
 +Use/Sub   ! Substandard
 +Use/Marg  ! Marginal
 +Use/-Spell !Exclude from speller
 +Use/SpellNoSugg !recognized but not suggested in speller
  +Use/Circ  ! Circular path
  +Use/CircN ! Circular number path
  +Use/-Ped   ! Remove from pedagogical speller
  +Use/NG    ! Do not generate, for isme-ped.fst and apertium

 +Use/LexSub ! The lemma is not an Erzya word (Depricating _+Src/F_)

!! ==== Dialect tags ====
 +Dial/SH ! Short forms
 +Dial/L  ! Long forms
 +Dial 	  ! No specification
    ! Specific to some dialects
    ! Rueter 2010: 8
 +Dial/C  ! 1 Central or Kozlovka-Mokshlei 
 +Dial/W  ! 2 Western or Insar
 +Dial/NW ! 3 North-Western or Alatyr
 +Dial/SE ! 4 South-Eastern or Sura
 +Dial/M  ! 5 Mixed or Drakino-Shoksha

!! ==== Orthography tags ====
  +Orth/PhonDeriv ! Derivation is phonetic
                   ! but declension and conjugation morphologic
  +Orth/PhonInfl  ! Entire inflection is phonetic 1821, 1920-30
  +Orth/PhonComb
  +Orth/SH
  +Orth/Colloq ! Colloquial speech reflected in spelling

!! The nominals are inflected in the following Case and Number
 +Sg +Pl
 +Nom ! Nominative case
 +Gen ! Genitive case
 +Abl ! Ablative case
 +DatCx ! Dative case
 +Ine ! Inessive
 +Ill ! Illative
 +Ela ! Elative
 +Tra ! Translative
 +Abe ! Abessive
 +Prl ! Prolative "га/ка/ва"
 +CompCx ! Comparative case form -шка
 +Com ! Comitative "-нек/-нэк"
 +Loc ! Locative "вить ён : вить ёно"
 +TempCx ! Temporalis case form "-не/-нэ"
 +Voc ! Vocative

!! The possession and other declension types are marked with:
 +PxSg1 +PxSg2 +PxSg3
 +PxPl1 +PxPl2 +PxPl3
 +Def ! Definite
 +InDef ! Indefinite

!! The comparative forms are:
 +Comp ! comparative as opposed to superlative
 +Superl ! superlative
 +Attr ! Attribute

 
!! Verb moods are:
 +Ind ! Indicative
 +Imprt ! Imperative
 +Conj ! Conjunctional "вОль"
 +Opt ! Optative 
 +Des ! Ыксэль 'was about to; wanted to' 
 +Cond ! Ындеря- (Derivational)
 +Oblig ! modality: deontic/directive/obligative
           !  андомс: андома , якамс: якама
 +Delib ! _+ Sugg modality_: deontic/directive/deliberative
       ! I still need the right word for this андомс: андомсат

!! Tenses in the indicative and infrequently in the conditional
 +Prs ! In Erzya There is no morphological distinction 
          ! between present and future
 +Prt1 ! Preterite 1
 +Prt2 ! Preterite 2 (This is also used in predicate forms not involving a finite verb.)

!! Verb personal forms are:
! Subject conjugation
  +ScSg1 +ScSg2 +ScSg3 +ScPl1 +ScPl2 +ScPl3
! Object conjugation
  +OcSg1 +OcSg2 +OcSg3 +OcPl1 +OcPl2 +OcPl3

!! Other verb forms are
  +Neg    ! verb of negation эзь, аволь, иля
  +ConNeg ! main verb complement to Neg, 
  +VGen   ! Verb Genitive
  +VAbl   ! Verb Ablative "озадо"
  +Inf    ! Infinitive
  +TelicPrc ! Telic participle ! "саевть" 
  +Prc ! participle
  +ActPrcLong  ! Ы2й (This is dealt with elsewhere as an active present participle)
  +ActPrcShort ! Ы2  (This is dealt with elsewhere as an active present participle)
  +ActDemPrc   ! Ы2ця  (This is dealt with elsewhere as an active present participle)
  +Ger ! Gerund This is used with Der/Озь and VAbl

!! Is this necessary ?? 2012-08-21
   +PrfPrc

! Abbreviated words are classified with:
  +ABBR ! Abbreviation
  +ACR  ! Acronym

!! Special symbols are classified with:
+CLB +PUNCT +LEFT +RIGHT

!! The verbs are syntactically split according to transitivity:
 +TV ! transitive verb
 +IV ! intransitive verb

!! Special multiword units are analysed with:
 +Multi
!! Non-dictionary words can be recognised with:
 +Guess

!! Question and Focus particles:
 +Qst +Foc


!! Semantics are classified with
 +Mal ! Male
 +Fem ! Female
 +Sur ! Surname
 +Patr ! Patronym
 +Plc ! Place name
 +Org ! name of organization
!! What is this 2012-08-21
 +Obj
 +Ani ! name of animal
 +Rvr ! name of river or water way, media of transportation
 +Anim ! Animate
 +Inanim ! Inanimate
 +Hum    ! Human
 +NonHum ! Nonhuman
 +Kin ! Kinship term (This is reflected in special PxSg2 forms)
 +Divinity ! -паз, -ава _divinity is God personified_
 +Constellation 

 +Plant
 +Group
 +Time 
 +Txt
 +Route
 +Measr 
 +Wthr
 +Build 
 +Event
 +Edu
 +Veh
 +Clth
 +Wpn
 +Body

!! Derivations are classified under the morphophonetic form of the suffix, the
!! source and target part-of-speech.
 +V→N +V→V +V→A


! Der#begin
+Der/ВтОмО
+Der/шка
+Der/Онь
+Der/ай ! vocative
+Der/кай ! vocative
 
!!Then they should have indication of whether they are gerunds or participles!!
!!
+Der/Овт !
+Der/Омс !
+Der/ОмО !
+Der/ОмстО
+Der/ОмсО
+Der/ОмдО
+Der/Омга
+Der/Ома
+Der/Омка
+Der/Ыця
+Der/Ы
+Der/Ый
+Der/Озь !Gerund 
+Der/Ындеря

! Declaring noun derivations
+Der/пель

! Modifier without noun
+Der/MWN ! Modifier without Noun
+Der/Dem ! Speaker-Oriented Demonstrative
! Conjugation of words other than finite verbs
+Der/Pr

!Declaring Indefinite Pronoun derivations
+Der/Гак
+Der/кой
+Der/та
+Der/таго
+Der/бути
+Der/Ыя ! +Indef  ковия, зярыя


!Declaring Noun derivations
+Der/чи ! adjective-to-noun
! the combinatory _Event_ preceding the NP-final noun
+Der/ОмА ! verb-to-noun

!Declaring Deverbal derivations of verbs
+Der/кшнО
+Der/се ! this also shows in denominal demonstrative _Der/Dem_
+Der/не


+Dimin 
+Der/ке
+Der/ка
+Der/нЕ
+Der/Ыне
+Der/Ынька
+Der/Ынькине




!! Morphophonology
!! ---------------
!! To represent phonologic variations in word forms we use the following
!! symbols in the lexicon files:
! {aä} {oö} {uü}

!! And following triggers to control variation
! {front} {back}

! Triggers for morphophonological rules


! Nominal stem vowel following vowel, word finally realized as "й"
!И4

! imperative suffix
!К1

! connegative suffix rendering consonant
!К2

! connegative suffix rendering vowel
!К3

А1 А2 Г1 Г2 Г4 Ь2 Т1 О1 Ы2 К1 К3 К4 У2 И2 TLoss KLoss VLoss WLoss %>
%^Inf
%^PATR
%^FEM

!These are used for variation некшнемс vs некшнэмс
+Stem/е
+Stem/э

!Range limitations with regard to 4 classical sentence types:
! Statement, Question,      Command, Exclamation
! --"--    , + Range/Interr, 
! 

!! MISC
  +Hyph  ! A tag to indicate that a hyphen was used when compounding

  ! Different focus particles
  +Foc/Гак

! Development tag
+WORK

ZERO
%0

%-

! tags
§+Dig1
+Dig2
+Dig3
+Dig4
+Rom !Roman numerals



! tags
+Emphatic
+Gr2000
+Pref ! Prefix
+Exclusive ! "ансяк"
+Intensive
+Intensifier ! уш


+Onom ! onomapoetic words
+Descr ! descriptive words
!! Focus clitics
+Clt/Гак
!! Imperative clitics
+Clt/Га ! редяка
+Clt/Гая ! редякая
+Clt/Гаять ! редякаять
+Clt/Гаятя ! редякаятя
+Clt/Гатя ! редякатя
+Clt/Гака ! редякака ARE these real?
+Clt/Гакая ! редякакая ARE these real?
!+Pred ! Marking the predicate
+Pred2 ! secondary predicate
       ! Joe came in _with his hat on_.
       ! Joe came in _Joe had his hat on_.

!!!! FUNCTIONS are all upper-case !!!!
+ACC +DAT +COM ! This marks a function not a morpheme

+NoPoss ! used with personal pronouns in oblique cases, 
        ! where a possessor index is expected

!!Pair Verbs, Serial Verbs compounding
+SerialVerb


!! We have manually optimised the structure of our lexicon using following
!! flag diacritics to restrict morhpological combinatorics:
!@P.NeedNoun.ON@  @D.NeedNoun.ON@  @C.NeedNoun@

!!!! Flags used to identify parts of speech
@P.POS.PRON@
@P.POS.N@
@R.POS.N@
@P.POS.V@
@R.POS.V@
@C.POS@

!!!! Flags used with serial verbs
! INF = Infinitive
@U.CONJ-INF.YES@
@U.CONJ-INF.NO@

! TX = tense
@U.CONJ-TX.NONPAST@
@U.CONJ-TX.PRT1@
@U.CONJ-TX.PRT2@

! MX = mood
@U.CONJ-MX.IND@
@D.CONJ-MX.IND@ ! 2012-11-04 should this be _D_ or _N_
@U.CONJ-MX.IMP@
@U.CONJ-MX.OPT@
@U.CONJ-MX.DES@
@U.CONJ-MX.CONJ@
@U.CONJ-MX.COND@

! CONNEG = negation
@U.CONJ-CONNEG.YES@
@U.CONJ-CONNEG.NO@

! NX = number
@U.CONJ-NX.PL@
@U.CONJ-NX.SG@

! POSS = possessive person
@U.CONJ-POSS.1@
@U.CONJ-POSS.2@
@U.CONJ-POSS.3@
@U.CONJ-POSS.2ACC@
@U.CONJ-POSS.3ACC@

! PX = person
@U.CONJ-PX.1%0@
@U.CONJ-PX.12@
@U.CONJ-PX.13@
@U.CONJ-PX.15@
@U.CONJ-PX.16@
@U.CONJ-PX.2%0@
@U.CONJ-PX.21@
@U.CONJ-PX.23@
@U.CONJ-PX.24@
@U.CONJ-PX.26@
@U.CONJ-PX.3%0@
@U.CONJ-PX.31@
@U.CONJ-PX.32@
@U.CONJ-PX.33@
@U.CONJ-PX.34@
@U.CONJ-PX.35@
@U.CONJ-PX.36@

@U.CONJ-PX.4%0@
@U.CONJ-PX.42@
@U.CONJ-PX.43@
@U.CONJ-PX.45@
@U.CONJ-PX.46@

@U.CONJ-PX.5%0@
@U.CONJ-PX.51@
@U.CONJ-PX.53@
@U.CONJ-PX.54@
@U.CONJ-PX.56@

@U.CONJ-PX.6%0@
@U.CONJ-PX.61@
@U.CONJ-PX.62@
@U.CONJ-PX.63@
@U.CONJ-PX.64@
@U.CONJ-PX.65@
@U.CONJ-PX.66@

! Object conjugation
@P.CONJ.ObjAll@
@R.CONJ.ObjAll@
@C.CONJ@
@P.TLOSS.ON@
@R.TLOSS.ON@

! nominals
@P.PossPx.Sg1@
@P.PossPx.Sg2@
@P.PossPx.Sg3@
@P.PossPx.Pl1@
@P.PossPx.Pl2@
@P.PossPx.Pl3@


@U.PossPx.Sg1@
@U.PossPx.Sg2@
@U.PossPx.Sg3@
@U.PossPx.Pl1@
@U.PossPx.Pl2@
@U.PossPx.Pl3@

@D.PossPx@
@C.PossPx@
!!Adding Tense 2006-12-04
@P.TNUM.SG@
@P.TNUM.PL@
@D.TNUM.SG@
@D.TNUM.PL@
@C.TNUM@

@P.TPERS.1@
@P.TPERS.2@
@P.TPERS.3@
@N.TPERS.1@
@N.TPERS.2@
@N.TPERS.3@
@U.TPERS.1@
@U.TPERS.2@
@U.TPERS.3@
@C.TPERS@

!!Cases
@N.CX.ILL@
@N.CX.ELA@
@N.CX.INE@
@N.CX.LAT@
@U.CX.ABL@
@U.CX.ELA@
@U.CX.ILL@
@U.CX.INE@
@U.CX.LAT@
@U.CX.LOC@
@U.CX.NOM@
@U.CX.PRL@
@U.CX.TRA@
@C.CX@

!!Dx number
@P.DNUM.PL@
@P.DNUM.SG@
@U.DNUM.PL@
@U.DNUM.SG@
@C.DNUM@


!!!!What are these?
!@P.PERS.1@
!@P.PERS.2@
!@P.PERS.3@
!@D.PERS.1@
!@D.PERS.2@
!@D.PERS.3@
!@N.PERS.1@
!@N.PERS.2@
!@N.PERS.3@
!@C.PERS@

!!
@P.NUM.SG@
@P.NUM.PL@
@D.NUM.SG@
@D.NUM.PL@
@C.NUM@

!These are used in INDEFinite-pronoun clitic determination
@P.INDEF.PREF@
@D.INDEF.PREF@
@R.INDEF.PREF@
@C.INDEF@

!! This allows or disallows combining with hyphen through loop
!! especially for acronyms 2012-11-04
@U.HYPH-COMBO.ACRO@
@D.HYPH-COMBO.ACRO@
@C.HYPH-COMBO@


!!Linking vowel for use with Translative
! Present in pron-myv-morph.txt
@P.LV.ON@ 
@P.LV.OFF@ 
@R.LV.ON@ 
@U.LV.ON@ 
@D.LV.ON@ 
@C.LV@ 



! Removal
@C.CONJ-INF@
@C.CONJ-TX@
@C.CONJ-MX@
@C.CONJ-CONNEG@
@C.CONJ-NX@
@C.CONJ-PX@
@C.CONJ-POSS@

@C.KLOSS@
@C.TLOSS@

!!! FLAGS USED WITH COLLECTIVE NOUNS
! number
@U.DECL-NX.SG@
@U.DECL-NX.PL@
@R.DECL-NX.PL@
! case
@U.DECL-CX.NOM@
@U.DECL-CX.ACC@
@U.DECL-CX.GEN@
@U.DECL-CX.DAT@
@U.DECL-CX.ABL@
@U.DECL-CX.ILL@
@U.DECL-CX.INE@
@U.DECL-CX.ELA@
@U.DECL-CX.LAT@
@U.DECL-CX.LOC@
@U.DECL-CX.TRA@
@U.DECL-CX.PRL@
@U.DECL-CX.COM@
@U.DECL-CX.TEMP@
@U.DECL-CX.ABE@
@U.DECL-CX.CMP@

! declension type
@U.DECL-DX.DEF@
@U.DECL-DX.INDEF@
@U.DECL-DX.PX@

! predicate
!@U.DECL-PR.1@
!@C.DECL-PR@

!! Removal
@C.DECL-NX@
@C.DECL-DX@
@C.DECL-CX@



! Key lexicon

! These are the lexica that are found in the bin/ directory,
! generated by the Makefile from the dictionary

LEXICON Root
!! The word forms in ERZYA start from the lexeme roots of basic
!! word classes, or optionally from prefixes:
TESTINGTESTING ;

 adjectives ;                                     ! 
 adjective-adverb-modifiers ;                                  ! 
 adverbs ;                                   ! 
 bahuvrihis ;                             ! 
 conjunctions ;                                  ! 
 demonstratives ;                                   ! 
 interjections ;                                ! 
 noun-deverbal ;                                  ! 
 nouns ;                                     ! 
 propernouns ;
 participles ;
 particles ;
 postpositions ;
 pronouns ;
 quantifiers ;
 verbs ;

!!
! CyrillicFemaleName ;
!! HUNSPELL Type name derivation
 RussianMalenamesDerive ; !
! RussianSurnamesDerive ; 
 urj-Cyrl-ProperNouns ; !Testing 2012-11-17

PERS ;                                  ! 
ejs-PO ;                                ! 
 Indef_Pronouns ;
 Indef_Pronouns-pref ;			! This will take prefix and Flags
EXCEPTIONS ;                          ! exceptions.lexc

Abbreviation ;                          ! abbr-myv-lex.txt
Acronym ;                               ! acro-myv-lex.txt
Punctuation ;                           ! punct-myv-lex.txt

!ABBR ;                                 ! ../../tmp/out/ABBR_myv-lex.xml           !2012-03-14
!ACRON ;                                ! ../../tmp/out/ACRON_myv-lex.xml          !2012-03-14
!IDIOM ;                                ! ../../tmp/out/IDIOM_myv-lex.xml          !2012-03-14
!N-DV ;                                 ! ../../tmp/out/N-DV_myv-lex.xml           !2012-03-14
!N-F ;                                  ! ../../tmp/out/N-F_myv-lex.xml            !2012-03-14
!No-e-element-N ;                       ! ../../tmp/out/No-e-element-N_myv-lex.xml !2012-03-14
!No-e-element-V ;                       ! ../../tmp/out/No-e-element-V_myv-lex.xml !2012-03-14
!NUM ;                                  ! ../../tmp/out/NUM_myv-lex.xml            !2012-03-14
!NUM-PREFIXES ;                         ! num-myv-morph.txt                        !2012-03-14
!PART-ONOM ;                            ! ../../tmp/out/PART-ONOM_myv-lex.xml      !2012-03-14
                                        ! ../../tmp/out/PERS_myv-lex.xml.txt
! Misc_Pronouns ;                                              !2012-04-03
!PRON-INDEF ;                           ! ../../tmp/out/PRON-INDEF_myv-lex.xml                           !2012-03-14
!PRON-INTER ;                           ! ../../tmp/out/PRON-INTER_myv-lex.xml                           !2012-03-14
!PRON-NEG ;                             ! pron-myv-morph.txt                                             !2012-03-14
!PRON-QNT ;                             ! ../../tmp/out/PRON-QNT_myv-lex.xml                             !2012-03-14
!PRON-REFL ;                            ! ../../tmp/out/PRON-REFL_myv-lex.xml                            !2012-03-14
!PRON-REL ;                             ! pron-myv-morph.txt                                             !2012-03-14
!PROP-RUSSIAN-TOPONYMS ;                ! ../../tmp/out/PROPRUSSIAN-TOPONYMS_myv-lex.xml                 !2012-03-14
!PROP-RUSSIAN-MALE-SURNAMES-Derivable ; ! ../../tmp/out/PROP-RUSSIAN-MALE-SURNAMES-Derivable_myv-lex.xml !2012-03-14
!PROP-CYRILLIC-MALE-SURNAMES ;          ! ../../tmp/out/PROP-CYRILLIC-MALE-SURNAMES_myv-lex.xml          !2012-03-14

!V-N2END ;                              ! ../../tmp/out/V-N2END_myv-lex.xml                              !2012-03-14


! ======================================================= !
! These lexica are for parts of speech without morphology !
! ======================================================= !

! Compounding

LEXICON R
%- nouns ;

LEXICON BahuvTag-kal
+Bahuvrihi: Adj-od ;

LEXICON BahuvTag-pango
+Bahuvrihi: Adj-nachko ;

LEXICON BahuvTag-kudo
+Bahuvrihi: Adj-shumbra ;


! ============ !
! Interj lexica !
! ============ !

LEXICON InterjTag
+Interj: Clitic ;

LEXICON Interj-Imp
+Interj+Imprt+ScSg2: ImpClitic ;
+Interj+Imprt+ScPl2:%>дО1 ImpClitic ;

LEXICON FormulaicTag
+Interj+Formulaic: WORD-END ;

LEXICON VocativeTag
+Interj+Voc: WORD-END ;

! ============ !
! Postp lexica !
! ============ !

LEXICON ejs-PO
эйсэ+Po+Ill:эйс Clitic ;
эйсэ+Po+Ine:эйсэ kudo-Conjugation ;
эйсэ+Po+Ela:эйстэ kudo-Conjugation ;
эйсэ+Po+Ill:эйз osh-SPAT-PossX ;
эйсэ+Po+Ine:эйсэ kudo-SPAT-PossX ;
эйсэ+Po+Ela:эйстэ kudo-SPAT-PossX ;

LEXICON marto-PO
+Po+Adn: Clitic ;
+Po+COM: kudo-Possessives2 ; 
+Po+Der/MWN: N-kudo ;


LEXICON kudo-PO-ILL
+Po+Ill:%>с Clitic ;
+Po+Ill:%>з osh-Possessives2 ;

LEXICON Postp-karsho
+Po: WORD-END ;
+Po: PossMarker-then-Clt ;
+Po+Ill:%>з%>О1 PossMarker-then-Clt ;

LEXICON vaksso-SPAT
 malaso-SPAT ;
+Po+Ela:%>О1стО1 kudo-Conjugation ;
+Po+Ill:%>О1с Clitic ;
+Po+Ine:%>О1сО1 kudo-Conjugation ;

LEXICON jutkso-SPAT
+Po+Prl:%>О1%>Г2а kudo-SPAT-PossX ;
+Po+Prl:%>О1%>Г2а kudo-Conjugation ;
 langso-SPAT ;

LEXICON langso-SPAT
+Po+Ela:%>стО1 kudo-Conjugation ;
+Po+Ill:%>с Clitic ;
+Po+Lat:%>О1в Clitic ;
+Po+Ine:%>сО1 kudo-Conjugation ;
+Po+Ela:%>стО1 kudo-SPAT-PossX ;
+Po+Ill:%>О1з osh-SPAT-PossX ;
+Po+Ine:%>сО1 kudo-SPAT-PossX ;

! malaso:mala
LEXICON malaso-SPAT
+Adv+Ela:%>стО1 kudo-Conjugation ;
+Adv+Ill:%>с Clitic ;
+Adv+Ine:%>сО1 kudo-Conjugation ;
+Adv+Prl:%>Г2а kudo-Conjugation ;
+Adv+Lat:%>в kudo-Conjugation ;
+Po+Ela:%>стО1 kudo-SPAT-PossX ;
+Po+Ill:%>О1з osh-SPAT-PossX ;
+Po+Ine:%>сО1 kudo-SPAT-PossX ;
+Po+Prl:%>Г2а kudo-SPAT-PossX ;
+Po+Lat:%>в kudo-Conjugation ;

LEXICON ushoso-SPAT
+Spat+Ela:%>стО1 kudo-Conjugation ;
+Spat+Ill:%>с Clitic ;
+Spat+Ine:%>сО1 kudo-Conjugation ;
+Spat+Prl:%>Г2а kudo-Conjugation ;

LEXICON kudo-SPAT-PossX
+PxSg1:%>нЬ2 kal-Conjugation ;
+PxSg1+Dial:%>м kal-Conjugation ;
+PxSg2:%>тЬ2 kit-Conjugation ;
+PxSg2+Dial:%>нтЬ2 kit-Conjugation ;
+PxSg3:%>нзО1 kudo-Conjugation ;
+PxPl1:%>нО1к lak-Conjugation ;
+PxPl2:%>нк lak-Conjugation ;
+PxPl3:%>ст kit-Conjugation ;

LEXICON osh-SPAT-PossX
+PxSg1:%>О1нЬ2 kal-Conjugation ;
+PxSg1+Dial:%>О1м kal-Conjugation ;
+PxSg2:%>О1тЬ2 kit-Conjugation ;
+PxSg2+Dial:%>О1нтЬ2 kit-Conjugation ;
+PxSg3:%>О1нзО1 kudo-Conjugation ;
+PxPl1:%>О1нО1к lak-Conjugation ;
+PxPl2:%>О1нк lak-Conjugation ;
+PxPl3:%>О1ст kit-Conjugation ;



! This contlex allows for relational word which, otherwise, are open 
! to extensive declension
LEXICON PostpTag
+Po: Clitic ;

LEXICON Dat-1Arg
+Po+DAT: Clitic ;



! ==================================== !
! Lexica called End, whatever they are !
! ==================================== !


LEXICON Cc
+CC: WORD-END ;

LEXICON Cs
+CS: WORD-END ;


!LEXICON EndConj
!+CC: WORD-END ;

!!Let's remember that Particle is merely an expression of form
!LEXICON PCLE
!а+Pcle+Neg:а WORD-END ;
!аволь+Pcle+Neg+Emphatic:аволь WORD-END ;
!ай+Pcle+Neg+Emphatic:ай N-kal ;
!апак+Pcle+Neg:апак lak-Tenses-Obl ;
!седе+Pcle+Comp:седе WORD-END ;
!сех+Pcle+Superl:сех WORD-END ;
!сех+Pcle+Superl+Abl:сехте WORD-END ; ! This is a comparative construction
!уш+Pcle+Intensifier:уш WORD-END ;

LEXICON Pcle-Compar-Stnd
+Pcle+Comp: WORD-END ;

LEXICON Pcle-Epist
+Pcle+Epist: WORD-END ;

LEXICON Pcle-Neg-Contrast
+Pcle+Neg+Intensive: WORD-END ;

LEXICON Pcle-Neg
+Pcle+Neg: WORD-END ;

LEXICON Pcle-Superl
+Pcle+Superl: WORD-END ;

LEXICON PcleTag
+Pcle: WORD-END ;


!LEXICON EndPart
!+Pcle: WORD-END ;

!LEXICON EndInterj
!+Interj: WORD-END ;

LEXICON Interj
+Interj: WORD-END ;

LEXICON N-Voc-Px1Sg
+N+PxSg1+Interj+Voc: WORD-END ;

!LEXICON EndNeg 
!+CC+Neg: WORD-END ; ! ни

!LEXICON EndQST
!+Qst: WORD-END ;

!LEXICON ONOM-End
!+Onom: WORD-END ;

!LEXICON DESCR-End
!+Descr: WORD-END ;


! List of spurious lexica
! These should be fixed.


! This lexicon would allow a loop whereby any full form
! could be treated as a noun
! E.G. мезеяк # 'anything'
! E.G. мезеяк+ ось # 'This _anything_ you are talking about'
!LEXICON Encapsulate


!LEXICON ADV-SUPERL
!+Adv+Superl: WORD-END ;


LEXICON TESTINGTESTING

Ыджыд TESTCOMP ;
син TESTCOMP ;
официал TESTCOMP ;
радиоактив TESTCOMP ;

LEXICON TESTCOMP

+CmpTest: WORD-END ;


!!!
!formerly Spatials_4cx
LEXICON alo-Spat
 alo-Spat-NoArg ;
 alo-Spat-1Arg ;

LEXICON alo-Spat-NoArg
+Adv+Spat+Orth/PhonDeriv+Loc+Der/це:%>це kudo-N2-Def-Declension ;
+Adv+Spat+Loc+Der/се:%>се kudo-N2-Def-Declension ;
+Adv+Spat+Der/кс:%>кс N-osh ;
+Adv+Spat+Lat:%>О1в Clitic ;
+Adv+Spat+Abl:%>дО1 Clitic ;
+Adv+Spat+Loc:%>О1 Clitic ;
+Adv+Spat+Prl:%>Г2а Clitic ;
+Adv+Spat+Loc+InDef+Der/MWN+N:%>О1 kudo-N2-Def-Declension ; 
+Adv+Spat+Prl+InDef+Der/MWN+N:%>Г2а kudo-N2-Def-Declension ;

LEXICON alo-Spat-1Arg
+Po+Spat+Abl:%>дО1 kudo-Indef--Poss ;
+Po+Spat+Loc:%>О1 kudo-Indef--Poss ; 
+Po+Spat+Lat:%>О1 kudo-Possessive-Before-Clitic ;
+Po+Spat+Prl:%>Г2а kudo-Indef--Poss ;
+Po+Spat+Orth/PhonDeriv+Loc+Der/це:%>це kudo-N2-Def-Declension ;
+Po+Spat+Loc+Der/се:%>се kudo-N2-Def-Declension ;
+Po+Spat+Der/кс:%>кс N-osh ;
+Po+Spat+Lat+InDef:%>О1в Clitic ;
+Po+Spat+Abl+InDef:%>дО1 kudo-N2-Def-Declension ;
+Po+Spat+Loc+InDef+Der/MWN+N:%>О1 kudo-N2-Def-Declension ; 
+Po+Spat+Prl+InDef+Der/MWN+N:%>Г2а kudo-N2-Def-Declension ;

LEXICON alo-Spat-AdvN
 alo-Spat-NoArg ;
 N-kal ;
+N+Loc:%>О1 Clitic ;

LEXICON alo-Spat-PoN
 alo-Spat-1Arg ;
 N-kal ;
+N+Loc:%>О1 Clitic ;

LEXICON kovolo-Spat
+Interr+Spat+Lat+InDef:%>О1в Clitic ;
+Interr+Spat+Abl+InDef:%>дО1 kudo-N2-Def-Declension ;
+Interr+Spat+Loc+InDef+Der/MWN+N:%>О1 kudo-N2-Def-Declension ; 
+Interr+Spat+Prl+InDef+Der/MWN+N:%>Г2а kudo-N2-Def-Declension ;
+Interr+Spat+Abl:%>дО1 kudo-Indef--Poss ;
+Interr+Spat+Loc:%>О1 kudo-Indef--Poss ; 
+Interr+Spat+Prl:%>Г2а kudo-Indef--Poss ;

+Rel+Spat+Lat+InDef:%>О1в Clitic ;
+Rel+Spat+Abl+InDef:%>дО1 kudo-N2-Def-Declension ;
+Rel+Spat+Loc+InDef+Der/MWN+N:%>О1 kudo-N2-Def-Declension ; 
+Rel+Spat+Prl+InDef+Der/MWN+N:%>Г2а kudo-N2-Def-Declension ;
+Rel+Spat+Abl:%>дО1 kudo-Indef--Poss ;
+Rel+Spat+Loc:%>О1 kudo-Indef--Poss ; 
+Rel+Spat+Prl:%>Г2а kudo-Indef--Poss ;



LEXICON Mezty
+Assoc+Pl+Nom:%>мезть Clitic ;
+Assoc+DatCx:%>мезнень Clitic ;
+Assoc+Gen:%>мезень Clitic ;
+Assoc+Abl:%>мезде Clitic ;
+Assoc+Abe:%>мезевтеме Clitic ;
!+Assoc:%>мезть Clitic


 
LEXICON osh-Indef-DefSg-Poss
 osh--DefSg- ;
 osh-Indef--Poss ;

LEXICON osh--DefSg-
+Sg+Def:%>О1нть Clitic ;

LEXICON osh-Indef--Poss
+InDef: osh-Tenses-Obl ;
+PxSg1:%>О1нЬ2 kal-Tenses-Obl ;
+PxSg2:%>О1тЬ2 kit-Tenses-Obl ;
+PxSg3:%>О1нзО1 kudo-Tenses-Obl ;
+PxPl1:%>О1нО1к lak-Tenses-Obl ;
+PxPl2:%>О1нк lak-Tenses-Obl ;
+PxPl3:%>О1ст kit-Tenses-Obl ;

LEXICON kudo-Indef-DefSg-Poss
 kudo--DefSg- ;
 kudo-Indef--Poss ;

LEXICON kudo--DefSg-
+Sg+Def:%>О1нть Clitic ;

LEXICON kudo-Indef--Poss
+InDef: kudo-Tenses-Obl ;
+PxSg1:%>нЬ2 kal-Tenses-Obl ;
+PxSg2:%>тЬ2 kit-Tenses-Obl ;
+PxSg3:%>нзО1 kudo-Tenses-Obl ;
+PxPl1:%>нО1к lak-Tenses-Obl ;
+PxPl2:%>нк lak-Tenses-Obl ;
+PxPl3:%>ст kit-Tenses-Obl ;




! TODO: Integrate this in xml-files in working…

!LEXICON SUBSTANDARDS 
!коръя+Use/Sub:корья Noun1 ;

!! +WORK Shows up then the following LEXICA are used.

LEXICON vitj-Adj-WORK
+WORK: Adj-vitj ;

LEXICON erek-Adj-WORK
+WORK: Adj-erek ;

LEXICON od-Adj-WORK
+WORK: Adj-od ;

LEXICON vanjks-Adj-WORK
+WORK: Adj-vanjks ;

LEXICON nachko-Adj-WORK
+WORK: Adj-nachko ;

LEXICON lembe-Adj-WORK
+WORK: Adj-lembe ;

LEXICON shumbra-Adj-WORK
+WORK: Adj-shumbra ;

LEXICON kal-N-WORK
+WORK: N-kal ;

LEXICON kit-N-WORK
+WORK: N-kit ;

LEXICON lak-N-WORK
+WORK: N-lak ;

LEXICON osh-N-WORK
+WORK: N-osh ;

LEXICON pango-N-WORK
+WORK: N-pango ;

LEXICON tinge-N-WORK
+WORK: N-tinge ;

LEXICON teshte-N-WORK
+WORK: N-teshte ;

LEXICON kudo-N-WORK
+WORK: N-kudo ;






! vim: set ft=xfst-lexc:
