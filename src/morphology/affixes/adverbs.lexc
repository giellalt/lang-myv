!! !!!Adverb inflection

!! The Erzya language adjectives do not compare.


LEXICON adverbs
!!
ков+Adv+Interr+Lat:ков K ;
коволга+Adv+Interr+Prl:коволга K ;
коволдо+Adv+Interr+Abl:коволдо K ;
коволо+Adv+Interr+Loc:коволо K ;
коволов+Adv+Interr+Lat:коволов K ;
!
кодаяк+Adv+Indef+Manner:кодаяк # ;
козонь+Adv+Interr+Ill:козонь K ;


! These _states_ can conjugate,
! a distinctive feature
!
!LEXICON AdvState-kudo_Sub
!+Err/Orth: AdvState-kudo ; 

LEXICON AdvState-kudo
+Adv+State: K ;
+Adv+State+Clt/Cop:%> StateConjugation-kudo ;
+Adv+State+Der/MWN: N_KUDO ;



! This is fed by AdvState
LEXICON StateConjugation-kudo
+Ind+Prs+ScSg1:А2н K ;
+Err/Orth+Dial+Ind+Prs+ScSg1:ян K ;
+Ind+Prs+ScSg2:А2т K ;
+Err/Orth+Dial+Ind+Prs+ScSg2:ят K ;
+Ind+Prs+ScSg3: K ;
+Ind+Prt2+ScSg1:линь K ;
+Ind+Prt2+ScSg2:лить K ;
+Ind+Prt2+ScSg3:ль K ;

+Ind+Prs+ScPl1:тА2но K ;
+Ind+Prs+ScPl2:тА2до K ;
+Ind+Prs+ScPl3:тЬ2 K ;
+Ind+Prt2+ScPl1:линек K-lak ;
+Ind+Prt2+ScPl2:лиде K ;
+Ind+Prt2+ScPl3:льть K ;


!LEXICON ABBR-IS_ADV
!+ABBR: ADVTAG ;

LEXICON Ad-A_ ! 
+Adv+AdA: WORD-END ;

LEXICON ADV_ ! 
+Adv: K ;

LEXICON ADV-EVID-NFH_ !келя 
+Adv+EvidNfh: K ;

LEXICON GER_
+Ger: K ;
+Ger+Der+Der/MWN: N_OSH ;

LEXICON ADV_KUDO ! Coping with stem structure
 ADV_ ;

! истяк 
LEXICON ADV_LAK
+Adv: K-lak ;

LEXICON ADV_OSH
+Adv: K ;

LEXICON ADV_KAL
+Adv: K ;

LEXICON ADV-TEMP_IS-TEMP
+Adv+Temp: K ;

LEXICON ADV-TEMP-ITER
+Adv+Temp+Iter: K ;

LEXICON ADV-TEMP_
+Adv+Temp: K ;

LEXICON ADV-SPAT_ !!= @CODE@ пачк
+Adv+Spat: K ;

LEXICON ADV-SPAT-IS-ABL !!= @CODE@ алов
+Adv+Spat+Abl: K ;

LEXICON ADV-SPAT-IS-LAT !!= @CODE@ алов
+Adv+Spat+Lat: K ;

LEXICON ADV-SPAT-IS-LOC !!= @CODE@ ало
+Adv+Spat+Loc: K ;

LEXICON ADV-SPAT-IS-PRL !!= @CODE@ перька
+Adv+Spat+Prl: K ;

LEXICON ADV/PO/PRON-SPAT_ALO !!= @CODE@ ало:ал
 ADV-SPAT_ALO ;
+Adp+Spat+Abl:%>дО1 K ;
+Adp+Spat+Loc:%>О1 K ;
+Adp+Spat+Prl:%>Г2а K ;
+Adp+Spat+Lat:%>%{ОЕЭØ%}в K ;
+Pron+Spat+Abl:%>дО1 POSSX4-END-FOC-CLT/COP-SC3 ;
+Pron+Spat+Loc:%>О1 POSSX4-END-FOC-CLT/COP-SC3 ;
+Pron+Spat+Prl:%>Г2а POSSX4-END-FOC-CLT/COP-SC3 ;
+Pron+Spat+Lat:%>О1 POSSX4-END-FOC-CLT/COP-SC3 ;



LEXICON ADV-SPAT_ALO !!= @CODE@ ало:ал
+Adv+Spat+Abl:%>дО1 K ;
+Adv+Spat+Loc:%>О1 K ;
+Adv+Spat+Prl:%>Г2а K ;
+Adv+Spat+Orth/PhonDeriv+Loc+Der/це:%>це _KUDO-N2-Def-Declension ;
+Adv+Spat+Der/кс:%>кс N_OSH ;
+Adv+Spat+Lat:%>%{ОЕЭØ%}в K ;

LEXICON ADV-SPAT_karsho
+Adv+Spat: K ;
+Adv+Spat: POSSX4-END-FOC-CLT/COP-SC3 ;
+Adv+Spat+Ill:%>з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON ADV-INE_KISE2
+Adv+Ine: POSSX4-END-FOC-CLT/COP-SC3 ;

LEXICON PO-SPAT_karsho
+Adp+Spat: K ;


!LEXICON ADV/PO/PRON-SPAT_
!!! relative space __мельга__
!+Adv+Spat: K ;
!+Adp+Spat: K ;
!+Adv+Spat: POSSX4-END-FOC-CLT/COP-SC3 ;


LEXICON PO/PRON-SPAT_
!! relative space __мельга__
+Adp+Spat: K ;
+Pron+Spat: POSSX4-END-FOC-CLT/COP-SC3 ;



LEXICON Adv_Phrase ! 
+Adv: WORD-END ;

LEXICON ADV-IDEOPH ! 
+Adv+Ideoph: K ;
! сорк, кальцк ; twang, thwang, bang

LEXICON ADV-IDEOPH-IS-INE ! 
+Adv+Ideoph+Ine: K ;

LEXICON ADV-INDEF_
+Adv+Indef: K ;

LEXICON Exclusive ! 
+Adv+Exclusive: K ;

LEXICON ADV-PARENTHETIC_ ! 
+Adv+Parenthetic: K ;

!LEXICON ADV_-ELA ! 
!+Adv+Ela: K ;

LEXICON Adv-Ine ! 
+Adv+Ine: K ;

!LEXICON Adv-Tra ! 
!+Adv+Tra: K ;

LEXICON ADV_-TRA ! 
+Adv+Tra: K ;

LEXICON MANNER_ ! 
+Adv+Manner: K ;

LEXICON MANNER_-ELA ! 
A-DER/ELA-ADV-MANNER ;

LEXICON A-DER/ELA-ADV-MANNER ! 
+Adv+Manner+Ela: K ;

LEXICON MANNER_-TRA ! 
+Adv+Manner+Tra: K ;

LEXICON ADV-IS-INTERR-MANNER
+Adv+Interr+Manner: K ;

!LEXICON Manner-Interr ! 
! ADV-IS-INTERR-MANNER ;

!LEXICON	PRED2-OBJ_KUDO !! "апак прядо"
!+Adv+Pred2: K ;
!+Adv+Pred2+Der/MWN: N_KUDO ;

!LEXICON	PRED2-OBJ_OSH
!+Adv+Pred2: K ;
!+Adv+Pred2+Der/MWN: N_OSH ;

LEXICON PRED2-SUBJ_KUDO_Sub !!
+Err/Orth: PRED2-SUBJ_KUDO ;
LEXICON PRED2-SUBJ_KUDO !! "стядо"
+Adv+Pred2: K ;
+Adv+Pred2+Der/MWN: N_KUDO ;

LEXICON PRED2-SUBJ_OSH
+Adv+Pred2: K ;
+Adv+Pred2+Der/MWN: N_OSH ;

LEXICON Pred2 ! "стядо", "апак прядо", "чавонь кедть"
+Adv+Pred2: K ;
+Adv+Pred2+Der/MWN: N_KUDO ;

!! spatial adverbs 
!! dependent and independent case marking 
LEXICON SPAT-NoArg
+Adv+Spat: K ;

LEXICON ADV-IS-SPAT-ELA
+Adv+Ela: K ;
+Adv+Ela: CLT/COP4_OBL ;

LEXICON ADV-IS-SPAT-ILL
+Adv+Ill: K ;

LEXICON ADV-IS-SPAT-INE
+Adv+Ine: K ;
+Adv+Ine: CLT/COP4_OBL ;

LEXICON ADV-IS-SPAT-ABL
+Adv+Abl: K ;

LEXICON ADV-IS-SPAT-LAT
+Adv+Lat: K ;

LEXICON ADV-IS-SPAT-PRL
+Adv+Prl: K ;
+Adv+Prl: CLT/COP4_OBL ;


LEXICON ADV-INTERR-IS-LAT
+Adv+Interr+Lat: K ;
!LEXICON SPAT-Interr
!+Interr+Spat: WORD-END ;

LEXICON ADV-INTERR-IS-SPAT-ABL
+Adv+Interr+Abl: K ;

LEXICON ADV-INTERR-IS-SPAT-INE
+Adv+Interr+Ine: K ;
+Adv+Interr+Ine: CLT/COP4_OBL ;

LEXICON ADV-INTERR-IS-SPAT-ELA
+Adv+Interr+Ela: K ;

LEXICON ADV-INTERR-IS-SPAT-LAT
+Adv+Interr+Lat: K ;

LEXICON ADV-INTERR-IS-SPAT-PRL
+Adv+Interr+Prl: K ;

LEXICON Spat_
+Adv+Spat: K ;

LEXICON SPAT-Ela
+Adv+Spat+Ela: K ;
!+Adp+Spat+Ela: _KUDO-Possessive-Before-K ;

LEXICON SPAT-Ine
+Adv+Spat+Ine: K ;
!+Adp+Spat+Ine: _KUDO-Possessive-Before-K ;

LEXICON SPAT-Lat
+Adv+Spat+Lat: K ;

LEXICON SPAT-Prl
+Adv+Spat+Prl: K ;
!+Adp+Spat+Prl: _KUDO-Possessive-Before-K ;

!!Temporal tags with cases
LEXICON TEMP_
+Adv+Temp: K ;

!! This marking would indicate a word form that may be 
!  construed as an adv as well as a adposition.
!LEXICON Temp-NoArg
!+Adv+Temp: K ;

LEXICON Temp-Coll
+Adv+Temp+Coll: K ;

LEXICON Temp-Distr
+Adv+Temp+Distr: K ;

LEXICON Temp-Ela
+Adv+Temp+Ela: K ;

LEXICON Temp-Ill
+Adv+Temp+Ill: K ;

LEXICON Temp-Indef
+Adv+Temp+Indef: K ;

LEXICON Temp-Interr
+Adv+Interr+Temp: K ;

LEXICON Temp-Lat
+Adv+Temp+Lat: K ;

LEXICON Temp-ms
+Adv+Der/мс+Temp+Ill: K ;

LEXICON Temp-Pl
+Adv+Temp+Pl: K ;

LEXICON Temp-Prl
+Adv+Temp+Prl: K ;

LEXICON ADV-TEMP_LAK
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_LAK ;

LEXICON ADV-TEMP_KIT
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_KIT ;

LEXICON ADV-TEMP_KUDO
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_KUDO ;



! vim: set ft=xfst-lexc:

