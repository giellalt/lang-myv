!! !!!Adverb inflection

!! The Erzya language adjectives do not compare.


! These _states_ can conjugate,
! a distinctive feature
!
LEXICON AdvState-kudo_Sub
+Err/Orth: AdvState-kudo ; 

LEXICON AdvState-kudo
+Adv+State: K ;
+Adv+State+Der/Pr:%> StateConjugation-kudo ;
+Adv+State+Der/MWN: N_КУДО ;



! This is fed by AdvState
LEXICON StateConjugation-kudo
+Ind+Prs+ScSg1:А2н K ;
+Err/Orth+Dial+Ind+Prs+ScSg1:ян K ;
+Ind+Prs+ScSg2:А2т K ;
+Err/Orth+Dial+Ind+Prs+ScSg2:ят K ;
+Ind+Prs+ScSg3: K ;
+Ind+Prt2+ScSg1:линь K ;
+Ind+Prt2+ScSg2:лить K ;
+Ind+Prt2+ScSg3:ль K ;

+Ind+Prs+ScPl1:тА2но K ;
+Ind+Prs+ScPl2:тА2до K ;
+Ind+Prs+ScPl3:тЬ2 K ;
+Ind+Prt2+ScPl1:линек K-lak ;
+Ind+Prt2+ScPl2:лиде K ;
+Ind+Prt2+ScPl3:льть K ;


!LEXICON ABBR-IS_ADV
!+ABBR: ADVTAG ;

LEXICON Ad-A_ ! 
+Adv+AdA: WORD-END ;

LEXICON ADV_ ! 
+Adv: K ;

LEXICON ADV_КУДО ! Coping with stem structure
 ADV_ ;

! истяк 
LEXICON ADV_ЛАК
+Adv: K-lak ;

LEXICON ADV_ОШ
+Adv: K ;

LEXICON ADV_КАЛ
+Adv: K ;

LEXICON ADV-TEMP_IS-TEMP
+Adv+Temp: K ;

LEXICON ADV-TEMP-ITER
+Adv+Temp+Iter: K ;

LEXICON ADV-TEMP_
+Adv+Temp: K ;

LEXICON ADV-SPAT_ !!= @CODE@ пачк
+Adv+Spat: K ;

LEXICON ADV-SPAT-IS-PRL !!= @CODE@ перька
+Adv+Spat+Prl: K ;

LEXICON ADV/PO/PRON-SPAT_АЛО !!= @CODE@ ало:ал
 ADV-SPAT_АЛО ;
+Po+Spat+Abl:%>дО1 K ;
+Po+Spat+Loc:%>О1 K ;
+Po+Spat+Prl:%>Г2а K ;
+Po+Spat+Lat:%>%{ОЕЭØ%}в K ;
+Pron+Spat+Abl:%>дО1 POSSX4-END-FOC-DER/PR-SC3 ;
+Pron+Spat+Loc:%>О1 POSSX4-END-FOC-DER/PR-SC3 ;
+Pron+Spat+Prl:%>Г2а POSSX4-END-FOC-DER/PR-SC3 ;
+Pron+Spat+Lat:%>О1 POSSX4-END-FOC-DER/PR-SC3 ;



LEXICON ADV-SPAT_АЛО !!= @CODE@ ало:ал
+Adv+Spat+Abl:%>дО1 K ;
+Adv+Spat+Loc:%>О1 K ;
+Adv+Spat+Prl:%>Г2а K ;
+Adv+Spat+Orth/PhonDeriv+Loc+Der/це:%>це _КУДО-N2-Def-Declension ;
+Adv+Spat+Der/кс:%>кс N_ОШ ;
+Adv+Spat+Lat:%>%{ОЕЭØ%}в K ;


LEXICON ADV/PO/PRON-SPAT_
!! relative space __мельга__
+Adv+Spat: K ;
+Po+Spat: K ;
+Pron+Spat: POSSX4-END-FOC-DER/PR-SC3 ;

LEXICON PO/PRON-SPAT_
!! relative space __мельга__
+Po+Spat: K ;
+Pron+Spat: POSSX4-END-FOC-DER/PR-SC3 ;



LEXICON Adv_Phrase ! 
+Adv: WORD-END ;

LEXICON ADV-IDEOPH ! 
+Adv-Ideoph: K ;
! сорк, кальцк ; twang, thwang, bang

LEXICON ADV-IDEOPH-IS-INE ! 
+Adv-Ideoph+Ine: K ;

LEXICON Exclusive ! 
+Adv+Exclusive: K ;

LEXICON ADV-PARENTHETIC_ ! 
+Adv+Parenthetic: K ;

LEXICON ADV_-ELA ! 
+Adv+Ela: K ;

LEXICON Adv-Ine ! 
+Adv+Ine: K ;

LEXICON Adv-Tra ! 
+Adv+Tra: K ;

LEXICON ADV_-TRA ! 
+Adv+Tra: K ;

LEXICON MANNER_ ! 
+Adv+Manner: K ;

LEXICON MANNER_-ELA ! 
A-DER/ELA-ADV-MANNER ;

LEXICON A-DER/ELA-ADV-MANNER ! 
+Adv+Manner+Ela: K ;

LEXICON MANNER_-TRA ! 
+Adv+Manner+Tra: K ;

LEXICON Manner-Interr ! 
+Interr+Adv+Manner: K ;

LEXICON	PRED2-OBJ_КУДО !! "апак прядо"
+Pred2: K ;
+Pred2+Der/MWN: N_КУДО ;

LEXICON	PRED2-OBJ_ОШ
+Pred2: K ;
+Pred2+Der/MWN: N_ОШ ;

LEXICON PRED2-SUBJ_КУДО_Sub !!
+Err/Orth: PRED2-SUBJ_КУДО ;
LEXICON PRED2-SUBJ_КУДО !! "стядо"
+Pred2: K ;
+Pred2+Der/MWN: N_КУДО ;
LEXICON PRED2-SUBJ_ОШ
+Pred2: K ;
+Pred2+Der/MWN: N_ОШ ;

LEXICON Pred2 ! "стядо", "апак прядо", "чавонь кедть"
+Pred2: K ;
+Pred2+Der/MWN: N_КУДО ;

!! spatial adverbs 
!! dependent and independent case marking 
LEXICON SPAT-NoArg
+Adv+Spat: K ;

LEXICON ADV-IS-SPAT-ELA
+Adv+Ela: K ;
+Adv+Ela: DER/PR4_OBL ;

LEXICON ADV-IS-SPAT-ILL
+Adv+Ill: K ;

LEXICON ADV-IS-SPAT-INE
+Adv+Ine: K ;
+Adv+Ine: DER/PR4_OBL ;

LEXICON ADV-IS-SPAT-LAT
+Adv+Lat: K ;

LEXICON ADV-IS-SPAT-PRL
+Adv+Prl: K ;
+Adv+Prl: DER/PR4_OBL ;

LEXICON SPAT-Interr
+Interr+Spat: WORD-END ;

LEXICON ADV-INTERR-IS-SPAT-INE
+Adv+Interr+Ine: K ;
+Adv+Interr+Ine: DER/PR4_OBL ;

LEXICON ADV-INTERR-IS-SPAT-ELA
+Adv+Interr+Ela: K ;

LEXICON ADV-INTERR-IS-SPAT-PRL
+Adv+Interr+Prl: K ;

LEXICON Spat_
+Adv+Spat: K ;

LEXICON SPAT-Ela
+Adv+Spat+Ela: K ;
!+Po+Spat+Ela: _КУДО-Possessive-Before-K ;

LEXICON SPAT-Ine
+Spat+Ine: K ;
!+Po+Spat+Ine: _КУДО-Possessive-Before-K ;

LEXICON SPAT-Lat
+Spat+Lat: K ;

LEXICON SPAT-Prl
+Spat+Prl: K ;
!+Po+Spat+Prl: _КУДО-Possessive-Before-K ;

!!Temporal tags with cases
LEXICON TEMP_
+Adv+Temp: K ;

!! This marking would indicate a word form that may be 
!  construed as an adv as well as a adposition.
LEXICON Temp-NoArg
+Adv+Temp: K ;

LEXICON Temp-Coll
+Coll+Adv+Temp: K ;

LEXICON Temp-Distr
+Distr+Adv+Temp: K ;

LEXICON Temp-Ela
+Ela+Adv+Temp: K ;

LEXICON Temp-Ill
+Ill+Adv+Temp: K ;

LEXICON Temp-Indef
+Adv+Temp+InDef: K ;

LEXICON Temp-Interr
+Adv+Temp+Interr: K ;

LEXICON Temp-Lat
+Lat+Adv+Temp: K ;

LEXICON Temp-ms
+Der/мс+Ill+Adv+Temp: K ;

LEXICON Temp-Pl
+Pl+Adv+Temp: K ;

LEXICON Temp-Prl
+Prl+Adv+Temp: K ;

LEXICON ADV-TEMP_ЛАК
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_ЛАК ;

LEXICON ADV-TEMP_КИТ
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_КИТ ;

LEXICON ADV-TEMP_КУДО
+Adv+Temp: K ;
+Adv+Temp+Der/MWN: N_КУДО ;



! vim: set ft=xfst-lexc:

