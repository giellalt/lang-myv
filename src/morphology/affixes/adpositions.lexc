! !!! Postpositions

!! The Erzya language postpositions can be broken into many subgroups
!! according to morphological and semantic criteria


LEXICON PO_ПАНГО-INE3
!! лангсо:ланг
+Po+Ine:%>с%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ine:%>О1%>с%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ela:%>ст%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ela:%>О1%>ст%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ill:%>с K ;
+Po+Ill:%>О1%>с K ;
+Po+Ill:%>%{ОЕЭØ%}з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON PO_OSH-INE4
!!вакссо:вакс
+Po+Ine:%>с%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ine:%>О1%>с%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ela:%>ст%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ela:%>О1%>ст%{оэØ%} PO_POSS-or-END-FOC-DER/COP-SC3 ;
+Po+Ill:%>с K ;
+Po+Ill:%>О1%>с K ;
+Po+Ill:%>%{ОЕЭØ%}з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Prl:%>Г2а PO_POSS-or-END-FOC-DER/COP-SC3 ;

LEXICON PO_KAL-LOC
!! ало:ал
!! alo-SPAT-1Arg
+Po+Spat+Abl:%>дО1 K ;
+Po+Spat+Abl:%>дО1 OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Loc:%>О1 K ; 
+Po+Spat+Loc:%>О1 OBL-PX-FOLLOWED-BY-END-OR-FOC ;
!+Po+Spat+Lat:%>%{ОЕЭØ%}в _KUDO-Possessive-Before-K ;
+Po+Spat+Prl:%>Г2а K ;
+Po+Spat+Prl:%>Г2а OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Orth/PhonDeriv+Loc+Der/це:%>це _KUDO-N2-Def-Declension ;
+Po+Spat+Loc+Der/се:%>се _KUDO-N2-Def-Declension ;
+Po+Spat+Der/кс:%>кс N_OSH ;
+Po+Spat+Lat:%>%{ОЕЭØ%}в K ;
+Po+Spat+Abl:%>дО1 _KUDO-N2-Def-Declension ;
+Po+Spat+Loc+Der/MWN+N:%>О1 _KUDO-N2-Def-Declension ; 
+Po+Spat+Prl+Der/MWN+N:%>Г2а _KUDO-N2-Def-Declension ;

LEXICON PO_POSS-or-END-FOC
!! This allows for possessor indices, word end or focus
 DER/COP4_OBL ;
 K ;


LEXICON PO_POSS-or-END-FOC-DER/COP-SC3
!! This allows for word end, possessor indices, predication
 K ;
 DER/COP4_OBL ; 
 POSSX4-END-FOC-DER/COP-SC3 ;


LEXICON PO-IS-ABL
!! postposition that is in ablative case
!! алдо:алдо
+Po+Abl: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-IS-ELA
!! postposition that is in elative case
!! потсто:потсто
+Po+Ela: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-IS-ILL-STEM
!! postposition that is in illative case
!! эземс:эзем
+Po+Ill:%>О1з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Ill:%>с K ;

LEXICON PO-IS-ILL-STEM_ЭЙС
!! postposition that is in illative case
!! эйс:э
+Po+Ill:йз OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Ill:зэз OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Ill:йс K ;


LEXICON PO-IS-INE
!! postposition that is in inessive case
!! потсо:потсо
+Po+Ine: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-IS-LAT-STEM
!! postposition that is in lative case
!! алов:ало
+Po+Lat: OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Lat:в K ;

LEXICON PO-IS-LOC
!! postposition that is in locative case
!! ало:ало
+Po+Loc: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-IS-PRL
!! postposition that is in prolative case
!! перька:перька
+Po+Prl: PO_POSS-or-END-FOC-DER/COP-SC3;

!!
LEXICON PO-SPAT_
!! postposition that has no continuation пачк
+Po+Spat: K ;

LEXICON PO-SPAT-IS-ABL
!! postposition that is in ablative case
!! алдо:алдо
+Po+Spat+Abl: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-SPAT-IS-ELA
!! postposition that is in elative case
!! потсто:потсто
+Po+Spat+Ela: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-SPAT-IS-ILL-STEM
!! postposition that is in illative case
!! малас:мала
+Po+Spat+Ill:%>О1з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Ill:%>с K ;

LEXICON PO-SPAT-IS-ILL-STEM_ПОТС
!! postposition that is in illative case
!! потс:пот
+Po+Spat+Ill:м%>О1з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Ill:%>с K ;

LEXICON PO-SPAT-IS-ILL-STEM_ЭЙС
!! postposition that is in illative case
!! эйс:э
+Po+Spat+Ill:йз OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Ill:зэз OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Ill:йс K ;


LEXICON PO-SPAT-IS-INE
!! postposition that is in inessive case
!! потсо:потсо
+Po+Spat+Ine: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-SPAT-IS-LAT-STEM
!! postposition that is in lative case
!! алов:ало
+Po+Spat+Lat: OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Spat+Lat:в K ;

LEXICON PO-SPAT-IS-LOC
!! postposition that is in locative case
!! ало:ало
+Po+Spat+Loc: PO_POSS-or-END-FOC-DER/COP-SC3;

LEXICON PO-SPAT-IS-PRL
!! postposition that is in prolative case
!! перька:перька
+Po+Spat+Prl: PO_POSS-or-END-FOC-DER/COP-SC3;


LEXICON PO-TEMP_
+Po+Temp: K ; !!= @CODE@ перть

LEXICON PO-TEMP-IS-ELA
+Po+Ela+Temp: PO_POSS-or-END-FOC ; !!= @CODE@ пингстэ


LEXICON ejs-PO
эйсэ+Po+Ill:эйс K ;
эйсэ+Po+Ine:эйсэ _KUDO-Conjugation ;
эйсэ+Po+Ela:эйстэ _KUDO-Conjugation ;
эйсэ+Po+Ill:эйз OBL-PX-FOLLOWED-BY-END-OR-FOC ;
эйсэ+Po+Ine:эйсэ POSSX4-END-FOC-DER/COP-SC3 ;
эйсэ+Po+Ela:эйстэ POSSX4-END-FOC-DER/COP-SC3 ;
эйсэ+Po+Ela:эйстэде OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON PO_МАРТО
+Po+COM: K ;
+Po+COM: _KUDO-Possessives2 ; 
+Po+Der/MWN: N_KUDO ;

!! Postpositions attr
LEXICON POSTP-IS-ATTR_КОНДЯМО !! кондямо:кондя
+Po+Attr+Dep:мо # ; !A_ПАНГО
+Po+Attr+Der/MWN:м N_ПАНГО-Def-Declension ;
+Po+Attr+Der/MWN:м _ПАНГО-Indef-Declension ;
+Po+Attr+Der/MWN:мо N_ПАНГО-Poss-Declension-CX ;
+Po+Attr+Der/MWN: NMN_DEF-PL-DECLENSION-ТНЭ ;


LEXICON PO_KUDO-ILL
+Po+Ill:%>с K ;
+Po+Ill:%>з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON PO_OSH-ILL
+Po+Ill:%>с K ;
+Po+Ill:%>%{ОЕЭØ%}з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON Postp-karsho
+Po: WORD-END ;
+Po: PossMarker-then-Clt ;
+Po+Ill:%>з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON vaksso-SPAT
 malaso-SPAT ;
+Po+Ela:%>О1%>ст%{оэØ%} _KUDO-Conjugation ;
+Po+Ill:%>О1%>с K ;
+Po+Ine:%>О1%>с%{оэØ%} _KUDO-Conjugation ;
+Po+Ill:%>%{ОЕЭØ%}з OBL-PX-FOLLOWED-BY-END-OR-FOC ;

LEXICON jutkso-SPAT
+Po+Prl:%>О1%>Г2а POSSX4-END-FOC-DER/COP-SC3 ;
+Po+Prl:%>О1%>Г2а _KUDO-Conjugation ;
 langso-SPAT ;

LEXICON langso-SPAT
+Po+Ela:%>ст%{оэØ%} _KUDO-Conjugation ;
+Po+Ill:%>с K ;
+Po+Lat:%>%{ОЕЭØ%}в K ;
+Po+Ine:%>с%{оэØ%} _KUDO-Conjugation ;
+Po+Ela:%>ст%{оэØ%} POSSX4-END-FOC-DER/COP-SC3 ;
+Po+Ill:%>О2з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Ine:%>с%{оэØ%} POSSX4-END-FOC-DER/COP-SC3 ;

LEXICON PO_KUDO-INE5
!! malaso:mala
+Po+Ela:%>ст%{оэØ%} POSSX4-END-FOC-DER/COP-SC3 ;
+Po+Ill:%>О2з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Po+Ine:%>с%{оэØ%} POSSX4-END-FOC-DER/COP-SC3 ;
+Po+Prl:%>Г2а POSSX4-END-FOC-DER/COP-SC3 ;
+Po+Lat:%>в _KUDO-Conjugation ;


! This contlex allows for relational word which, otherwise, are open 
! to extensive declension
LEXICON POSTP_
+Po: K ;

LEXICON Dat-1Arg
+Po+DAT: K ;






! vim: set ft=xfst-lexc:

