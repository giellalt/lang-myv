!! Proper noun inflection

!! Erzya proper nouns inflect in the same cases as regular
!! nouns.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Male given name for deriving patronyms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PropNameMaleDer-J-0Evich
!! Андрей:Андре
+Orth/Colloq:ич PROP_ОШ_PATRMAL ;
:евич PROP_ОШ_PATRMAL ;
:евна PROP_КУДО_PATRFEM ;
:й PROP_КАЛ_MAL ;

LEXICON PropNameMaleDer-IJ-Y0Evich
+Orth/Colloq:ьич PROP_ОШ_PATRMAL ;
:ьевич PROP_ОШ_PATRMAL ;
:ьевна PROP_КУДО_PATRFEM ;
:ий PROP_КАЛ_MAL ;

LEXICON PropNameMaleDer-IJ-I0Evich
+Orth/Colloq:ьич PROP_ОШ_PATRMAL ;
:иевич PROP_ОШ_PATRMAL ;
:иевна PROP_КУДО_PATRFEM ;
:ий PROP_КАЛ_MAL ;

LEXICON PropNameMaleDer-Y-0Evich
+Orth/Colloq:ич PROP_ОШ_PATRMAL ;
:евич PROP_ОШ_PATRMAL ;
:евна PROP_КУДО_PATRFEM ;
:ь PROP_КАЛ_MAL ;

LEXICON PropNameMaleDer-I-YEvich
!! Вили:Вил
+Orth/Colloq:ич PROP_ОШ_PATRMAL ;
:ьевич PROP_ОШ_PATRMAL ;
:ьевна PROP_КУДО_PATRFEM ;
:и PROP_КАЛ_MAL ;


LEXICON PropNameMaleDer-Ovich
+Orth/Colloq:ыч PROP_ОШ_PATRMAL ;
:ович PROP_ОШ_PATRMAL ;
:овна PROP_КУДО_PATRFEM ;
 PROP_КАЛ_MAL ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Russian type Surnames 
!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Deriv-RUS-В_SURMAL
!! Абдеев:Абдеев
: PROP_КАЛ_SURMAL ;
:а PROP_КУДО_SURFEM ;

LEXICON Deriv-RUS-ИЙ_SURMAL
!! Багрий:Багр
:ий PROP_КАЛ_SURMAL ;
:яя PROP_КУДО_SURFEM ;

LEXICON Deriv-RUS-КИЙ_SURMAL
!! Аморский:Аморск
:ий PROP_КАЛ_SURMAL ;
:ая PROP_КУДО_SURFEM ;

LEXICON Deriv-RUS-ОЙ_SURMAL
:ой PROP_КАЛ_SURMAL ;
:ая PROP_КУДО_SURFEM ;

LEXICON Deriv-RUS-ЫЙ_SURMAL
:ый PROP_КАЛ_SURMAL ;
:ая PROP_КУДО_SURFEM ;

LEXICON Deriv-RUS-АН_SURMAL
 Deriv-RUS-В_SURMAL ;

LEXICON Deriv-RUS-ИН_SURMAL
 Deriv-RUS-В_SURMAL ;


LEXICON PROP_КАЛ_SURMAL
+N+Prop+Sem/Sur+Sem/Mal: N_КАЛ-Derivation ;
+N+Prop+Sem/Sur+Sem/Mal: N_КАЛ-Def-Declension ;
+N+Prop+Sem/Sur+Sem/Mal: _КАЛ-Indef-Declension ;
+N+Prop+Sem/Sur+Sem/Mal: N_КАЛ-Poss-Declension-CX ;

LEXICON PROP_КУДО_SURFEM
+N+Prop+Sem/Sur+Sem/Fem: N_КУДО-Derivation ;
+N+Prop+Sem/Sur+Sem/Fem: NMN_DEF-DECLENSION ;
+N+Prop+Sem/Sur+Sem/Fem: _КУДО-Indef-Declension ;
+N+Prop+Sem/Sur+Sem/Fem: N_КУДО-Poss-Declension-CX ;

LEXICON CYRL-CONS_SUR
 PROP_КАЛ_SUR ;

LEXICON PROP_КАЛ_SUR
+N+Prop+Sem/Sur: N_КАЛ-Derivation ;
+N+Prop+Sem/Sur: N_КАЛ-Def-Declension ;
+N+Prop+Sem/Sur: _КАЛ-Indef-Declension ;
+N+Prop+Sem/Sur: N_КАЛ-Poss-Declension-CX ;


LEXICON CYRL-Т_SUR
 PROP_КИТ_SUR ;

LEXICON PROP_КИТ_SUR
 PROP_КАЛ_SUR ;

LEXICON CYRL-Л_SUR
 PROP_КАЛ_SUR ;

LEXICON CYRL-К_SUR
 PROP_ЛАК_SUR ;

LEXICON PROP_ЛАК_SUR
 PROP_КАЛ_SUR ;

LEXICON CYRL-SIBILANT_SUR
 PROP_ОШ_SUR ;

LEXICON PROP_ОШ_SUR
+N+Prop+Sem/Sur: NMN_ОШ ;

LEXICON CYRL-VOW_SUR
 PROP_КУДО_SUR ;

LEXICON CYRL-А_SUR
 PROP_КУДО_SUR ;

LEXICON PROP_КУДО_SUR
+N+Prop+Sem/Sur: NMN_КУДО ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Erzya Homestead names !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_HOMESTEAD
+N+Prop+Sem/Plc: NMN_КАЛ ;

LEXICON PROP_ОШ_HOMESTEAD
+N+Prop+Sem/Plc: NMN_ОШ ;

LEXICON PROP_КУДО_HOMESTEAD
+N+Prop+Sem/Plc: NMN_КУДО ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! PLACE NAMES FROM TEMPLATES !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP-PLC_КАЛ
+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КАЛ ;

LEXICON PROP-PLC_КИТ
+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КИТ ;

LEXICON PROP-PLC_ОШ
+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_ОШ ;

LEXICON PROP-PLC_КУДО
+N+Prop+Sem/Plc:%> LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КУДО ;

!!!!!!!!!!!!!!!!!!!!!!!
!! Erzya settlements !!
!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP-SETTLEMENT_КАЛ
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
!+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КАЛ ;

LEXICON PROP-SETTLEMENT_КИТ
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КИТ ;

LEXICON PROP-SETTLEMENT_ОШ
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_ОШ ;

LEXICON PROP-SETTLEMENT_ПАНГО
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_ПАНГО ;

LEXICON PROP-SETTLEMENT_КУДО
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_КУДО ;

!!!!!!!!!!!!!!!!
!! Divinities !!
!!!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_КАЛ ;

LEXICON PROP_ОШ_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_ОШ ;

LEXICON PROP_ПАНГО_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_ПАНГО ;

LEXICON PROP_КУДО_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_КУДО ;

!!!!!!!!!!!!!!!!!!!!
!! Constellations !!
!!!!!!!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_КАЛ ;

LEXICON PROP_ОШ_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_ОШ ;

LEXICON PROP_ОШ-PL_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc+Pl: NMN_ОШ-PL ;

LEXICON PROP_ПАНГО_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_ПАНГО ;

LEXICON PROP_КУДО_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_КУДО ;

LEXICON PROP_КУДО-PL_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc+Pl: NMN_КУДО-PL ; 

LEXICON PROP_ТЕШТЕ_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_ТЕШТЕ ;


!!!!!!!!!!!!!!!!
!! TEXT NAMES !!
!!!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_TXT
+N+Prop+Sem/Txt: NMN_КАЛ ;

LEXICON PROP_ОШ_TXT
+N+Prop+Sem/Txt: NMN_ОШ ;

LEXICON PROP_КУДО_TXT
+N+Prop+Sem/Txt: NMN_КУДО ;

!!!!!!!!!!!!!!
!! HOLIDAYS !!
!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_HOLIDAY
+N+Prop+Event: NMN_КАЛ ;

LEXICON PROP_ОШ_HOLIDAY
+N+Prop+Event: NMN_ОШ ;

LEXICON PROP_КУДО_HOLIDAY
+N+Prop+Event: NMN_КУДО ;


!!!!!!!!!!!!!!!!!
!! RIVER NAMES !!
!!!!!!!!!!!!!!!!!
LEXICON PROP_КАЛ_RIVERS
+N+Prop+Rvr: NMN_КАЛ ;

LEXICON PROP_ОШ_RIVERS
+N+Prop+Rvr: NMN_ОШ ;

LEXICON PROP_КУДО_RIVERS
+N+Prop+Rvr: NMN_КУДО ;


LEXICON PROP_КАЛ
+N+Prop: NMN_КАЛ ;

LEXICON PROP_ЛАК
+N+Prop: NMN_ЛАК ;

LEXICON PROP_КИТ
+N+Prop: N_КИТ-Derivation ;
+N+Prop+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;
+N+Prop: N_КИТ-Def-Declension ;
+N+Prop: _КИТ-Indef-Declension ;
+N+Prop: N_КАЛ-Poss-Declension-CX ;

LEXICON PROP_ОШ
+N+Prop: N_ОШ-Derivation ;
+N+Prop: N_ОШ-Def-Declension ;
+N+Prop: _ОШ-Indef-Declension ;
+N+Prop: N_ОШ-Poss-Declension-CX ;

LEXICON PROP_ТЕШТЕ
 N_ПАНГО ;

LEXICON PROP_ТИНГЕ
 N_ПАНГО ;

LEXICON PROP_ПАНГО
+N+Prop: N_ПАНГО-Derivation ;
+N+Prop: N_ПАНГО-Def-Declension ;
+N+Prop: _ПАНГО-Indef-Declension ;
+N+Prop: N_ПАНГО-Poss-Declension-CX ;

LEXICON PROP_ВЕЛЕ_IS-НЕ-DIM
+N+Prop+Der/НЕ+Dimin+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop+Der/НЕ+Dimin+N+Prop: _КУДО-Indef-Declension ;
+N+Prop+Der/НЕ+Dimin+N+Prop: N_КУДО-Poss-Declension-CX ;
 
LEXICON PROP_ВЕЛЕ
!! Front-vowel stem
+N+Prop: N_КУДО-Derivation ;
+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop: _КУДО-Indef-Declension ;
+N+Prop: N_КУДО-Poss-Declension-CX ;

LEXICON PROP_КУДО
+N+Prop: N_КУДО-Derivation ;
+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop: _КУДО-Indef-Declension ;
+N+Prop: N_КУДО-Poss-Declension-CX ;


LEXICON PROP_КАЛ_MAL
+N+Prop+Sem/Mal: NMN_КАЛ ;

LEXICON PROP_ЛАК_MAL
+N+Prop+Sem/Mal: NMN_ЛАК ;

LEXICON PROP_КИТ_MAL
+N+Prop+Sem/Mal: NMN_КИТ ;
+N+Prop+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;

LEXICON PROP_ОШ_MAL
+N+Prop+Sem/Mal: NMN_ОШ ;

LEXICON PROP_ТЕШТЕ_MAL
 PROP_ПАНГО_MAL ;

LEXICON PROP_ТИНГЕ_MAL
 PROP_ПАНГО_MAL ;

LEXICON PROP_ПАНГО_MAL
+N+Prop+Sem/Mal: NMN_ПАНГО ;

LEXICON PROP_КУДО_MAL
+N+Prop+Sem/Mal: NMN_КУДО ;

LEXICON PROP_КАЛ_FEM
+N+Prop+Sem/Fem: NMN_КАЛ ;

LEXICON PROP_ЛАК_FEM
+N+Prop+Sem/Fem: NMN_ЛАК ;

LEXICON PROP_КИТ_FEM
+N+Prop+Sem/Fem: NMN_КИТ ;
+N+Prop+Sem/Fem+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;

LEXICON PROP_ОШ_FEM
+N+Prop+Sem/Fem: NMN_ОШ ;

LEXICON PROP_ТЕШТЕ_FEM
 PROP_ПАНГО_FEM ;

LEXICON PROP_ТИНГЕ_FEM
 PROP_ПАНГО_FEM ;

LEXICON PROP_ПАНГО_FEM
+N+Prop+Sem/Fem: NMN_ПАНГО ;

LEXICON PROP_КУДО_FEM
+N+Prop+Sem/Fem: NMN_КУДО ;


LEXICON PROP_ОШ_PATRMAL
+N+Prop+Sem/Patr+Sem/Mal: N_ОШ-Derivation ;
+N+Prop+Sem/Patr+Sem/Mal: N_ОШ-Def-Declension ;
+N+Prop+Sem/Patr+Sem/Mal: _ОШ-Indef-Declension ;
+N+Prop+Sem/Patr+Sem/Mal: N_ОШ-Poss-Declension-CX ;

LEXICON PROP_КУДО_PATRFEM
+N+Prop+Sem/Patr+Sem/Fem: N_КУДО-Derivation ;
+N+Prop+Sem/Patr+Sem/Fem: NMN_DEF-DECLENSION ;
+N+Prop+Sem/Patr+Sem/Fem: _КУДО-Indef-Declension ;
+N+Prop+Sem/Patr+Sem/Fem: N_КУДО-Poss-Declension-CX ;

!! DECLENSION LIMITATIONS

LEXICON PROP_КУДО-PL
+N+Prop: NMN_DEF-PL-DECLENSION ;
+N+Prop: _КУДО-Indef-Declension ;
+N+Prop: N_КУДО-Poss-Declension-CX-PL ;

LEXICON PROP_КУДО_SUB
+Err/Orth: PROP_КУДО ;

LEXICON PROP_КУДО_SH
+Use/Short: PROP_КУДО ;


! vim: set ft=xfst-lexc:

