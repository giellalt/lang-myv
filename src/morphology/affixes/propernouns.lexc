!! Proper noun inflection

!! Erzya proper nouns inflect in the same cases as regular
!! nouns.

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Male given name for deriving patronyms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PropNameMaleDer-J-0Evich
!! Андрей:Андре
+Orth/Colloq:ич PROP_OSH_PATRMAL ;
:евич PROP_OSH_PATRMAL ;
:евна PROP_KUDO_PATRFEM ;
:й PROP_KAL_MAL ;

LEXICON PropNameMaleDer-IJ-Y0Evich
+Orth/Colloq:ьич PROP_OSH_PATRMAL ;
:ьевич PROP_OSH_PATRMAL ;
:ьевна PROP_KUDO_PATRFEM ;
:ий PROP_KAL_MAL ;

LEXICON PropNameMaleDer-IJ-I0Evich
+Orth/Colloq:ьич PROP_OSH_PATRMAL ;
:иевич PROP_OSH_PATRMAL ;
:иевна PROP_KUDO_PATRFEM ;
:ий PROP_KAL_MAL ;

LEXICON PropNameMaleDer-Y-0Evich
+Orth/Colloq:ич PROP_OSH_PATRMAL ;
:евич PROP_OSH_PATRMAL ;
:евна PROP_KUDO_PATRFEM ;
:ь PROP_KAL_MAL ;

LEXICON PropNameMaleDer-I-YEvich
!! Вили:Вил
+Orth/Colloq:ич PROP_OSH_PATRMAL ;
:ьевич PROP_OSH_PATRMAL ;
:ьевна PROP_KUDO_PATRFEM ;
:и PROP_KAL_MAL ;


LEXICON PropNameMaleDer-Ovich
+Orth/Colloq:ыч PROP_OSH_PATRMAL ;
:ович PROP_OSH_PATRMAL ;
:овна PROP_KUDO_PATRFEM ;
 PROP_KAL_MAL ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Russian type Surnames 
!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Deriv-RUS-V_SURMAL
!! Абдеев:Абдеев
: PROP_KAL_SURMAL ;
:а PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-IJ_SURMAL
!! Багрий:Багр
:ий PROP_KAL_SURMAL ;
:яя PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-KIJ_SURMAL
!! Аморский:Аморск
:ий PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-OJ_SURMAL
:ой PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-YJ_SURMAL
:ый PROP_KAL_SURMAL ;
:ая PROP_KUDO_SURFEM ;

LEXICON Deriv-RUS-AN_SURMAL
 Deriv-RUS-V_SURMAL ;

LEXICON Deriv-RUS-IN_SURMAL
 Deriv-RUS-V_SURMAL ;


LEXICON PROP_KAL_SURMAL
+Sem/Sur_Mal+N+Prop: N_KAL-Derivation ;
+Sem/Sur_Mal+N+Prop: N_KAL-Def-Declension ;
+Sem/Sur_Mal+N+Prop: _KAL-Indef-Declension ;
+Sem/Sur_Mal+N+Prop: N_KAL-Poss-Declension-CX ;

LEXICON PROP_KUDO_SURFEM
+Sem/Sur_Fem+N+Prop: N_KUDO-Derivation ;
+Sem/Sur_Fem+N+Prop: NMN_DEF-DECLENSION ;
+Sem/Sur_Fem+N+Prop: _KUDO-Indef-Declension ;
+Sem/Sur_Fem+N+Prop: N_KUDO-Poss-Declension-CX ;

LEXICON CYRL-CONS_SUR
 PROP_KAL_SUR ;

LEXICON PROP_KAL_SUR
+N+Prop+Sem/Sur: N_KAL-Derivation ;
+N+Prop+Sem/Sur: N_KAL-Def-Declension ;
+N+Prop+Sem/Sur: _KAL-Indef-Declension ;
+N+Prop+Sem/Sur: N_KAL-Poss-Declension-CX ;


LEXICON CYRL-T_SUR
 PROP_KIT_SUR ;

LEXICON PROP_KIT_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-L_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-K_SUR
 PROP_LAK_SUR ;

LEXICON PROP_LAK_SUR
 PROP_KAL_SUR ;

LEXICON CYRL-SIBILANT_SUR
 PROP_OSH_SUR ;

LEXICON PROP_OSH_SUR
+N+Prop+Sem/Sur: NMN_OSH ;

LEXICON CYRL-VOW_SUR
 PROP_KUDO_SUR ;

LEXICON CYRL-A_SUR
 PROP_KUDO_SUR ;

LEXICON PROP_KUDO_SUR
+N+Prop+Sem/Sur: NMN_KUDO ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Erzya Homestead names !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP_KAL_HOMESTEAD
+N+Prop+Sem/Plc: NMN_KAL ;

LEXICON PROP_OSH_HOMESTEAD
+N+Prop+Sem/Plc: NMN_OSH ;

LEXICON PROP_KUDO_HOMESTEAD
+N+Prop+Sem/Plc: NMN_KUDO ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! PLACE NAMES FROM TEMPLATES !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP-PLC_KAL
+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KAL ;

LEXICON PROP-PLC_KIT
+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KIT ;

!LEXICON PROP-PLC_OSH
!+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
!+N+Prop+Sem/Plc: NMN_OSH ;

LEXICON PROP-PLC_KUDO
+N+Prop+Sem/Plc:%> LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KUDO ;

!!!!!!!!!!!!!!!!!!!!!!!
!! Erzya settlements !!
!!!!!!!!!!!!!!!!!!!!!!!
LEXICON PROP-SETTLEMENT_KAL
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
!+N+Prop+Sem/Plc:%>О1 LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KAL ;

LEXICON PROP-SETTLEMENT_KIT
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KIT ;

LEXICON PROP-SETTLEMENT_OSH
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_OSH ;

LEXICON PROP-SETTLEMENT_PANGO
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_PANGO ;

LEXICON PROP-SETTLEMENT_KUDO
+N+Prop+Sem/Plc@U.DECL-NX.SP@:000@U.DECL-NX.SP@ LAT-INDEF ;
+N+Prop+Sem/Plc: NMN_KUDO ;

!!!!!!!!!!!!!!!!
!! Divinities !!
!!!!!!!!!!!!!!!!
LEXICON PROP_KAL_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_KAL ;

LEXICON PROP_OSH_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_OSH ;

LEXICON PROP_PANGO_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_PANGO ;

LEXICON PROP_KUDO_DIVINITY
+N+Prop+Sem/Hum+Sem/Divinity: NMN_KUDO ;

!!!!!!!!!!!!!!!!!!!!
!! Constellations !!
!!!!!!!!!!!!!!!!!!!!
LEXICON PROP_KAL_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_KAL ;

LEXICON PROP_OSH_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_OSH ;

LEXICON PROP_OSH-PL_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc+Pl: NMN_OSH-PL ;

LEXICON PROP_PANGO_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_PANGO ;

LEXICON PROP_KUDO_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_KUDO ;

LEXICON PROP_KUDO-PL_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc+Pl: NMN_KUDO-PL ; 

LEXICON PROP_TESHTE_CONSTELLATION
+N+Prop+Sem/Constellation+Sem/Plc: NMN_TESHTE ;


!!!!!!!!!!!!!!!!
!! TEXT NAMES !!
!!!!!!!!!!!!!!!!
!LEXICON PROP_KAL_TXT
!+N+Prop+Sem/Txt: NMN_KAL ;

!LEXICON PROP_OSH_TXT
!+N+Prop+Sem/Txt: NMN_OSH ;

LEXICON PROP_KUDO_TXT
+N+Prop+Sem/Txt: NMN_KUDO ;

!!!!!!!!!!!!!!
!! HOLIDAYS !!
!!!!!!!!!!!!!!
!LEXICON PROP_KAL_HOLIDAY
!+N+Prop+Event: NMN_KAL ;

LEXICON PROP_OSH_HOLIDAY
+N+Prop+Event: NMN_OSH ;

LEXICON PROP_KUDO_HOLIDAY
+N+Prop+Event: NMN_KUDO ;


!!!!!!!!!!!!!!!!!
!! RIVER NAMES !!
!!!!!!!!!!!!!!!!!
LEXICON PROP_KAL_RIVERS
+N+Prop+Rvr: NMN_KAL ;

!LEXICON PROP_OSH_RIVERS
!+N+Prop+Rvr: NMN_OSH ;

LEXICON PROP_KUDO_RIVERS
+N+Prop+Rvr: NMN_KUDO ;


LEXICON PROP_KAL
+N+Prop: NMN_KAL ;

LEXICON PROP_LAK
+N+Prop: NMN_LAK ;

LEXICON PROP_KIT
+N+Prop: N_KIT-Derivation ;
+N+Prop+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;
+N+Prop: N_KIT-Def-Declension ;
+N+Prop: _KIT-Indef-Declension ;
+N+Prop: N_KAL-Poss-Declension-CX ;

LEXICON PROP_OSH
+N+Prop: N_OSH-Derivation ;
+N+Prop: N_OSH-Def-Declension ;
+N+Prop: _OSH-Indef-Declension ;
+N+Prop: N_OSH-Poss-Declension-CX ;

LEXICON PROP_TESHTE
 N_PANGO ;

!LEXICON PROP_TINGE
! N_PANGO ;

LEXICON PROP_PANGO
+N+Prop: N_PANGO-Derivation ;
+N+Prop: N_PANGO-Def-Declension ;
+N+Prop: _PANGO-Indef-Declension ;
+N+Prop: N_PANGO-Poss-Declension-CX ;

LEXICON PROP_VELE_IS-NE-DIM
+N+Prop+Der+Der/NJE+Dimin+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop+Der+Der/NJE+Dimin+N+Prop: _KUDO-Indef-Declension ;
+N+Prop+Der+Der/NJE+Dimin+N+Prop: N_KUDO-Poss-Declension-CX ;
 
LEXICON PROP_VELE
!! Front-vowel stem
+N+Prop: N_KUDO-Derivation ;
+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop: _KUDO-Indef-Declension ;
+N+Prop: N_KUDO-Poss-Declension-CX ;

LEXICON PROP_KUDO
+N+Prop: N_KUDO-Derivation ;
+N+Prop: NMN_DEF-DECLENSION ;
+N+Prop: _KUDO-Indef-Declension ;
+N+Prop: N_KUDO-Poss-Declension-CX ;


LEXICON PROP_KAL_MAL
+Sem/Ant_Mal+N+Prop: NMN_KAL ;

LEXICON PROP_LAK_MAL
+Sem/Ant_Mal+N+Prop: NMN_LAK ;

LEXICON PROP_KIT_MAL
+Sem/Ant_Mal+N+Prop: NMN_KIT ;
+N+Prop+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;

LEXICON PROP_OSH_MAL
+Sem/Ant_Mal+N+Prop: NMN_OSH ;

!LEXICON PROP_TESHTE_MAL
! PROP_PANGO_MAL ;

!LEXICON PROP_TINGE_MAL
! PROP_PANGO_MAL ;

!LEXICON PROP_PANGO_MAL
!+Sem/Ant_Mal+N+Prop: NMN_PANGO ;

LEXICON PROP_KUDO_MAL
+Sem/Ant_Mal+N+Prop: NMN_KUDO ;

LEXICON PROP_KAL_FEM
+Sem/Ant_Fem+N+Prop: NMN_KAL ;

!LEXICON PROP_LAK_FEM
!+Sem/Ant_Fem+N+Prop: NMN_LAK ;

LEXICON PROP_KIT_FEM
+Sem/Ant_Fem+N+Prop: NMN_KIT ;
+Sem/Ant_Fem+N+Prop+Err/Orth+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;

LEXICON PROP_OSH_FEM
+Sem/Ant_Fem+N+Prop: NMN_OSH ;

!LEXICON PROP_TESHTE_FEM
! PROP_PANGO_FEM ;

!LEXICON PROP_TINGE_FEM
! PROP_PANGO_FEM ;

!LEXICON PROP_PANGO_FEM
!+Sem/Ant_Fem+N+Prop: NMN_PANGO ;

LEXICON PROP_KUDO_FEM
+Sem/Ant_Fem+N+Prop: NMN_KUDO ;


LEXICON PROP_OSH_PATRMAL
+Sem/Patr_Mal+N+Prop: N_OSH-Derivation ;
+Sem/Patr_Mal+N+Prop: N_OSH-Def-Declension ;
+Sem/Patr_Mal+N+Prop: _OSH-Indef-Declension ;
+Sem/Patr_Mal+N+Prop: N_OSH-Poss-Declension-CX ;

LEXICON PROP_KUDO_PATRFEM
+Sem/Patr_Fem+N+Prop: N_KUDO-Derivation ;
+Sem/Patr_Fem+N+Prop: NMN_DEF-DECLENSION ;
+Sem/Patr_Fem+N+Prop: _KUDO-Indef-Declension ;
+Sem/Patr_Fem+N+Prop: N_KUDO-Poss-Declension-CX ;

LEXICON PROP_NIZE
+N+Prop+Sg+Nom+PxSg3:%>зэ K ;
+N+Prop+Sg+Gen+PxSg3:%>нзэ K ;
+N+Prop+Sg+Dat+PxSg3:%>нстэнь K ;
+N+Prop+Sg+Nom+Def:%>сь K ;
+N+Prop+Sg+Gen+Def:%>нть K ;
+N+Prop+Sg+Dat+Def:%>нтень K ;

!! DECLENSION LIMITATIONS

LEXICON PROP_KUDO-PL
+N+Prop: NMN_DEF-PL-DECLENSION ;
+N+Prop: _KUDO-Indef-Declension ;
+N+Prop: N_KUDO-Poss-Declension-CX-PL ;

!LEXICON PROP_KUDO_SUB
!+Err/Orth: PROP_KUDO ;

!LEXICON PROP_KUDO_SH
!+Use/Short: PROP_KUDO ;


! vim: set ft=xfst-lexc:

