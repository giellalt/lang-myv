!! !!!Noun inflection

!! Nouns in ERZYA inflect for number, case and declension (definite, indefinite and possessive).


!! !!KINSHIP 

LEXICON N-KIN_КАЛ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КАЛ ;
 N-KIN ;

LEXICON N-KIN_ЛАК
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_ЛАК ;
 N-KIN ;

LEXICON N-KIN_КИТ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КИТ ;
 N-KIN ;

LEXICON N-KIN_ОШ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_ОШ ;
 N-KIN ;

LEXICON N-KIN_КЛАСС
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КЛАСС ;
 N-KIN ;

LEXICON N-KIN_ПАНГО
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_ПАНГО ;
 N-KIN ;

LEXICON N-KIN_ТИНГЕ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_ТИНГЕ ;
 N-KIN ;
LEXICON N-KIN_ВЕЛЕ_IS-КЕ-DIM
+N+Sem/Kin+Der/ке+Dimin+N@P.POS.N@:0000@P.POS.N@ N-KIN_ВЕЛЕ ;
LEXICON N-KIN_ВЕЛЕ_IS-НЕ-DIM
+N+Sem/Kin+Der/НЕ+Dimin+N@P.POS.N@:0000@P.POS.N@ N-KIN_ВЕЛЕ ;
LEXICON N-KIN_ВЕЛЕ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КУДО ;
 N-KIN ;
LEXICON N-KIN_ПИЗЭ_IS-НЕ-DIM
+N+Sem/Kin+Der/НЕ+Dimin+N@P.POS.N@:0000@P.POS.N@ N-KIN_ПИЗЭ ;
LEXICON N-KIN_ПИЗЭ
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КУДО ;
 N-KIN ;

LEXICON N-KIN_КУДО
+N+Sem/Kin@P.POS.N@:000@P.POS.N@ NMN_КУДО ;
 N-KIN ;


!! !!HUMAN 

LEXICON N-HUM_КАЛ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КАЛ ;

LEXICON N-HUM_ЛАК
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_ЛАК ;

LEXICON N-HUM_КИТ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КИТ ;

LEXICON N-HUM_ОШ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_ОШ ;

LEXICON N-HUM_КЛАСС
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КЛАСС ;

LEXICON N-HUM_ПАНГО
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_ПАНГО ;

LEXICON N-HUM_ТИНГЕ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_ТИНГЕ ;

LEXICON N-HUM_ТЕШТЕ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_ТЕШТЕ ;

LEXICON N-HUM_ВЕЛЕ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КУДО ;
LEXICON N-HUM_ПИЗЭ
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КУДО ;
LEXICON N-HUM_КУДО
+N+Sem/Hum@P.POS.N@:00@P.POS.N@ NMN_КУДО ;

!! !!PLACE 
LEXICON N_КАЛ_PLC
N-PLC_КАЛ ;
LEXICON N-PLC_КАЛ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КАЛ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ЛАК_PLC
N-PLC_ЛАК ;
LEXICON N-PLC_ЛАК
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_ЛАК ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КИТ_PLC
N-PLC_КИТ ;
LEXICON N-PLC_КИТ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КИТ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ОШ_PLC
N-PLC_ОШ ;
LEXICON N-PLC_ОШ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_ОШ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;
!€# ошов +N+Sem/Plc+SP+Lat+Indef: 
!€# ош%>%{ОЕЭØ%}в
!€0 ош0ов

LEXICON N_КЛАСС_PLC
N-PLC_КЛАСС ;
LEXICON N-PLC_КЛАСС
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КЛАСС ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ПАНГО_PLC
N-PLC_ПАНГО ;
LEXICON N-PLC_ПАНГО
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_ПАНГО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ТИНГЕ_PLC
N-PLC_ТИНГЕ ;
LEXICON N-PLC_ТИНГЕ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_ТИНГЕ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КИ_PLC
N-PLC_КИ ;
LEXICON N-PLC_КИ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КИ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ВЕЛЕ_PLC
N-PLC_ВЕЛЕ ;
LEXICON N-PLC_ВЕЛЕ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КУДО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ПИЗЭ_PLC
N-PLC_ПИЗЭ ;
LEXICON N-PLC_ПИЗЭ
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КУДО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КУДО_PLC
N-PLC_КУДО ;
LEXICON N-PLC_КУДО
+N+Sem/Plc@P.POS.N@:00@P.POS.N@ NMN_КУДО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

!! !!LATIVE 
LEXICON N_КАЛ-LAT
 N_КАЛ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ЛАК-LAT
 N_ЛАК ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КИТ-LAT
 N_КИТ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ОШ-LAT_SUB
+Err/Sub:	N_ОШ-LAT ;
LEXICON N_ОШ-LAT
 N_ОШ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КЛАСС-LAT
 N_КЛАСС ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ПАНГО-LAT
 N_ПАНГО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ТИНГЕ-LAT
 N_ТИНГЕ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_КИ-LAT
 N_КИ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

LEXICON N_ВЕЛЕ-LAT
 N_ВЕЛЕ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;
LEXICON N_ПИЗЭ-LAT
 N_ПИЗЭ ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;
LEXICON N_КУДО-LAT
 N_КУДО ;
+N+Sem/Plc@P.POS.N@@U.DECL-NX.SP@:00@P.POS.N@@U.DECL-NX.SP@ LAT-INDEF ;

!! !!VOCATIVE 
LEXICON N_КАЙ_HUMVOC
+N+Sem/Hum+Der/кай+Voc@P.POS.N@:0000@P.POS.N@ # ;

LEXICON N_АЙ_HUMVOC
+N+Sem/Hum+Der/ай+Voc@P.POS.N@:0000@P.POS.N@ # ;

LEXICON N_HUMVOC
+N+Sem/Hum+Voc@P.POS.N@:000@P.POS.N@ # ;

LEXICON N_КАЙ_KINVOC
+N+Sem/Kin+Der/кай+Voc@P.POS.N@:0000@P.POS.N@ # ;

LEXICON N_АЙ_KINVOC
+N+Sem/Kin+Der/ай+Voc@P.POS.N@:0000@P.POS.N@ # ;

LEXICON N_KINVOC
+N+Sem/Kin+Voc@P.POS.N@:000@P.POS.N@ # ;

!! !!NAMES OF MONTHS
LEXICON N-MONTHS_КАЛ_SUB
+N+Sem/Mnth+Err/Sub: NMN_КАЛ ;

LEXICON N-MONTHS_КАЛ
+N+Sem/Mnth: NMN_КАЛ ;

!! !!COMMON NOUNS 

LEXICON N_КАЛ
+N@P.POS.N@:0@P.POS.N@ NMN_КАЛ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Н1е0@P.POS.N@ NMN_КУДО ;

LEXICON N_КАЛ_SUB
+Err/Sub: N_КАЛ ;

LEXICON N_КАЛ-PL
+N@P.POS.N@:0@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _КАЛ-Indef-Declension-PL ;
+N@P.POS.N@:0@P.POS.N@ N_КАЛ-Poss-Declension-CX-PL ;

LEXICON N_КАЛ-PL_SUB
+Err/Sub: N_КАЛ-PL ;

LEXICON N_ЛАК
+N@P.POS.N@:0@P.POS.N@ NMN_ЛАК ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Н1е0@P.POS.N@ NMN_КУДО ;

LEXICON N_ЛАК-PL
+N@P.POS.N@:0@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _КАЛ-Indef-Declension-PL ;
+N@P.POS.N@:0@P.POS.N@ N_КАЛ-Poss-Declension-CX-PL ;

LEXICON N_КИТ
+N@P.POS.N@:0@P.POS.N@ NMN_КИТ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Н1е0@P.POS.N@ NMN_КУДО ;

LEXICON N_КИТ-PL
!2013-03-15!+Err/Sub+Orth/PhonInfl+N:%>TLoss NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _КИТ-Indef-Declension ;
+N@P.POS.N@:0@P.POS.N@ N_КАЛ-Poss-Declension-CX-PL ;

LEXICON N_КЛАСС
+N@P.POS.N@:0@P.POS.N@ NMN_КЛАСС ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Н1е0@P.POS.N@ NMN_КУДО ;

LEXICON N_ОШ_SUB
+Err/Sub: N_ОШ ;

LEXICON N_ОШ
+N@P.POS.N@:0@P.POS.N@ NMN_ОШ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Н1е0@P.POS.N@ NMN_КУДО ;

LEXICON N_ОШ-PL
+N@P.POS.N@:0@P.POS.N@ N_ОШ-Derivation ;
+N@P.POS.N@:0@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _ОШ-Indef-Declension-PL ;
+N@P.POS.N@:0@P.POS.N@ N_ОШ-Poss-Declension-CX-PL ;

LEXICON N_ИЕ
:е N_ВЕЛЕ ;
+N+Err/Sub+Pl+Nom+Indef:йть K ;
+N+Err/Sub@P.POS.N@:й0@P.POS.N@ NMN_DEF-PL-DECLENSION ;

LEXICON N_ТЕШТЕ
+N@P.POS.N@:0@P.POS.N@ NMN_ТЕШТЕ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>ке0@P.POS.N@ NMN_КУДО ;

LEXICON N_ТИНГЕ
+N@P.POS.N@:0@P.POS.N@ NMN_ТИНГЕ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON N_ТИНГЕ-PL
+N@P.POS.N@:0@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N0@P.POS.N@:%>О1@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _ПАНГО-Indef-Declension-PL ;
+N@P.POS.N@:0@P.POS.N@ N_КУДО-Poss-Declension-CX-PL ;

LEXICON N_ПАНГО_SUB
+Err/Sub: N_ПАНГО ;

LEXICON N_ПАНГО
!! панго:панг
+N@P.POS.N@:0@P.POS.N@ NMN_ПАНГО ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON DV-N_
+V+Der/ОмА: N_КУДО ;

! This might be moved to exceptions 2012-11-21
LEXICON N_КИ
+N@P.POS.N@:0@P.POS.N@ NMN_КИ ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON N_ВЕЛЕ_IS-КЕ-DIM
!! Front vowel, non-palatal consonant before vowel
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
LEXICON N_ВЕЛЕ_IS-НЕ-DIM
!! Front vowel, non-palatal consonant before vowel
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
LEXICON N_ВЕЛЕ
!! Front vowel, non-palatal consonant before vowel
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON N_ПИЗЭ_IS-НЕ-DIM
!! Front vowel, non-palatal consonant before vowel
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
LEXICON N_ПИЗЭ
!! Front vowel, non-palatal consonant before vowel
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON N_КУДО_SUB
+Err/Sub: N_КУДО ;

LEXICON N_КУДО
+N@P.POS.N@:0@P.POS.N@ NMN_КУДО ;
+N+Der/НЕ+Dimin+N@P.POS.N@:%>Ы2не@P.POS.N@ NMN_КУДО ;

LEXICON N_КУДО-PL
+N@P.POS.N@:0@P.POS.N@ N_КУДО-Derivation ;
+N@P.POS.N@:0@P.POS.N@ NMN_DEF-PL-DECLENSION ;
+N@P.POS.N@:0@P.POS.N@ _КУДО-Indef-Declension-PL ;
+N@P.POS.N@:0@P.POS.N@ N_КУДО-Poss-Declension-CX-PL ;

LEXICON N_АВИДЕМ
!! Does this need a diminutive?
+N+AssocColl@P.POS.N@:00@P.POS.N@ N_КУДО-Poss-Declension-CX ;

!! NMN
LEXICON NMN_КАЛ
 N_КАЛ-Derivation ;
 N_КАЛ-Def-Declension ;
 _КАЛ-Indef-Declension ;
 N_КАЛ-Poss-Declension-CX ;

LEXICON NMN_ЛАК
 N_ЛАК-Derivation ;
 N_ЛАК-Def-Declension ;
 _ЛАК-Indef-Declension ;
 N_КАЛ-Poss-Declension-CX ;

LEXICON NMN_КИТ
 N_КИТ-Derivation ;
!2013-03-15!+Err/Sub+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;
 N_КИТ-Def-Declension ;
 _КИТ-Indef-Declension ;
 N_КАЛ-Poss-Declension-CX ;

LEXICON NMN_КЛАСС
 N_КЛАСС-Derivation ;
 N_ОШ-Def-Declension ;
 _ОШ-Indef-Declension ;
 N_КАЛ-Poss-Declension-CX ;

LEXICON NMN_ОШ
 N_ОШ-Derivation ;
 N_ОШ-Def-Declension ;
 _ОШ-Indef-Declension ;
 N_КАЛ-Poss-Declension-CX ;

LEXICON NMN_ОШ-PL !!= * __@CODE@__ This needs checking 2013-03-27
 N_ОШ-Derivation ;
 NMN_DEF-PL-DECLENSION ;
 _ОШ-Indef-Declension-PL ;
 N_ОШ-Poss-Declension-CX-PL ;

LEXICON NMN_ТЕШТЕ
 NMN_ПАНГО ;

LEXICON NMN_ТИНГЕ
 NMN_ПАНГО ;

LEXICON NMN_ПАНГО
 N_ПАНГО-Derivation ;
 N_ПАНГО-Def-Declension ;
 _ПАНГО-Indef-Declension ;
 N_ПАНГО-Poss-Declension-CX ;

LEXICON NMN_КИ
 NMN_КУДО ;
:я PRL-INDEF ;
:я SG-PRL-DEF ;
+Sg+Prl:ява PossPX1 ;

LEXICON NMN_КУДО
 N_КУДО-Derivation ;
 NMN_DEF-DECLENSION ;
 _КУДО-Indef-Declension ;
 N_КУДО-Poss-Declension-CX ;

LEXICON NMN_КУДО-PL !!= * __@CODE@__ This needs checking 2013-03-27
 N_КУДО-Derivation ;
 NMN_DEF-PL-DECLENSION ;
 _КУДО-Indef-Declension-PL ;
 N_КУДО-Poss-Declension-CX-PL ;


! ========== !
!! DERIVATION 
! ========== !

LEXICON N_ЛАК-Derivation
 N_КАЛ-Derivation ;
+Err/Sub+Orth/PhonInfl+Tra+Indef:%>KLossкс%> N2Dem-SE ;
+Err/Sub+Orth/PhonInfl+Tra+Indef:%>KLossкс%>О1%> N2Dem-SE ;


LEXICON N_КИТ-Derivation
 N_КАЛ-Derivation;


LEXICON N_КАЛ-Derivation
+Ela+Indef+Der/MWN+N:%>стО1 _КУДО-N2-Def-Declension ;
+Ine+Indef+Der/MWN+N:%>сО1 _КУДО-N2-Def-Declension ;
+Prl+Indef+Der/MWN+N:%>Г2а _КУДО-N2-Def-Declension ;
+CompCx+Indef+Der/MWN+N:%>%{ОЕЭØ%}шка _КУДО-N2-Def-Declension ;
+Abe+Indef+Der/MWN+N:%>втО1мО1 _КУДО-N2-Def-Declension ;

+Tra+Indef+Der/MWN+N:%>%{ОЕЭØ%}кс _ОШ-N2-Def-Declension ;

+Pl+Gen: POSSX-OBL4-N2DEM-SE ;
+Ela:%>стО1 POSSX-OBL4-N2DEM-SE ;
+Ine:%>сО1 POSSX-OBL4-N2DEM-SE ;
+Prl:%>Г2а POSSX-OBL4-N2DEM-SE ;
+CompCx:%>%{ОЕЭØ%}шка POSSX-OBL4-N2DEM-SE ;
+Abe:%>втО1мО1 POSSX-OBL4-N2DEM-SE ;


 N_КАЛ-Derivation-WITH-POSSX ;

+Pl+Ela+Def+Der/MWN+N:%>тнЕ3%>стэ _КУДО-N2-Def-Declension ;
+Pl+Ine+Def+Der/MWN+N:%>тнЕ3%>сэ _КУДО-N2-Def-Declension ;
+Pl+Prl+Def+Der/MWN+N:%>тнЕ3%>Г2а _КУДО-N2-Def-Declension ;
+Pl+CompCx+Def+Der/MWN+N:%>тнЕ3%>шка _КУДО-N2-Def-Declension ;
+Pl+Abe+Def+Der/MWN+N:%>тнЕ3%>втеме _КУДО-N2-Def-Declension ;
+Pl+Tra+Def+Der/MWN+N:%>тнЕ3%>кс _ОШ-N2-Def-Declension ;
+Gen+Indef:%>%{ОЕЭØ%}нь N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Ela+Indef:%>стО1%> N2Dem-SE ;
+Ine+Indef:%>сО1%> N2Dem-SE ;
+Prl+Indef:%>Г2а%> N2Dem-SE ;
+CompCx+Indef:%>%{ОЕЭØ%}шка%> N2Dem-SE ;
+Abe+Indef:%>втО1мО1%> N2Dem-SE ;
+Tra+Indef:%>кс%> N2Dem-SE ;
+Tra+Indef:%>кс%>О1%> N2Dem-SE ;


+Sg+Gen+Def:%>%{ОЕЭØ%}нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Ela+Def:%>стО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Ine+Def:%>сО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Prl+Def:%>Г2а%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+CompCx+Def:%>%{ОЕЭØ%}шка%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Abe+Def:%>втО1мО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+Gen+Def+Err/Sub+Dial:%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+Ela+Def+Err/Sub+Dial:%>стО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+Ine+Def+Err/Sub+Dial:%>сО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+Prl+Def+Err/Sub+Dial:%>Г2а%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+CompCx+Def+Err/Sub+Dial:%>%{ОЕЭØ%}шка%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Use/-Spell+Sg+Abe+Def+Err/Sub+Dial:%>втО1мО1%>%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Pl+Gen+Def:%>тнЕ3%>нь N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Pl+Ela+Def:%>тнЕ3%>стэ%> N2Dem-SE ;
+Pl+Ine+Def:%>тнЕ3%>сэ%> N2Dem-SE ;
+Pl+Prl+Def:%>тнЕ3%>Г2а%> N2Dem-SE ;
+Pl+CompCx+Def:%>тнЕ3%>шка%> N2Dem-SE ;
+Pl+Abe+Def:%>тнЕ3%>втеме%> N2Dem-SE ;
+Pl+Tra+Def:%>тнЕ3%>кс%> N2Dem-SE ;
+Pl+Tra+Def:%>тнЕ3%>ксэ%> N2Dem-SE ;

LEXICON N_КЛАСС-Derivation
!! This requires a twolc rule that will [ с с:0 > с ] SLoss
 N_ОШ-Derivation ;
+SLoss+Ela+Indef+Der/MWN+N:%>SLossстО1 _КУДО-N2-Def-Declension ;
+SLoss+Ine+Indef+Der/MWN+N:%>SLossсО1 _КУДО-N2-Def-Declension ;
+SLoss+Ela+PxSg3+Der/MWN+N:%>SLossстО1%>нзО1 _КУДО-N2-Def-Declension ;
+SLoss+Ine+PxSg3+Der/MWN+N:%>SLossсО1%>нзО1 _КУДО-N2-Def-Declension ;
+SLoss+Ela+PxPl1+Der/MWN+N:%>SLossстО1%>нО1к _ЛАК-N2-Def-Declension ;
+SLoss+Ine+PxPl1+Der/MWN+N:%>SLossсО1%>нО1к _ЛАК-N2-Def-Declension ;
+SLoss+Ela+PxPl2+Der/MWN+N:%>SLossстО1%>нк _ЛАК-N2-Def-Declension ;
+SLoss+Ine+PxPl2+Der/MWN+N:%>SLossсО1%>нк _ЛАК-N2-Def-Declension ;
+SLoss+Ela+PxPl3+Der/MWN+N:%>SLossстО1%>ст _КИТ-N2-Def-Declension ;
+SLoss+Ine+PxPl3+Der/MWN+N:%>SLossсО1%>ст _КИТ-N2-Def-Declension ;
+SLoss+Ela+Indef:%>SLossстО1%> N2Dem-SE ;
+SLoss+Ine+Indef:%>SLossсО1%> N2Dem-SE ;
+SLoss+Ela:%>SLossстО1 POSSX-OBL4-N2DEM-SE ;
+SLoss+Ine:%>SLossсО1 POSSX-OBL4-N2DEM-SE ;

+SLoss+Sg+Ela+Def:%>SLossстО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+SLoss+Sg+Ine+Def:%>SLossсО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+SLoss+Sg+Ela+Def+Err/Sub+Dial:%>SLossстО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+SLoss+Sg+Ine+Def+Err/Sub+Dial:%>SLossсО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension


LEXICON N_ОШ-Derivation
 N_КАЛ-Derivation ;
+Ela+Indef+Der/MWN+N:%>О1%>стО1 _КУДО-N2-Def-Declension ;
+Ine+Indef+Der/MWN+N:%>О1%>сО1 _КУДО-N2-Def-Declension ;
+Ela+PxSg3+Der/MWN+N:%>О1%>стО1%>нзО1 _КУДО-N2-Def-Declension ;
+Ine+PxSg3+Der/MWN+N:%>О1%>сО1%>нзО1 _КУДО-N2-Def-Declension ;
+Ela+PxPl1+Der/MWN+N:%>О1%>стО1%>нО1к _ЛАК-N2-Def-Declension ;
+Ine+PxPl1+Der/MWN+N:%>О1%>сО1%>нО1к _ЛАК-N2-Def-Declension ;
+Ela+PxPl2+Der/MWN+N:%>О1%>стО1%>нк _ЛАК-N2-Def-Declension ;
+Ine+PxPl2+Der/MWN+N:%>О1%>сО1%>нк _ЛАК-N2-Def-Declension ;
+Ela+PxPl3+Der/MWN+N:%>О1%>стО1%>ст _КИТ-N2-Def-Declension ;
+Ine+PxPl3+Der/MWN+N:%>О1%>сО1%>ст _КИТ-N2-Def-Declension ;
+Ela+Indef:%>О1%>стО1%> N2Dem-SE ;
+Ine+Indef:%>О1%>сО1%> N2Dem-SE ;
+Ela:%>О1%>стО1 POSSX-OBL4-N2DEM-SE ;
+Ine:%>О1%>сО1 POSSX-OBL4-N2DEM-SE ;

+Sg+Ela+Def:%>О1%>стО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Ine+Def:%>О1%>сО1%>нть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Ela+Def+Err/Sub+Dial:%>О1%>стО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Sg+Ine+Def+Err/Sub+Dial:%>О1%>сО1%>сть N2Dem-SE ; !!= * __@CODE@__ _ь_ retension

LEXICON N_ПАНГО-Derivation
 N_КУДО-Derivation ;
:%>VLoss N_КУДО-Derivation-noArrow ;

LEXICON N_КУДО-Derivation
:%> N_КУДО-Derivation-noArrow ;

LEXICON N_КУДО-Derivation-noArrow
+Ela+Indef+Der/MWN+N:стО1 _КУДО-N2-Def-Declension ;
+Ine+Indef+Der/MWN+N:сО1 _КУДО-N2-Def-Declension ;
+Prl+Indef+Der/MWN+N:Г2а _КУДО-N2-Def-Declension ;
+CompCx+Indef+Der/MWN+N:шка _КУДО-N2-Def-Declension ;
+Abe+Indef+Der/MWN+N:втО1мО1 _КУДО-N2-Def-Declension ;
+Tra+Indef+Der/MWN+N:кс _ОШ-N2-Def-Declension ;
+Gen+PxSg3+Der/MWN+N:нзО1 _КУДО-N2-Def-Declension ;
+Ela+PxSg3+Der/MWN+N:стО1%>нзО1 _КУДО-N2-Def-Declension ;
+Ine+PxSg3+Der/MWN+N:сО1%>нзО1 _КУДО-N2-Def-Declension ;
+Prl+PxSg3+Der/MWN+N:Г2а%>нзО1 _КУДО-N2-Def-Declension ;
+CompCx+PxSg3+Der/MWN+N:шка%>нзО1 _КУДО-N2-Def-Declension ;
+Abe+PxSg3+Der/MWN+N:втО1мО1%>нзО1 _КУДО-N2-Def-Declension ;
+Gen+PxPl1+Der/MWN+N:нО1к _ЛАК-N2-Def-Declension ;
+Ela+PxPl1+Der/MWN+N:стО1%>нО1к _ЛАК-N2-Def-Declension ;
+Ine+PxPl1+Der/MWN+N:сО1%>нО1к _ЛАК-N2-Def-Declension ;
+Prl+PxPl1+Der/MWN+N:Г2а%>нО1к _ЛАК-N2-Def-Declension ;
+CompCx+PxPl1+Der/MWN+N:шка%>нО1к _ЛАК-N2-Def-Declension ;
+Abe+PxPl1+Der/MWN+N:втО1мО1%>нО1к _ЛАК-N2-Def-Declension ;
+Gen+PxPl2+Der/MWN+N:нк _ЛАК-N2-Def-Declension ;
+Ela+PxPl2+Der/MWN+N:стО1%>нк _ЛАК-N2-Def-Declension ;
+Ine+PxPl2+Der/MWN+N:сО1%>нк _ЛАК-N2-Def-Declension ;
+Prl+PxPl2+Der/MWN+N:Г2а%>нк _ЛАК-N2-Def-Declension ;
+CompCx+PxPl2+Der/MWN+N:шка%>нк _ЛАК-N2-Def-Declension ;
+Abe+PxPl2+Der/MWN+N:втО1мО1%>нк _ЛАК-N2-Def-Declension ;
+Gen+PxPl3+Der/MWN+N:ст _КИТ-N2-Def-Declension ;
+Ela+PxPl3+Der/MWN+N:стО1%>ст _КИТ-N2-Def-Declension ;
+Ine+PxPl3+Der/MWN+N:сО1%>ст _КИТ-N2-Def-Declension ;
+Prl+PxPl3+Der/MWN+N:Г2а%>ст _КИТ-N2-Def-Declension ;
+CompCx+PxPl3+Der/MWN+N:шка%>ст _КИТ-N2-Def-Declension ;
+Abe+PxPl3+Der/MWN+N:втО1мО1%>ст _КИТ-N2-Def-Declension ;
+Pl+Ela+Def+Der/MWN+N:тнЕ3%>стэ _КУДО-N2-Def-Declension ;
+Pl+Ine+Def+Der/MWN+N:тнЕ3%>сэ _КУДО-N2-Def-Declension ;
+Pl+Prl+Def+Der/MWN+N:тнЕ3%>Г2а _КУДО-N2-Def-Declension ;
+Pl+CompCx+Def+Der/MWN+N:тнЕ3%>шка _КУДО-N2-Def-Declension ;
+Pl+Abe+Def+Der/MWN+N:тнЕ3%>втеме _КУДО-N2-Def-Declension ;
+Pl+Tra+Def+Der/MWN+N:тнЕ3%>кс _ОШ-N2-Def-Declension ;
+Gen+Indef:нь N2Dem-SE ; !!= * __@CODE@__ _ь_ retension
+Ela+Indef:стО1%> N2Dem-SE ;
+Ine+Indef:сО1%> N2Dem-SE ;
+Prl+Indef:Г2а%> N2Dem-SE ;
+CompCx+Indef:шка%> N2Dem-SE ;
+Abe+Indef:втО1мО1%> N2Dem-SE ;
+Tra+Indef:кс%> N2Dem-SE ;
+Tra+Indef:кс%>О1%> N2Dem-SE ;
+Sg+Gen+PxSg1:м%> N2Dem-SE ;
+Pl+Gen+PxSg1:нЬ2%> N2Dem-SE ;

+Ela:стО1 POSSX-OBL4-N2DEM-SE ;
+Ine:сО1 POSSX-OBL4-N2DEM-SE ;
+Prl:Г2а POSSX-OBL4-N2DEM-SE ;
+CompCx:шка POSSX-OBL4-N2DEM-SE ;
+Abe:втО1мО1 POSSX-OBL4-N2DEM-SE ;

+Gen+PxSg1+Err/Sub+Dial:м%> N2Dem-SE ;
+Gen+PxSg2:тЬ2%> N2Dem-SE ;
+Gen+PxSg2+Err/Sub+Dial:нтЬ2%> N2Dem-SE ;
+Gen+PxSg3:нзО1%> N2Dem-SE ;
+Gen+PxPl1:нО1к%> N2Dem-SE ;
+Gen+PxPl2:нк%> N2Dem-SE ;
+Gen+PxPl3:ст%> N2Dem-SE ;

+Sg+Gen+Def:нть N2Dem-SE ; ! _ь_ retension
+Sg+Ela+Def:стО1%>нть N2Dem-SE ; ! _ь_ retension
+Sg+Ine+Def:сО1%>нть N2Dem-SE ; ! _ь_ retension
+Sg+Prl+Def:Г2а%>нть N2Dem-SE ; ! _ь_ retension
+Sg+CompCx+Def:шка%>нть N2Dem-SE ; ! _ь_ retension
+Sg+Abe+Def:втО1мО1%>нть N2Dem-SE ; ! _ь_ retension
+Sg+Gen+Def+Err/Sub+Dial:сть N2Dem-SE ; ! _ь_ retension
+Sg+Ela+Def+Err/Sub+Dial:стО1%>сть N2Dem-SE ; ! _ь_ retension
+Sg+Ine+Def+Err/Sub+Dial:сО1%>сть N2Dem-SE ; ! _ь_ retension
+Sg+Prl+Def+Err/Sub+Dial:Г2а%>сть N2Dem-SE ; ! _ь_ retension
+Sg+CompCx+Def+Err/Sub+Dial:шка%>сть N2Dem-SE ; ! _ь_ retension
+Sg+Abe+Def+Err/Sub+Dial:втО1мО1%>%>сть N2Dem-SE ; ! _ь_ retension
+Pl+Gen+Def:тнЕ3%>нь N2Dem-SE ; ! _ь_ retension
+Pl+Ela+Def:тнЕ3%>стэ%> N2Dem-SE ;
+Pl+Ine+Def:тнЕ3%>сэ%> N2Dem-SE ;
+Pl+Prl+Def:тнЕ3%>Г2а%> N2Dem-SE ;
+Pl+CompCx+Def:тнЕ3%>шка%> N2Dem-SE ;
+Pl+Abe+Def:тнЕ3%>втеме%> N2Dem-SE ;
+Pl+Tra+Def:тнЕ3%>кс%> N2Dem-SE ;
+Pl+Tra+Def:тнЕ3%>ксэ%> N2Dem-SE ;


LEXICON _ЛАК-N2-Def-Declension
!!2012-05-03!:%>KLoss _КАЛ-N2-Def-Declension ;
 _КАЛ-N2-Def-Declension ;

LEXICON _КИТ-N2-Def-Declension
 _КАЛ-N2-Def-Declension ;
!2013-03-15!+Err/Sub+Orth/PhonInfl:%>TLoss NMN_DEF-PL-DECLENSION ;

LEXICON _КАЛ-N2-Def-Declension
 N_КАЛ-Def-Declension ;
+Tra+Indef:%>%{ОЕЭØ%}кс K ;
+Abl+Indef:%>дО1 _КУДО-Conjugation ;

LEXICON _ОШ-N2-Def-Declension
 N_ОШ-Def-Declension ;
+Tra+Indef:%>%{ОЕЭØ%}кс K ;
+Abl+Indef:%>дО1 _КУДО-Conjugation ;

LEXICON _КУДО-N2-Def-Declension
 NMN_DEF-SG-DECLENSION ;
+Tra+Indef:%>кс K ;
+Abl+Indef:%>дО1 _КУДО-Conjugation ;

! =================== !
! Inflectional lexica !
! =================== !

!
! Due to irregularity in spelling
! where triple consonants are disallowed
! and some double consonants are alternately dropped
! A two-class system has been extended to a six-classer
! _ПАНГО-N and Adj-chevte types are closed-class word sets
! which can drop their stem final vowels in certain combinations


! ================ !
! N-Def-Declension !
! ================ !

LEXICON N_КИТ-Def-Declension
 SG-NOM-DEF ;
 SG-GEN-DEF ;
 SG-DAT-DEF ;
 SG-CMP-DEF ;
 SG-ELA-DEF ;
 SG-INE-DEF ;
 SG-ABL-DEF ;
 SG-PRL-DEF ;
 SG-ABE-DEF ;
:%>%{ОЕЭØ%} SG-TRA-DEF ;
+Err/Sub+Dial/NW: SG-NOM-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-GEN-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-DAT-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-CMP-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-PRL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-TRA-DEF_Dial-NW ;

:%> NMN_DEF-PL-DECLENSION ;
!2013-03-15!+Err/Sub:%>TLoss NMN_DEF-PL-DECLENSION ;


LEXICON N_ЛАК-Def-Declension
 N_КАЛ-Def-Declension ;

LEXICON N_КАЛ-Def-Declension
 SG-NOM-DEF ;
 SG-GEN-DEF ;
 SG-DAT-DEF ;
 SG-CMP-DEF ;
 SG-ELA-DEF ;
 SG-INE-DEF ;
 SG-ABL-DEF ;
 SG-PRL-DEF ;
 SG-ABE-DEF ;
 SG-TRA-DEF ;
+Err/Sub+Dial/NW: SG-NOM-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-GEN-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-DAT-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-CMP-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-PRL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-TRA-DEF_Dial-NW ;

:%> NMN_DEF-PL-DECLENSION ;


LEXICON N_ОШ-Def-Declension
 SG-NOM-DEF ;
 SG-GEN-DEF ;
 SG-DAT-DEF ;
 SG-CMP-DEF ;
 SG-ELA-DEF ;
:%>%{ОЕЭØ%} SG-ELA-DEF ;
 SG-INE-DEF ;
:%>%{ОЕЭØ%} SG-INE-DEF ;
 SG-ABL-DEF ;
 SG-PRL-DEF ;
 SG-ABE-DEF ;
:%>%{ОЕЭØ%} SG-TRA-DEF ;
+Err/Sub+Dial/NW: SG-GEN-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-DAT-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-CMP-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>%{ОЕЭØ%} SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-PRL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-TRA-DEF_Dial-NW ;


:%> NMN_DEF-PL-DECLENSION ;

LEXICON N_ПАНГО-Def-Declension
!! pango:pang
:%>О1 SG-NOM-DEF ;
:%>О1 SG-GEN-DEF ;
:%>О1 SG-DAT-DEF ;
:%>О1 SG-CMP-DEF ;
 SG-ELA-DEF ;
:%>О1 SG-ELA-DEF ;
 SG-INE-DEF ;
:%>О1 SG-INE-DEF ;
+Err/Sub: SG-ABL-DEF ;
:%>О1 SG-ABL-DEF ;
:%>О1 SG-PRL-DEF ;
+Err/Sub: SG-ABE-DEF ;
:%>О1 SG-ABE-DEF ;
:%>О1 SG-TRA-DEF ;
+Err/Sub+Dial/NW:%>О1 SG-GEN-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-DAT-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%> SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-CMP-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%> SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%> SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-PRL-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-ABE-DEF_Dial-NW ;
+Err/Sub+Dial/NW:%>О1 SG-TRA-DEF_Dial-NW ;


:%>О1%> NMN_DEF-PL-DECLENSION ;
:%> NMN_DEF-PL-DECLENSION ;

LEXICON NMN_DEF-DECLENSION
 NMN_DEF-SG-DECLENSION ;
 NMN_DEF-PL-DECLENSION ;


LEXICON NMN_DEF-SG-DECLENSION
!! N_КУДО-Def-Declension
 SG-NOM-DEF ;
 SG-GEN-DEF ;
 SG-DAT-DEF ;
 SG-CMP-DEF ;
 SG-ELA-DEF ;
 SG-INE-DEF ;
 SG-ABL-DEF ;
 SG-PRL-DEF ;
 SG-ABE-DEF ;
 SG-TRA-DEF ;
+Err/Sub+Dial/NW: SG-GEN-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-DAT-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-CMP-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ELA-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-INE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-PRL-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-ABE-DEF_Dial-NW ;
+Err/Sub+Dial/NW: SG-TRA-DEF_Dial-NW ;


:%> NMN_DEF-PL-DECLENSION ;


!! !!DEFINITE SINGULAR TAGS 

LEXICON SG-NOM-DEF
!!€gt-norm: Noun singular nominative definite
!!€ калось кал+N+Sg+Nom+Def
!!€ калоськак кал+N+Sg+Nom+Def+Foc/Гак
!!$ калосьгак кал+N+Sg+Nom+Def+Foc/Гак
+Sg+Nom+Def0@U.DECL-NX.SG@:%>%{ОЕЭØ%}сь@U.DECL-NX.SG@ K ;
+Sg+Nom+Def0@U.DECL-NX.SG@:%>%{ОЕЭØ%}сь@U.DECL-NX.SG@ DER/PR_SG ;

LEXICON SG-GEN-DEF
!!€gt-norm: Noun singular genitive definite
!!€ калонть кал+N+Sg+Gen+Def
!!€ калонтькак кал+N+Sg+Gen+Def+Foc/Гак
+Sg+Gen+Def00@U.DECL-NX.SG@:%>%{ОЕЭØ%}нть@U.DECL-NX.SG@ K ;

LEXICON SG-DAT-DEF
+Sg+DatCx+Def0000@U.DECL-NX.SG@:%>%{ОЕЭØ%}нтень@U.DECL-NX.SG@ K ;

LEXICON SG-CMP-DEF
+Sg+CompCx+Def000000@U.DECL-NX.SG@:%>%{ОЕЭØ%}шка%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-ELA-DEF
+Sg+Ela+Def00000@U.DECL-NX.SG@:%>стО1%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-INE-DEF
+Sg+Ine+Def0000@U.DECL-NX.SG@:%>сО1%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-ABL-DEF
+Sg+Abl+Def0000@U.DECL-NX.SG@:%>дО1%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-PRL-DEF
+Sg+Prl+Def0000@U.DECL-NX.SG@:%>Г2а%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-ABE-DEF
+Sg+Abe+Def0000000@U.DECL-NX.SG@:%>втО1мО1%>нть@U.DECL-NX.SG@ K ;

LEXICON SG-TRA-DEF
+Sg+Tra+Def000000@U.DECL-NX.SG@:%>%{ОЕЭØ%}кс%>%{ОЕЭØ%}нть@U.DECL-NX.SG@ K ;

LEXICON SG-NOM-DEF_Dial-NW
+Sg+Nom+Def@U.DECL-NX.SG@:%>сь@U.DECL-NX.SG@ K ;

LEXICON SG-GEN-DEF_Dial-NW
+Use/-Spell+Sg+Gen+Def@U.DECL-NX.SG@:%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-DAT-DEF_Dial-NW
+Use/-Spell+Sg+DatCx+Def00@U.DECL-NX.SG@:%>стень@U.DECL-NX.SG@ K ;

LEXICON SG-ABL-DEF_Dial-NW
+Use/-Spell+Sg+Abl+Def000@U.DECL-NX.SG@:%>дО1%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-ELA-DEF_Dial-NW
+Use/-Spell+Sg+Ela+Def0000@U.DECL-NX.SG@:%>стО1%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-INE-DEF_Dial-NW
+Use/-Spell+Sg+Ine+Def000@U.DECL-NX.SG@:%>сО1%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-PRL-DEF_Dial-NW
+Use/-Spell+Sg+Prl+Def000@U.DECL-NX.SG@:%>Г2а%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-CMP-DEF_Dial-NW
+Use/-Spell+Sg+CompCx+Def0000@U.DECL-NX.SG@:%>шка%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-ABE-DEF_Dial-NW
+Use/-Spell+Sg+Abe+Def000000@U.DECL-NX.SG@:%>втО1мО1%>сть@U.DECL-NX.SG@ K ;

LEXICON SG-TRA-DEF_Dial-NW
+Use/-Spell+Sg+Tra+Def0000@U.DECL-NX.SG@:%>кс%>%{ОЕЭØ%}сть@U.DECL-NX.SG@ K ;




!! !!INDEFINITE DECLENSION

LEXICON _КИТ-Indef-Declension
! _КИТ-Indef-Declension-SG ;
! _КИТ-Indef-Declension-PL ;
 SG-NOM-INDEF ;
 PL-NOM-INDEF ;
 _КИТ-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;

LEXICON _КИТ-Indef-Declension-SG
 _КИТ-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;
 SG-NOM-INDEF ;
! SG-NOM-INDEF_КИТ ;

LEXICON _КИТ-Indef-Declension-PL
 _КИТ-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;
 PL-NOM-INDEF ;

LEXICON _КИТ-Indef-Declension-MUTUAL
:%>О1 CMP-INDEF ;
:%>О1 TRA-INDEF ;

LEXICON _ЛАК-Indef-Declension
! _ЛАК-Indef-Declension-SG ;
! _ЛАК-Indef-Declension-PL ;
 SG-NOM-INDEF ;
 PL-NOM-INDEF ;
 _ЛАК-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;

LEXICON _ЛАК-Indef-Declension-SG
 _ЛАК-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;
 SG-NOM-INDEF ;
! SG-NOM-INDEF_ЛАК ;

LEXICON _ЛАК-Indef-Declension-PL
 _ЛАК-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;
 PL-NOM-INDEF ;

LEXICON _ЛАК-Indef-Declension-MUTUAL
:%>О1 CMP-INDEF ;
:%>О1 TRA-INDEF ;

LEXICON _КАЛ-Indef-Declension
! _КАЛ-Indef-Declension-SG ;
! _КАЛ-Indef-Declension-PL ;
 SG-NOM-INDEF ;
 PL-NOM-INDEF ;
 _КАЛ-Indef-Declension-MUTUAL ;
 _MUTUAL-Indef-Declension-SP ;

LEXICON _КАЛ-Indef-Declension-SG
 _КАЛ-Indef-Declension-MUTUAL ;
 SG-NOM-INDEF ;
! SG-NOM-INDEF_КАЛ ;
 _MUTUAL-Indef-Declension-SP ;

LEXICON _КАЛ-Indef-Declension-PL
 _КАЛ-Indef-Declension-MUTUAL ;
 PL-NOM-INDEF ;
 _MUTUAL-Indef-Declension-SP ;

LEXICON _КАЛ-Indef-Declension-MUTUAL
:%>О1 CMP-INDEF ;
:%>О1 TRA-INDEF ;



LEXICON _ОШ-Indef-Declension
! _ОШ-Indef-Declension-SG ;
! _ОШ-Indef-Declension-PL ;
 SG-NOM-INDEF ;
 PL-NOM-INDEF ;
 _ОШ-Indef-Declension-MUTUAL ;

LEXICON _ОШ-Indef-Declension-SG
 _ОШ-Indef-Declension-MUTUAL ;
 SG-NOM-INDEF ;
! SG-NOM-INDEF_ОШ ;

LEXICON _ОШ-Indef-Declension-PL
 _ОШ-Indef-Declension-MUTUAL ;
 PL-NOM-INDEF ;

LEXICON _ОШ-Indef-Declension-MUTUAL
 ABE-INDEF ;
 ABL-INDEF ;
:%>О1 CMP-INDEF ;
 COM-INDEF ;
 DAT-INDEF ;
 ELA-INDEF ;
:%>О1 ELA-INDEF ;
 GEN-INDEF ;
 ILL-INDEF ;
:%>О1 ILL-INDEF ;
 INE-INDEF ;
:%>О1 INE-INDEF ;
 PRL-INDEF ;
 TMP-INDEF ;
:%>О1 TRA-INDEF ;



LEXICON _ПАНГО-Indef-Declension
! _ПАНГО-Indef-Declension-SG ;
! _ПАНГО-Indef-Declension-PL ;
:%>О1  SG-NOM-INDEF ;
:%>О1 PL-NOM-INDEF ;
 PL-NOM-INDEF ;
 _ПАНГО-Indef-Declension-MUTUAL ;
:%>О1 _MUTUAL-Indef-Declension-SP ;

LEXICON _ПАНГО-Indef-Declension-SG
 _ПАНГО-Indef-Declension-MUTUAL ;
:%>О1 _MUTUAL-Indef-Declension-SP ;
:%>О1  SG-NOM-INDEF ;
!SG-NOM-INDEF_ПАНГО ;

LEXICON _ПАНГО-Indef-Declension-PL
 _ПАНГО-Indef-Declension-MUTUAL ;
:%>О1 _MUTUAL-Indef-Declension-SP ;
:%>О1 PL-NOM-INDEF ;
 PL-NOM-INDEF ;

LEXICON _ПАНГО-Indef-Declension-MUTUAL
+Err/Sub:%> ABE-INDEF ;
+Err/Sub: ABL-INDEF ;
 ELA-INDEF ;
 ILL-INDEF ;
 INE-INDEF ;

LEXICON _КУДО-Indef-Declension
! _КУДО-Indef-Declension-SG ;
! _КУДО-Indef-Declension-PL ;
 SG-NOM-INDEF ;
 PL-NOM-INDEF ;
 _MUTUAL-Indef-Declension-SP ;


LEXICON _КУДО-Indef-Declension-SG
 _MUTUAL-Indef-Declension-SP ;
 SG-NOM-INDEF ;
! SG-NOM-INDEF_КУДО ;

LEXICON _КУДО-Indef-Declension-PL
 _MUTUAL-Indef-Declension-SP ;
 PL-NOM-INDEF ;

LEXICON _MUTUAL-Indef-Declension-SP
 ABE-INDEF ;
 ABL-INDEF ;
 CMP-INDEF ;
 COM-INDEF ;
 DAT-INDEF ;
 ELA-INDEF ;
 GEN-INDEF ;
 ILL-INDEF ;
 INE-INDEF ;
 PRL-INDEF ;
 TMP-INDEF ; !!= * __@CODE@__ Check this
 TRA-INDEF ;


!! !!INDEFINITE TAGS
LEXICON ABE-INDEF
+SP+Abe+Indef000@U.DECL-NX.SP@@U.DECL-CX.ABE@:%>втО1мО1@U.DECL-NX.SP@@U.DECL-CX.ABE@ _КУДО-Conjugation ;

LEXICON ABL-INDEF
!!€gt-norm: Noun singular nominative definite
!!€ калдо кал+N+SP+Abl+Indef
!!€ калдояк кал+N+SP+Abl+Indef+Foc/Гак
+SP+Abl+Indef@U.DECL-NX.SP@@U.DECL-CX.ABL@:%>дО1@U.DECL-NX.SP@@U.DECL-CX.ABL@ _КУДО-Conjugation ;

LEXICON CMP-INDEF
+SP+CompCx+Indef0@U.DECL-NX.SP@@U.DECL-CX.CMP@:%>шка@U.DECL-NX.SP@@U.DECL-CX.CMP@ _КУДО-Conjugation ;

LEXICON COM-INDEF
+SP+Com+Indef0@U.DECL-NX.SP@@U.DECL-CX.COM@:%>нек@U.DECL-NX.SP@@U.DECL-CX.COM@ NounEnd_K-lak ;
+SP+Com+Indef+Coll:%>нек%-мезнек WORD-END ;

LEXICON DAT-INDEF
+SP+DatCx+Indef00@U.DECL-NX.SP@@U.DECL-CX.DAT@:%>нень@U.DECL-NX.SP@@U.DECL-CX.DAT@ K ;

LEXICON ELA-INDEF
+SP+Ela+Indef0@U.DECL-NX.SP@@U.DECL-CX.ELA@:%>стО1@U.DECL-NX.SP@@U.DECL-CX.ELA@ _КУДО-Conjugation ;

LEXICON GEN-INDEF
+SP+Gen+Indef0@U.DECL-NX.SP@@U.DECL-CX.GEN@:%>%{ОЕЭØ%}нь@U.DECL-NX.SP@@U.DECL-CX.GEN@ GEN-INDEF-4-Conjugation ;

LEXICON ILL-INDEF
+SP+Ill+Indef@U.DECL-NX.SP@@U.DECL-CX.ILL@:%>с0@U.DECL-NX.SP@@U.DECL-CX.ILL@ K ;

LEXICON INE-INDEF
+SP+Ine+Indef@U.DECL-NX.SP@@U.DECL-CX.INE@:%>сО1@U.DECL-NX.SP@@U.DECL-CX.INE@ _КУДО-Conjugation ;

LEXICON LAT-INDEF
+SP+Lat+Indef@U.DECL-NX.SP@@U.DECL-CX.LAT@:%>%{ОЕЭØ%}в@U.DECL-NX.SP@@U.DECL-CX.LAT@ K ;

LEXICON LOC-INDEF
+SP+Loc+Indef@U.DECL-NX.SP@@U.DECL-CX.LOC@:%>О10@U.DECL-NX.SP@@U.DECL-CX.LOC@ K ;

LEXICON SG-NOM-INDEF
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ K ;
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ DER/PR_SG ;
+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRS-PL1 ;
+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRS-PL2 ;
+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRS-PL3 ;

+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRT2-PL1 ;
+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRT2-PL2 ;
+Pl+Nom+Indef@U.DECL-NX.PL@:000@U.DECL-NX.PL@ DER/PR-IND-PRT2-PL3 ;


LEXICON SG-NOM-INDEF_КАЛ
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ K ;
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ DER/PR_SG ;

LEXICON SG-NOM-INDEF_КИТ
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ K ;
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ DER/PR_SG ;

LEXICON SG-NOM-INDEF_ЛАК
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ K ;
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ DER/PR_SG ;

LEXICON SG-NOM-INDEF_ОШ
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ K ;
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ DER/PR_SG ;

LEXICON SG-NOM-INDEF_ПАНГО
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ _ПАНГО-NomSg-Conjugation ;

LEXICON SG-NOM-INDEF_КУДО
+Sg+Nom+Indef@U.DECL-NX.SG@:000@U.DECL-NX.SG@ _КУДО-NomSg-Conjugation ;

LEXICON PRL-INDEF
+SP+Prl+Indef@U.DECL-NX.SP@@U.DECL-CX.PRL@:%>Г2а@U.DECL-NX.SP@@U.DECL-CX.PRL@ K ;
+SP+Prl+Indef@U.DECL-NX.SP@@U.DECL-CX.PRL@:%>Г2а@U.DECL-NX.SP@@U.DECL-CX.PRL@ DER/PR4_OBL ;

LEXICON TMP-INDEF
+SP+TempCx+Indef@U.DECL-NX.SP@@U.DECL-CX.TEMP@:%>не@U.DECL-NX.SP@@U.DECL-CX.TEMP@ K ;

LEXICON TRA-INDEF
+SP+Tra+Indef@U.DECL-NX.SP@@U.DECL-CX.TRA@:%>кс@U.DECL-NX.SP@@U.DECL-CX.TRA@ K ;
+SP+Tra+Indef@U.DECL-NX.SP@@U.DECL-CX.TRA@:%>кс@U.DECL-NX.SP@@U.DECL-CX.TRA@ DER/PR4_OBL ;

LEXICON PL-NOM-INDEF
+Pl+Nom+Indef@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.INDEF@:%>тЬ2@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.INDEF@ _КИТ-NomPl-Conjugation ;
+Pl+Nom+Indef@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.INDEF@:%>тЬ2@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.INDEF@ K ;




!! !!POSSESSIVE DECLENSION 


LEXICON N_КАЛ-Poss-Declension-CX
 N_КАЛ-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;
 PL-GEN-PX ;
 PL-NOM-PX ;

LEXICON N_КАЛ-Poss-Declension-CX-SG
 N_КАЛ-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;


LEXICON N_КАЛ-Poss-Declension-CX-PL
 N_КАЛ-Poss-Declension-CX-Mutual ;
 PL-GEN-PX ;
 PL-NOM-PX ;

LEXICON N_КАЛ-Poss-Declension-CX-Mutual
 ABE-PX ;
 ABL-PX ;
 CMP-PX ;
:%>О1 CMP-PX ;
 DAT-PX ;
 ELA-PX ;
:%> ILL-PX ;
:%>%{ОЕЭØ%} ILL-PX ;
 INE-PX ;
 PRL-PX ;
 TRA-PX ;
:%>О1 TRA-PX ;


LEXICON N_КИТ-Poss-Declension-CX
 N_КАЛ-Poss-Declension-CX ;


LEXICON N_ОШ-Poss-Declension-CX
 N_ОШ-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;
 PL-GEN-PX ;
 PL-NOM-PX ;

LEXICON N_ОШ-Poss-Declension-CX-SG
 N_ОШ-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;

LEXICON N_ОШ-Poss-Declension-CX-PL
 N_ОШ-Poss-Declension-CX-Mutual ;
 PL-GEN-PX ;
 PL-NOM-PX ;

LEXICON N_ОШ-Poss-Declension-CX-Mutual
 ABE-PX ;
 ABL-PX ;
:%>О1 CMP-PX ;
 DAT-PX ;
 ELA-PX ;
:%>О1 ELA-PX ;
:%> ILL-PX ;
:%>О1 ILL-PX ;
 INE-PX ;
:%>О1 INE-PX ;
 PRL-PX ;
 TRA-PX ;
:%>О1 TRA-PX ;

LEXICON N_ПАНГО-Poss-Declension-CX
 N_ПАНГО-Poss-Declension-CX-Mutual ;
:%>О1 SG-GEN-PX ;
:%>О1 SG-NOM-PX ;
:%>О1 PL-GEN-PX ;
:%>О1 PL-NOM-PX ;

LEXICON N_ПАНГО-Poss-Declension-CX-SG
 N_ПАНГО-Poss-Declension-CX-Mutual ;
:%>О1 SG-GEN-PX ;
:%>О1 SG-NOM-PX ;

LEXICON N_ПАНГО-Poss-Declension-CX-PL
 N_ПАНГО-Poss-Declension-CX-Mutual ;
:%>О1 PL-GEN-PX ;
:%>О1 PL-NOM-PX ;

LEXICON N_ПАНГО-Poss-Declension-CX-Mutual
:%>О1 ABE-PX ;
+Err/Sub:%> ABL-PX ;
:%>О1 ABL-PX ;
:%>О1 CMP-PX ;
:%>О1 DAT-PX ;
 ELA-PX ;
:%>О1 ELA-PX ;
:%>О1%> ILL-PX ;
 INE-PX ;
:%>О1 INE-PX ;
:%>О1 PRL-PX ;
:%>О1 TRA-PX ;


LEXICON N_КУДО-Poss-Declension-CX
 N_КУДО-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;
 PL-GEN-PX ;
 PL-NOM-PX ;


LEXICON N_КУДО-Poss-Declension-CX-SG
 N_КУДО-Poss-Declension-CX-Mutual ;
 SG-GEN-PX ;
 SG-NOM-PX ;

LEXICON N_КУДО-Poss-Declension-CX-PL
 N_КУДО-Poss-Declension-CX-Mutual ;
 PL-GEN-PX ;
 PL-NOM-PX ;

LEXICON N_КУДО-Poss-Declension-CX-Mutual
 ABE-PX ;
 ABL-PX ;
 CMP-PX ;
 DAT-PX ;
 ELA-PX ;
 ILL-PX ;
 INE-PX ;
 PRL-PX ;
 TRA-PX ;



!! !!CASES BEFORE POSSESSIVE TAGS 

LEXICON ABE-PX
+SP+Abe:%>втО1мО1 N_КУДО-Poss-Declension-PossX ;

LEXICON ABL-PX
+SP+Abl:%>дО1 N_КУДО-Poss-Declension-PossX ;

LEXICON CMP-PX
+SP+CompCx:%>шка N_КУДО-Poss-Declension-PossX ;

LEXICON ELA-PX
+SP+Ela:%>стО1 N_КУДО-Poss-Declension-PossX ;

LEXICON ILL-PX
+SP+Ill:з OBL-PX-FOLLOWED-BY-END-OR-FOC ;


LEXICON INE-PX
+SP+Ine:%>сО1 N_КУДО-Poss-Declension-PossX ;

LEXICON LOC-PX
+SP+Loc:%>О1 N_КУДО-Poss-Declension-PossX ;

LEXICON PRL-PX
+SP+Prl:%>Г2а N_КУДО-Poss-Declension-PossX ;

LEXICON TRA-PX
+SP+Tra:%>кс OBL-PX4-END-FOC_or_END ;



!! !!DEFINITE PLURAL


LEXICON NMN_DEF-PL-DECLENSION
+Pl+Nom+Def@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.DEF@:тнЕ3@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.DEF@ _КУДО-NomPl-Conjugation ;
+Pl+Gen+Def000@U.DECL-NX.PL@@U.DECL-CX.GEN@@U.DECL-DX.DEF@:тнЕ3%>нь@U.DECL-NX.PL@@U.DECL-CX.GEN@@U.DECL-DX.DEF@ K ;
+Pl+DatCx+Def00000@U.DECL-NX.PL@@U.DECL-CX.DAT@@U.DECL-DX.DEF@:тнЕ3%>нень@U.DECL-NX.PL@@U.DECL-CX.DAT@@U.DECL-DX.DEF@ K ;
+Pl+Abl+Def000@U.DECL-NX.PL@@U.DECL-CX.ABL@@U.DECL-DX.DEF@:тнЕ3%>де@U.DECL-NX.PL@@U.DECL-CX.ABL@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Ela+Def0000@U.DECL-NX.PL@@U.DECL-CX.ELA@@U.DECL-DX.DEF@:тнЕ3%>стэ@U.DECL-NX.PL@@U.DECL-CX.ELA@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Ill+Def00@U.DECL-NX.PL@@U.DECL-CX.ILL@@U.DECL-DX.DEF@:тнЕ3%>с@U.DECL-NX.PL@@U.DECL-CX.ILL@@U.DECL-DX.DEF@ K ;
+Pl+Ine+Def000@U.DECL-NX.PL@@U.DECL-CX.INE@@U.DECL-DX.DEF@:тнЕ3%>сэ@U.DECL-NX.PL@@U.DECL-CX.INE@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Prl+Def000@U.DECL-NX.PL@@U.DECL-CX.PRL@@U.DECL-DX.DEF@:тнЕ3%>Г2а@U.DECL-NX.PL@@U.DECL-CX.PRL@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+CompCx+Def0000@U.DECL-NX.PL@@U.DECL-CX.CMP@@U.DECL-DX.DEF@:тнЕ3%>шка@U.DECL-NX.PL@@U.DECL-CX.CMP@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Com+Def0000@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@:тнЕ3%>нек@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@ NounEnd_K-lak ;
+Pl+Abe+Def000000@U.DECL-NX.PL@@U.DECL-CX.ABE@@U.DECL-DX.DEF@:тнЕ3%>втеме@U.DECL-NX.PL@@U.DECL-CX.ABE@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Tra+Def000@U.DECL-NX.PL@@U.DECL-CX.TRA@@U.DECL-DX.DEF@:тнЕ3%>кс@U.DECL-NX.PL@@U.DECL-CX.TRA@@U.DECL-DX.DEF@ _ОШ-Conjugation ;
+Pl+Lat+Def00@U.DECL-NX.PL@@U.DECL-CX.LAT@@U.DECL-DX.DEF@:тнЕ3%>в@U.DECL-NX.PL@@U.DECL-CX.LAT@@U.DECL-DX.DEF@ K ;
+Err/Sub+Dial/NW+Pl+Com+Def00@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@:тнЕ3%>нек@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@ NounEnd_K-lak ;

!! !!Cases for тнэ
LEXICON NMN_DEF-PL-DECLENSION-ТНЭ
+Pl+Nom+Def@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.DEF@:тнэ@U.DECL-NX.PL@@U.DECL-CX.NOM@@U.DECL-DX.DEF@ _КУДО-NomPl-Conjugation ;
+Pl+Gen+Def000@U.DECL-NX.PL@@U.DECL-CX.GEN@@U.DECL-DX.DEF@:тнэ%>нь@U.DECL-NX.PL@@U.DECL-CX.GEN@@U.DECL-DX.DEF@ K ;
+Pl+DatCx+Def00000@U.DECL-NX.PL@@U.DECL-CX.DAT@@U.DECL-DX.DEF@:тнэ%>нень@U.DECL-NX.PL@@U.DECL-CX.DAT@@U.DECL-DX.DEF@ K ;
+Pl+Abl+Def000@U.DECL-NX.PL@@U.DECL-CX.ABL@@U.DECL-DX.DEF@:тнэ%>де@U.DECL-NX.PL@@U.DECL-CX.ABL@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Ela+Def0000@U.DECL-NX.PL@@U.DECL-CX.ELA@@U.DECL-DX.DEF@:тнэ%>стэ@U.DECL-NX.PL@@U.DECL-CX.ELA@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Ill+Def00@U.DECL-NX.PL@@U.DECL-CX.ILL@@U.DECL-DX.DEF@:тнэ%>с@U.DECL-NX.PL@@U.DECL-CX.ILL@@U.DECL-DX.DEF@ K ;
+Pl+Ine+Def000@U.DECL-NX.PL@@U.DECL-CX.INE@@U.DECL-DX.DEF@:тнэ%>сэ@U.DECL-NX.PL@@U.DECL-CX.INE@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Prl+Def000@U.DECL-NX.PL@@U.DECL-CX.PRL@@U.DECL-DX.DEF@:тнэ%>Г2а@U.DECL-NX.PL@@U.DECL-CX.PRL@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+CompCx+Def0000@U.DECL-NX.PL@@U.DECL-CX.CMP@@U.DECL-DX.DEF@:тнэ%>шка@U.DECL-NX.PL@@U.DECL-CX.CMP@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Com+Def0000@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@:тнэ%>нек@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@ NounEnd_K-lak ;
+Pl+Abe+Def000000@U.DECL-NX.PL@@U.DECL-CX.ABE@@U.DECL-DX.DEF@:тнэ%>втеме@U.DECL-NX.PL@@U.DECL-CX.ABE@@U.DECL-DX.DEF@ _КУДО-Conjugation ;
+Pl+Tra+Def000@U.DECL-NX.PL@@U.DECL-CX.TRA@@U.DECL-DX.DEF@:тнэ%>кс@U.DECL-NX.PL@@U.DECL-CX.TRA@@U.DECL-DX.DEF@ _ОШ-Conjugation ;
+Pl+Lat+Def00@U.DECL-NX.PL@@U.DECL-CX.LAT@@U.DECL-DX.DEF@:тнэ%>в@U.DECL-NX.PL@@U.DECL-CX.LAT@@U.DECL-DX.DEF@ K ;
+Err/Sub+Dial/NW+Pl+Com+Def00@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@:тнэ%>нек@U.DECL-NX.PL@@U.DECL-CX.COM@@U.DECL-DX.DEF@ NounEnd_K-lak ;



LEXICON _КАЛ-SecondaryDeclination
:%>О1 SecondaryDeclination_1RequiresVowel ;
 SecondaryDeclination_1RequiresNoVowel ;

LEXICON _КИТ-SecondaryDeclination
:%>О1 SecondaryDeclination_1RequiresVowel ;
 SecondaryDeclination_1RequiresNoVowel ;

LEXICON _ЛАК-SecondaryDeclination
:%>О1 SecondaryDeclination_1RequiresVowel ;
 SecondaryDeclination_1RequiresNoVowel ;

LEXICON _КУДО-SecondaryDeclination
 SecondaryDeclination ;

LEXICON SecondaryDeclination
:%> SecondaryDeclination_1RequiresVowel ;
 SecondaryDeclination_1RequiresNoVowel ;

LEXICON SecondaryDeclination_1RequiresVowel
+Sg+Def+Nom:сь  _ОШ-Tenses1_Prep-SG ;
+Sg+Def+Gen:нть K ;
+Sg+Def+DatCx:нтень K ;
+Sg+Tra+Def:%>кс _ОШ-SecondaryDeclination_SgDet-PossPx ;

LEXICON SecondaryDeclination_1RequiresNoVowel
+Sg+Prl+Def:%>Г2а SecondaryDeclination_SgDet-PossPx ;
+Sg+Abe+Def:%>втО1мО1 SecondaryDeclination_SgDet-PossPx ;
+Sg+Abl+Def:%>дО1 SecondaryDeclination_SgDet-PossPx ;
+Sg+Ine+Def:%>сО1 SecondaryDeclination_SgDet-PossPx ;
+Sg+Ela+Def:%>стО1 SecondaryDeclination_SgDet-PossPx ;
+Sg+CompCx+Def:%>шка SecondaryDeclination_SgDet-PossPx ;

 K ;
+Pl+Nom+Indef:%>тЬ2 K ;
+Pl+Nom+Def:%>тнЕ3 K ; 
+Pl+Abl+Def:%>тнЕ3%>де K ;
+Pl+Abe+Def:%>тнЕ3%>с K ;
+Pl+CompCx+Def:%>тнЕ3%>шка K ;
+Pl+Gen+Def:%>тнЕ3%>нь K ;
+Pl+DatCx+Def:%>тнЕ3%>нень K ;
+Pl+Ill+Def:%>тнЕ3%>с K ;
+Pl+Ine+Def:%>тнЕ3%>сэ K ;
+Pl+Ela+Def:%>тнЕ3%>стэ K ;
+Pl+Tra+Def:%>тнЕ3%>кс K ;
+Pl+Prl+Def:%>тнЕ3%>ва K ;


LEXICON SecondaryDeclination_SgDet-PossPx
+Sg+Def:%>нть K ; 
 K ; 

LEXICON _ОШ-SecondaryDeclination_SgDet-PossPx
+Sg+Def:%>%{ОЕЭØ%}нть K ; 
 K ; 







LEXICON Nouns1_A
 Possessive-NomGen ;

+Abe:%>втО1мО1 _КУДО-Indef-DefSg-Poss ;
+Abl:%>дО1 _КУДО-Indef-DefSg-Poss ;
+CompCx:%>%{ОЕЭØ%}шка _КУДО-Indef-DefSg-Poss ;
+Prl:%>Г2а _КУДО-Indef-DefSg-Poss ;
+Tra:%>%{ОЕЭØ%}кс _ОШ-Indef-DefSg-Poss ;
+Ill:%>%{ОЕЭØ%}з _ОШ-Possessive-Before-K ;

+TempCx+Indef:%>не K ;
+Lat+Indef:%>%{ОЕЭØ%}в K ;

+DatCx+Indef:%>нень K ;
 DAT-PX ;
+Sg+Nom+Def:%>%{ОЕЭØ%}сь K ;
+Sg+Nom+Def:%>%{ОЕЭØ%}сь DER/PR_SG ;
+Sg+Gen+Def:%>%{ОЕЭØ%}нть K ;
+Sg+DatCx+Def:%>%{ОЕЭØ%}нтень K ;

+Gen+Indef:%>%{ОЕЭØ%}нь _КАЛ-Tenses-Obl ;
+Com+Indef:%>нек K-lak ;

+Err/Sub+Dial+Ill:%>з _ОШ-Possessive-Before-K ;
+Sg+Nom+PxSg3:%>зО1 K ;
+Sg+Nom+PxSg3:%>зО1 DER/PR_SG ;
+Err/Sub+Dial+Nom+PxPl1:%>нО1к _ЛАК-Tenses-NomSg ;
+Err/Sub+Dial+Gen+PxPl1:%>нО1к _ЛАК-Tenses-Obl ;
+Err/Sub+Dial+Sg+Nom+Def:%>сь K ;
+Err/Sub+Dial+Sg+Nom+Def:%>сь DER/PR_SG ;
+Err/Sub+Dial+Sg+Gen+Def:%>сть K ;
+Err/Sub+Dial+Sg+DatCx+Def:%>стень K ;


!! Nouns1S_A
!HERE

LEXICON Nouns1S_A_ReqVow
 Possessive-NomGen ;

+Ine:%>О1%>сО1 _КУДО-Indef-DefSg-Poss ;
+Ela:%>О1%>стО1 _КУДО-Indef-DefSg-Poss ;
+CompCx:%>%{ОЕЭØ%}шка _КУДО-Indef-DefSg-Poss ;
+Ill:%>О1%>с K ;

+Lat+Indef:%>%{ОЕЭØ%}в K ;


+Ine:%>сО1 _КУДО-Indef-DefSg-Poss ;
+Ela:%>стО1 _КУДО-Indef-DefSg-Poss ;
+CompCx:%>шка _КУДО-Indef-DefSg-Poss ;
+Tra:%>%{ОЕЭØ%}кс _ОШ-Indef-DefSg-Poss ;
+Ill:%>с K ;

+Err/Sub+Dial+Sg+Gen+Def:%>%{ОЕЭØ%}сть K ;
+Err/Sub+Dial+Sg+DatCx+Def:%>%{ОЕЭØ%}стень K ;

 DAT-PX ;

+Sg+Nom+Def:%>%{ОЕЭØ%}сь K ;
+Sg+Nom+Def:%>%{ОЕЭØ%}сь DER/PR_SG ;
+Sg+Gen+Def:%>%{ОЕЭØ%}нть K ;
+Sg+DatCx+Def:%>%{ОЕЭØ%}нтень K ;
+Gen+Indef:%>%{ОЕЭØ%}нь _КАЛ-Tenses-Obl ;



LEXICON Nouns1S_A_NoReqVow
+Abl:%>дО1 _КУДО-Indef-DefSg-Poss ;
+Prl:%>Г2а _КУДО-Indef-DefSg-Poss ;
+Abe:%>втО1мО1 _КУДО-Indef-DefSg-Poss ;

+Err/Sub+Dial+DatCx+PxSg2:%>теть K ;
+Err/Sub+Dial+DatCx+PxSg2:%>те K ;
+DatCx+PxSg2:%>тень K ;
+DatCx+PxSg1:%>нень K ;


+DatCx:%>нень K ;
+Com:%>нек K-lak ;
+TempCx:%>не K ;
!!This one would render voiceless "s"
+Err/Sub+Dial+Ill:%>з OBL-PX-FOLLOWED-BY-END-OR-FOC ;
+Err/Sub+Dial+Sg+Nom+PxSg3:%>зО1 K ;
+Err/Sub+Dial+Sg+Nom+PxSg3:%>зО1 DER/PR_SG ;
Nouns_Pl ;





LEXICON istja-Gen-Attributes
 K ; !!= * __@CODE@__ This means that stem is word final 
+Abl+Indef:%>дО1 K ;
+Indef+GenIndef:%>нь _КАЛ-Tenses-Obl ;
+OrthOld+GenIndef+Der/се:%>н%> N2Dem-SE ;
+OrthMorph+GenIndef+Der/се:%>нь N2Dem-SE ; ! _ь_ retension
+OrthPhon+GenIndef+Der/се:%>нце _КУДО-N2-Def-Declension ;


LEXICON chop-Gen-Attributes
 pek-Gen-Attributes ;

LEXICON pek-Gen-Attributes
 K ; !!= * __@CODE@__ This means that stem is word final 
+Abl+Indef:%>дО1 K ;
+Indef+GenIndef:%>%{ОЕЭØ%}нь _КАЛ-Tenses-Obl ;
+OrthOld+GenIndef+Der/се:%>%{ОЕЭØ%}н%> N2Dem-SE ;
+OrthMorph+GenIndef+Der/се:%>%{ОЕЭØ%}нь N2Dem-SE ; ! _ь_ retension
+OrthPhon+GenIndef+Der/се:%>%{ОЕЭØ%}нце _КУДО-N2-Def-Declension ;






LEXICON _КУДО-Possessives2-Ill
 _КУДО-Possessives2 ;
+NoVowX+Ill:%>з _ОШ-Possessive-Before-K ;

LEXICON _КАЛ-Possessives2
 Possessives2-PxSg1 ;
 Possessives2-PxSg2 ;
 Possessives2-PxSg3 ;
 Possessives2-PxPl1 ;
 Possessives2-PxPl2 ;
 Possessives2-PxPl3 ;

! These are possessor Indices for non-nominative singular
! NonNomSg
LEXICON _ОШ-Possessives2
 Possessives2-PxSg1 ;
 Possessives2-PxSg2 ;
 Possessives2-PxSg3 ;
 Possessives2-PxPl1 ;
 Possessives2-PxPl2 ;
 Possessives2-PxPl3 ;

LEXICON _КУДО-Possessives2
 Possessives2-PxSg1 ;
 Possessives2-PxSg2 ;
 Possessives2-PxSg3 ;
 Possessives2-PxPl1 ;
 Possessives2-PxPl2 ;
 Possessives2-PxPl3 ;

LEXICON _КАЛ-Determinatives1
+Der/се:%> N2Dem-SE ;
+OrthPhon+Der/се:%>це _КУДО-N2-Def-Declension ;
 _КАЛ-Tenses-Obl ;

LEXICON _КИТ-Determinatives1
+Der/се:%> N2Dem-SE ;
+OrthPhon+Der/се:%>це _КУДО-N2-Def-Declension ;
 _КИТ-Tenses-Obl ;

LEXICON _ЛАК-Determinatives1
 N2Dem-SE ; ! +Der/се:%>се _КУДО-N2-Def-Declension ;
+OrthPhon+Der/се:%>це _КУДО-N2-Def-Declension ;
 _ЛАК-Tenses-Obl ;


LEXICON _КУДО-Determinatives1
 N2Dem-SE ; !+Der/се:%>се _КУДО-N2-Def-Declension
+OrthPhon+Der/се:%>це _КУДО-N2-Def-Declension ;
 _КУДО-Tenses-Obl ;

LEXICON Dem_
+Pron+Dem: K ; 

LEXICON DEM_АЛЦЕ
+Spat+Der/се: _КУДО-N2-Def-Declension ;

LEXICON letter-F ! this needs fixing front vowel
+Prop:%- PROP_КУДО ;

LEXICON letter-B ! this needs fixing back vowel
+Prop:%- PROP_КУДО ;

LEXICON ACRONDELIMITER ! Here we need some kind of vowel harmony
    PROP_КУДО ;
:%: PROP_КУДО ;
:%- PROP_КУДО ;
:%' PROP_КУДО ;


!! !!POSSESSIVE marking followed by clitics

LEXICON _ОШ-Possessive-Before-K-or-K 
 K ;
 _ОШ-Possessive-Before-K ;

LEXICON _КУДО-Possessive-Before-K-or-K 
 K ;
 _КУДО-Possessive-Before-K ;


LEXICON _ОШ-Possessive-Before-K
:%>О1 Possessive-Before-K-noArrow ;

LEXICON _КУДО-Possessive-Before-K
:%> Possessive-Before-K-noArrow ;


! vim: set ft=xfst-lexc:

