! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ==================== !
! The Erzya dates !
! ==================== !


! 12.8.
! Умарьковонь 12-це чи
! Умарьковонь кемголмовоце чи

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG


LEXICON Root

! For each month: First the normative, then the non-normatives, then initial capital

@U.MONTH.1@:якшамковонь%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:якшамговонь%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:январень%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:январь% ковонь%_@U.MONTH.1@ 31DAYS ;

@U.MONTH.1@+Use/NG:Якшамковонь%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:Якшамговонь%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:Январень%_@U.MONTH.1@ 31DAYS ;
@U.MONTH.1@+Use/NG:Январь% ковонь%_@U.MONTH.1@ 31DAYS ;


@U.MONTH.2@:даволковонь%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:даволговонь%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:февралень%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:февраль% ковонь%_@U.MONTH.2@ 29DAYS ;

@U.MONTH.2@+Use/NG:Даволковонь%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:Даволговонь%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:Февралень%_@U.MONTH.2@ 29DAYS ;
@U.MONTH.2@+Use/NG:Февраль% ковонь%_@U.MONTH.2@ 29DAYS ;


@U.MONTH.3@:эйзюрковонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:эйзюрговонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:мартонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:март% ковонь%_@U.MONTH.3@ 31DAYS ;

@U.MONTH.3@+Use/NG:Эйзюрковонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:Эйзюрговонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:Мартонь%_@U.MONTH.3@ 31DAYS ;
@U.MONTH.3@+Use/NG:Март% ковонь%_@U.MONTH.3@ 31DAYS ;


@U.MONTH.4@:чадыковонь%_@U.MONTH.4@ 30DAYS ;
@U.MONTH.4@+Use/NG:априлень%_@U.MONTH.4@ 31DAYS ;
@U.MONTH.4@+Use/NG:априль% ковонь%_@U.MONTH.4@ 31DAYS ;

@U.MONTH.4@+Use/NG:Чадыковонь%_@U.MONTH.4@ 30DAYS ;
@U.MONTH.4@+Use/NG:Априлень%_@U.MONTH.4@ 31DAYS ;
@U.MONTH.4@+Use/NG:Априль% ковонь%_@U.MONTH.4@ 31DAYS ;


@U.MONTH.5@:панжиковонь%_@U.MONTH.5@ 31DAYS ;
@U.MONTH.5@+Use/NG:маень%_@U.MONTH.5@ 31DAYS ;
@U.MONTH.5@+Use/NG:май% ковонь%_@U.MONTH.5@ 31DAYS ;

@U.MONTH.5@+Use/NG:Панжиковонь%_@U.MONTH.5@ 31DAYS ;
@U.MONTH.5@+Use/NG:Маень%_@U.MONTH.5@ 31DAYS ;
@U.MONTH.5@+Use/NG:Май% ковонь%_@U.MONTH.5@ 31DAYS ;


@U.MONTH.6@:аштемковонь%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:аштемговонь%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:июнень%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:июнь% ковонь%_@U.MONTH.6@ 30DAYS ;

@U.MONTH.6@+Use/NG:Аштемковонь%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:Аштемговонь%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:Июнень%_@U.MONTH.6@ 30DAYS ;
@U.MONTH.6@+Use/NG:Июнь% ковонь%_@U.MONTH.6@ 30DAYS ;


@U.MONTH.7@:медьковонь%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:медьговонь%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:июлень%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:июль% ковонь%_@U.MONTH.7@ 31DAYS ;

@U.MONTH.7@+Use/NG:Медьковонь%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:Медьговонь%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:Июлень%_@U.MONTH.7@ 31DAYS ;
@U.MONTH.7@+Use/NG:Июль% ковонь%_@U.MONTH.7@ 31DAYS ;


@U.MONTH.8@:умарьковонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:умарьговонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:августонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:август% ковонь%_@U.MONTH.8@ 31DAYS ;

@U.MONTH.8@+Use/NG:Умарьковонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:Умарьговонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:Августонь%_@U.MONTH.8@ 31DAYS ;
@U.MONTH.8@+Use/NG:Август% ковонь%_@U.MONTH.8@ 31DAYS ;


@U.MONTH.9@:таштамковонь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:таштамговонь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:сентябрянь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:сентября% ковонь%_@U.MONTH.9@ 30DAYS ;

@U.MONTH.9@+Use/NG:Таштамковонь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:Таштамговонь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:Сентябрянь%_@U.MONTH.9@ 30DAYS ;
@U.MONTH.9@+Use/NG:Сентября% ковонь%_@U.MONTH.9@ 30DAYS ;


@U.MONTH.10@:ожоковонь%_@U.MONTH.10@ 31DAYS ;
@U.MONTH.10@+Use/NG:октябрянь%_@U.MONTH.10@ 31DAYS ;
@U.MONTH.10@+Use/NG:октября% ковонь%_@U.MONTH.10@ 31DAYS ;

@U.MONTH.10@+Use/NG:Ожоковонь%_@U.MONTH.10@ 31DAYS ;
@U.MONTH.10@+Use/NG:Октябрянь%_@U.MONTH.10@ 31DAYS ;
@U.MONTH.10@+Use/NG:Октября% ковонь%_@U.MONTH.10@ 31DAYS ;

@U.MONTH.11@:сундерьковонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:сундерьговонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:ноябрянь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:ноября% ковонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?

@U.MONTH.11@+Use/NG:Сундерьковонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:Сундерьговонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:Ноябрянь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?
@U.MONTH.11@+Use/NG:Ноября% ковонь%_@U.MONTH.11@ 30DAYS ; ! bytt NG?


@U.MONTH.12@:ацамковонь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:ацамговонь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:декабрянь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:декабря ковонь%_@U.MONTH.12@ 31DAYS ;

@U.MONTH.12@+Use/NG:Ацамковонь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:Ацамговонь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:Декабрянь%_@U.MONTH.12@ 31DAYS ;
@U.MONTH.12@+Use/NG:Декабря ковонь%_@U.MONTH.12@ 31DAYS ;



LEXICON 29DAYS
1:васенце B ;
2:омбоце B ;
3:колмоце B ;
4:нилеце B ;
5:ветеце B ;
6:котоце B ;
7:сисемце B ;
8:кавксоце B ;
9:вейксэце B ;
1%0:кеменце B ;
11:кемвейкееце B ;
11+Use/NG:кевейкееце B ;
12:кемкавтовоце B ;
12+Use/NG:кемгавтовоце B ;
13:кемколмовоце B ;
13+Use/NG:кемголмовоце B ;
14:кемнилеце B ;
15:кемветееце B ;
15+Use/NG:кеветееце B ;
16:кемкотовоце B ;
16+Use/NG:кемготовоце B ;
17:кемсисемгеце B ;
17+Use/NG:кемзисемгеце B ;
18:кемкавксовоце B ;
18+Use/NG:кемгавксовоце B ;
19:кемвейксэеце B ;
19+Use/NG:кевейксэеце B ;
2%0:комсеце B ;
21:комсьвейкееце B ;
22:комськавтовоце B ;
23:комськолмовоце B ;
24:комсьнилеце B ;
25:комсьветееце B ;
26:комськотовоце B ;
27:комсьсемгеце B ;
28:комськавксовоце B ;
29:комсьвейксэеце B ;

LEXICON 30DAYS
3%0:колмонькеменце B ;
3%0+Use/NG:колоньгеменце B ;
29DAYS ;

LEXICON 31DAYS
31:колмонькемень% васенце B ;
31+Use/NG:колоньгемень% васенце B ;
30DAYS ;

LEXICON B
%.:%_чи MONTH ;

LEXICON MONTH
1@U.MONTH.1@:@U.MONTH.1@ X ;
2@U.MONTH.2@:@U.MONTH.2@ X ;
3@U.MONTH.3@:@U.MONTH.3@ X ;
4@U.MONTH.4@:@U.MONTH.4@ X ;
5@U.MONTH.5@:@U.MONTH.5@ X ;
6@U.MONTH.6@:@U.MONTH.6@ X ;
7@U.MONTH.7@:@U.MONTH.7@ X ;
8@U.MONTH.8@:@U.MONTH.8@ X ;
9@U.MONTH.9@:@U.MONTH.9@ X ;
1%0@U.MONTH.10@:@U.MONTH.10@ X ;
11@U.MONTH.11@:@U.MONTH.11@ X ;
12@U.MONTH.12@:@U.MONTH.12@ X ;

LEXICON X
%.: # ;
! # ; ! sloppy punctuation
