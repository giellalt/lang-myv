! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

! ==================== !
! The Erzya dates !
! ==================== !


! 12.3.
! Умарьковонь 12-це чи
! Умарьковонь кемголмовоце чи

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG ! For substandard month names used in Oahpa
! They will be accepted but not generated.


LEXICON Root

! For each month: First the normative, then the non-normatives, then initial capital 
@U.MONTH.1@:якшамковонь% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:якшамговонь+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:январень+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:январь% ковонь+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:Якшамковонь+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:Якшамговонь+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:Январень+Use/NG% @U.MONTH.1@ 31DAYS ;
@U.MONTH.1@:Январь% ковонь+Use/NG% @U.MONTH.1@ 31DAYS ;


@U.MONTH.2@:даволковонь% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:даволговонь+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:февралень+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:февраль% ковонь+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:Даволковонь+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:Даволговонь+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:Февралень+Use/NG% @U.MONTH.2@ 29DAYS ;
@U.MONTH.2@:Февраль% ковонь+Use/NG% @U.MONTH.2@ 29DAYS ;

@U.MONTH.3@:эйзюрковонь% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:эйзюрговонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:мартонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:март% ковонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:Эйзюрковонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:Эйзюрговонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:Мартонь+Use/NG% @U.MONTH.3@ 31DAYS ;
@U.MONTH.3@:Март% ковонь+Use/NG% @U.MONTH.3@ 31DAYS ;

@U.MONTH.4@:чадыковонь% @U.MONTH.4@ 30DAYS ;
@U.MONTH.4@:априлень+Use/NG% @U.MONTH.4@ 30DAYS ;
@U.MONTH.4@:априль% ковонь+Use/NG% @U.MONTH.4@ 30DAYS ;
@U.MONTH.4@:Чадыковонь+Use/NG% @U.MONTH.4@ 30DAYS ;
@U.MONTH.4@:Априлень+Use/NG% @U.MONTH.4@ 30DAYS ;
@U.MONTH.4@:Априль% ковонь+Use/NG% @U.MONTH.4@ 30DAYS ;

@U.MONTH.5@:панжиковонь% @U.MONTH.5@ 31DAYS ;
@U.MONTH.5@:маень+Use/NG% @U.MONTH.5@ 31DAYS ;
@U.MONTH.5@:май% ковонь+Use/NG% @U.MONTH.5@ 31DAYS ;
@U.MONTH.5@:Панжиковонь+Use/NG% @U.MONTH.5@ 31DAYS ;
@U.MONTH.5@:Маень+Use/NG% @U.MONTH.5@ 31DAYS ;
@U.MONTH.5@:Май% ковонь+Use/NG% @U.MONTH.5@ 31DAYS ;

@U.MONTH.6@:аштемковонь% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:аштемговонь+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:июнень+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:июнь% ковонь+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:Аштемковонь+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:Аштемговонь+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:Июнень+Use/NG% @U.MONTH.6@ 30DAYS ;
@U.MONTH.6@:Июнь% ковонь+Use/NG% @U.MONTH.6@ 30DAYS ;

@U.MONTH.7@:медьковонь% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:медьговонь+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:июлень+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:июль% ковонь+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:Медьковонь+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:Медьговонь+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:Июлень+Use/NG% @U.MONTH.7@ 31DAYS ;
@U.MONTH.7@:Июль% ковонь+Use/NG% @U.MONTH.7@ 31DAYS ;

@U.MONTH.8@:умарьковонь% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:умарьговонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:августонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:август% ковонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:Умарьковонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:Умарьговонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:Августонь+Use/NG% @U.MONTH.8@ 31DAYS ;
@U.MONTH.8@:Август% ковонь+Use/NG% @U.MONTH.8@ 31DAYS ;

@U.MONTH.9@:таштамковонь% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:таштамговонь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:сентябрянь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:сентября% ковонь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:Таштамковонь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:Таштамговонь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:Сентябрянь+Use/NG% @U.MONTH.9@ 30DAYS ;
@U.MONTH.9@:Сентября% ковонь+Use/NG% @U.MONTH.9@ 30DAYS ;

@U.MONTH.10@:ожоковонь% @U.MONTH.10@ 31DAYS ;
@U.MONTH.10@:октябрянь+Use/NG% @U.MONTH.10@ 31DAYS ;
@U.MONTH.10@:октября% ковонь+Use/NG% @U.MONTH.10@ 31DAYS ;
@U.MONTH.10@:Ожоковонь+Use/NG% @U.MONTH.10@ 31DAYS ;
@U.MONTH.10@:Октябрянь+Use/NG% @U.MONTH.10@ 31DAYS ;
@U.MONTH.10@:Октября% ковонь+Use/NG% @U.MONTH.10@ 31DAYS ;

@U.MONTH.11@:сундерьковонь% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:сундерьговонь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:ноябрянь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:ноября% ковонь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:Сундерьковонь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:Сундерьговонь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:Ноябрянь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 
@U.MONTH.11@:Ноября% ковонь+Use/NG% @U.MONTH.11@ 30DAYS ; ! 


@U.MONTH.12@:ацамковонь% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:ацамговонь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:декабрянь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:декабря% ковонь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:Ацамковонь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:Ацамговонь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:Декабрянь+Use/NG% @U.MONTH.12@ 31DAYS ;
@U.MONTH.12@:Декабря% ковонь+Use/NG% @U.MONTH.12@ 31DAYS ;



LEXICON 29DAYS
1:васенце B ;
2:омбоце B ;
3:колмоце B ;
4:нилеце B ;
5:ветеце B ;
6:котоце B ;
7:сисемце B ;
8:кавксоце B ;
9:вейксэце B ;
1%0:кеменце B ;
11:кевейкееце B ;
12:кемгавтовоце B ;
13:кемголмовоце B ;
14:кемнилеце B ;
15:кеветееце B ;
16:кемготовоце B ;
17:кемзисемгеце B ;
18:кемгавксовоце B ;
19:кевейксэеце B ;
2%0:комсеце B ;
21:комсьвейкееце B ;
22:комськавтовоце B ;
23:комськолмовоце B ;
24:комсьнилеце B ;
25:комсьветееце B ;
26:комськотовоце B ;
27:комсьсисемгеце B ;
28:комськавксовоце B ;
29:комсьвейксэеце B ;


LEXICON 30DAYS
3%0:колоньгеменце B ;
29DAYS ;

LEXICON 31DAYS
31:колоньгемень% васенце B ;
30DAYS ;

LEXICON B
%.:% чи MONTH ;

LEXICON MONTH
1@U.MONTH.1@:@U.MONTH.1@ X ;
2@U.MONTH.2@:@U.MONTH.2@ X ;
3@U.MONTH.3@:@U.MONTH.3@ X ;
4@U.MONTH.4@:@U.MONTH.4@ X ;
5@U.MONTH.5@:@U.MONTH.5@ X ;
6@U.MONTH.6@:@U.MONTH.6@ X ;
7@U.MONTH.7@:@U.MONTH.7@ X ;
8@U.MONTH.8@:@U.MONTH.8@ X ;
9@U.MONTH.9@:@U.MONTH.9@ X ;
1%0@U.MONTH.10@:@U.MONTH.10@ X ;
11@U.MONTH.11@:@U.MONTH.11@ X ;
12@U.MONTH.12@:@U.MONTH.12@ X ;

LEXICON X
%.: # ;
! # ; ! sloppy punctuation
