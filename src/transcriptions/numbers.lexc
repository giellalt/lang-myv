!Distributed under the terms of the GNU General Public License version 2
! or any later version.


LEXICON Root
HUNDREDSM ;		! 200M
сядо:1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
сядо:1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
тёжа% :1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 30-99
TEENS ;			! 10-19
TWEENS ;		! 20-29 ! not just Hobbits
ONES ;			! 1-9

LEXICON HUNDREDSM
кавто:2  CUODIM ;
колмо:3 CUODIM ;
ниле:4 CUODIM ; ! Google: 459
вете:5 CUODIM ;
кото:6 CUODIM ;
сисем:7 CUODIM ;
кавксо:8 CUODIM ;
вейксэ:9 CUODIM ;

LEXICON CUODIM
сядт% : HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
:%0   ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
ке:1 TEENM ;

LEXICON TEENM
вейкее% :1     LOHKAIM ;
мгавтово% :2   LOHKAIM ;
мголмово% :3    LOHKAIM ;
мнилее% :4 LOHKAIM ;
ветее% :5   LOHKAIM ;
мготово% :6   LOHKAIM ;
мзисемге% :7 LOHKAIM ;
мгавксово% :8 LOHKAIM ;
вейксэе% :9   LOHKAIM ;

LEXICON LOHKAIM
MILJON ;

LEXICON TENSM
кемень% :1%0 MILJON ;
комсь% :2%0 MILJON ;
колонь:3    LUHKIEM ;
нилень:4 LUHKIEM ;
ведь:5   LUHKIEM ;
код:6   LUHKIEM ;
сизь:7 LUHKIEM ;
кавксонь:8 LUHKIEM ;
вейксэнь:9   LUHKIEM ;

LEXICON LUHKIEM
гемень% :%0 MILJON ;
гемень% :   ONESM ;

LEXICON ONESM
вейке% миллион% :1 OVERTHOUSANDS ;
кавто:2         MILJON ;
колмо:3          MILJON ;
ниле:4       MILJON ;
вете:5         MILJON ;
кото:6         MILJON ;
сисем:7       MILJON ;
кавксо:8       MILJON ;
вейксэ:9         MILJON ; 

LEXICON MILJON
% миллион% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
сядо% :1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
:%0 TEENST ;		!  10.000.000
:%0%0 ONEST ;		!   2.000.000
тёжа:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	!  x000000

LEXICON HUNDREDST
кавто:2   CUODIT ;	! 200000-299999
колмо:3    CUODIT ;	! 300000-399999
ниле:4 CUODIT ;	! 400000-499999
вете:5   CUODIT ;	! 500000-599999
кото:6   CUODIT ;	! 600000-699999
сисем:7 CUODIT ;	! 700000-799999
кавксо:8 CUODIT ;	! 800000-899999
вейксэ:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
сядт% : HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
вейке:%01 THOUSANDS ;
:%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
luhkie:1   TEENT ;
luhkie:1%0 THOUSANDS ;

LEXICON TEENT
вейке:1     LOHKAIT ;
кавто:2   LOHKAIT ;
колмо:3    LOHKAIT ;
ниле:4 LOHKAIT ;
вете:5   LOHKAIT ;
кото:6   LOHKAIT ;
сисем:7 LOHKAIT ;
кавксо:8 LOHKAIT ;
вейксэ:9   LOHKAIT ;

LEXICON LOHKAIT
THOUSANDS ;

LEXICON TENST
комсь:2   LUHKIET ;
колоньгемень% :3    LUHKIET ;
ниленьгемень:4 LUHKIET ;
ведьгемень:5   LUHKIET ;
кодгемень:6   LUHKIET ;
сизьгемень:7 LUHKIET ;
кавксоньгемень:8 LUHKIET ;
вейксэньгемень:9   LUHKIET ;

LEXICON LUHKIET
:%0 THOUSANDS ;
вейке% :1 THOUSANDS ;
ONEST ;

LEXICON ONEST
кавто% :2 THOUSANDS ;
колмо% :3 THOUSANDS ;
ниле% :4 THOUSANDS ;
вете% :5 THOUSANDS ;
кото% :6 THOUSANDS ;
сисем% :7 THOUSANDS ;
кавксо% :8 THOUSANDS ;
вейксэ% :9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
% тёжа% : THOUSAND ;
% тёжа% :%0%0%0 COMMA ;	   ! 1000


LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
:%0 TENS ;	   ! 1030-1099
:%0 TEENS ;	   ! 1010-1019
:%0 TWEENS ;	   ! 1020-1029
:%0%0 ONES ;	   ! 1001-1009

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
сядо% :1 HUNDRED ;
сядоце:1  CUODAAT ;

LEXICON HUNDREDS
кавто:2   CUODI ;
колмо:3    CUODI ;
ниле:4 CUODI ;
вете:5   CUODI ;
кото:6   CUODI ;
сисем:7   CUODI ;
сисемзядт+Use/NG% :7 HUNDRED ;
сисемзядоце+Use/NG:7 CUODAAT ;
кавксо:8 CUODI ;
вейксэ:9   CUODI ;

LEXICON CUODI
сядт% :   HUNDRED ;
сядоце:  CUODAAT ;


LEXICON HUNDRED
      TENS ;
      TEENS ;
      TWEENS ;
:%0   ONES ;
:%0%0 COMMA ;

LEXICON CUODAAT
:%0%0%.    COMMA ;

LEXICON TEENS
 CARDINAL-TEENS ;
 ORDINAL-TEENS ;

LEXICON TWEENS
комсь:2%0 COMMA ;
комсеце:2%0%. COMMA ;
комсь:2 ONES-TW ;


LEXICON TENS
колмонь:3 LUHKIE_3-9_morph ;
колонь:3 LUHKIE_3-9 ;
нилень:4 LUHKIE_3-9 ;
нилень:4 LUHKIE_3-9_morph ;
ветень:5 LUHKIE_3-9_morph ;
ведь:5 LUHKIE_3-9 ;
котонь:6 LUHKIE_3-9_morph ;
код:6 LUHKIE_3-9 ;
сисьмень:7 LUHKIE_3-9_morph ;
сизь:7 LUHKIE_3-9 ;
кавксонь:8 LUHKIE_3-9 ;
кавксонь:8 LUHKIE_3-9_morph ;
вейксэнь:9 LUHKIE_3-9 ;
вейксэнь:9 LUHKIE_3-9_morph ;

LEXICON LUHKIE_3-9
гемень:%0 COMMA ;
геменце:%0%. COMMA ;
гемень% : ONES ;

LEXICON LUHKIE_3-9_morph
кемень+Use/SpellNoSugg:%0 COMMA ;
кеменце+Use/SpellNoSugg:%0%. COMMA ;
кемень+Use/SpellNoSugg% : ONES ;


LEXICON CARDINAL-TEENS
кемень:1%0 COMMA ;
кевейкее:11 COMMA ;
кемгавтово:12 COMMA ;
кемголмово:13 COMMA ;
кемнилее:14 COMMA ;
кеветее:15 COMMA ;
кемготово:16 COMMA ;
кемзисемге:17 COMMA ;
кемгавксово:18 COMMA ;
кевейксэе:19 COMMA ;

LEXICON ORDINAL-TEENS
кеменце:1%0%. COMMA ;
кевейкееце:11%.     COMMA ;
кемгавтовоце:12%.   COMMA ;
кемголмовоце:13%.   COMMA ;
кемнилееце:14%. COMMA ;
кеветееце:15%.   COMMA ;
кемготовоце:16%.   COMMA ;
кемзисемгеце:17%.    COMMA ;
кемгавксовоце:18%.   COMMA ;
кевейксэеце:19%.    COMMA ;

LEXICON ONES-TW
 CARDINAL-TW ;
 ORDINAL-TW ;

LEXICON CARDINAL-TW
вейкее:1 COMMA ;
кавтово:2 COMMA ;
колмово:3 COMMA ;
нилее:4 COMMA ;
ветее:5 COMMA ;
котово:6 COMMA ;
сисемге:7 COMMA ;
кавксово:8 COMMA ;
вейксэе:9 COMMA ;

LEXICON ORDINAL-TW
вейкееце:1%.     COMMA ;
кавтовоце:2%.   COMMA ;
колмовоце:3%.   COMMA ;
нилееце:4%. COMMA ;
ветееце:5%.   COMMA ;
котовоце:6%.   COMMA ;
сисемгеце:7%.    COMMA ;
кавксовоце:8%.   COMMA ;
вейксэеце:9%.    COMMA ;

LEXICON ONES
CARDINAL ;
ORDINAL ;

LEXICON CARDINAL
вейке:1 COMMA ;
кавто:2 COMMA ;
колмо:3 COMMA ;
ниле:4 COMMA ;
вете:5 COMMA ;
кото:6 COMMA ;
сисем:7 COMMA ;
кавксо:8 COMMA ;
вейксэ:9 COMMA ;

LEXICON ORDINAL
васенце:1%.     COMMA ;
омбоце:2%.   COMMA ;
колмоце:3%.   COMMA ;
нилеце:4%. COMMA ;
ветеце:5%.   COMMA ;
котоце:6%.   COMMA ;
сисемце:7%.    COMMA ;
кавксоце:8%.   COMMA ;
вейксэце:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;

LEXICON ENDLEX
 # ;
