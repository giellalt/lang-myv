!! !!!The Erzya morphophonological/twolc rules file 

Alphabet
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я  
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я


! Nominal stem vowels that alternate 
! work with "+VLoss" and "+NoVLoss" tags
!
!У2:о
!У2:0
!И2:е
!И2:ь
!И2:0

!! !!Special letters in the root that might be useful in dialect research and etymology later
Ь3:0  !!= * __@CODE@__ арсемс:арсе  arśems  vs арсемс:арЬ3се  aŕśems 
Ӓ3:э  !!= * __@CODE@__ эрямс:Ӓ3ря 
Ӓ4:е  !!= * __@CODE@__ пелемс:пӒ4ль

%^Ь2ZERO:0  !!= * __@CODE@__ removes stem-final soft sign

!Suffix final variant vowel for hard vs soft
!Ь2:ь !
Ь2:0 !

! verbal stem consonant that appear in PretI 3sg
!Т1:0 !

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !

! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
!А2:а !А2
А2:я !А2
!Ы2:и !
Ы2:ы !

! derivational suffix protovowel  "mo" and "me"
О1:е !
О1:э !
О1:о !
О1:0 !

! derivation suffix-initial archivowel 
%{ОЕЭØ%}:е
%{ОЕЭØ%}:э
%{ОЕЭØ%}:о
%{ОЕЭØ%}:0

Е3:е ! тне тнэ

! clitic proto consonant
Г1:г !
Г1:к !
Г1:0 !



! Prolative Ga
Г2:г !
Г2:к !
Г2:в !


! Imperative clitic proto consonant
Г4:г !
Г4:к !
Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
К1:к !

!

! connegative suffix rendering vowel
! панемс пань>%{ОЕЭØ%}К3 : пан00е0
К3:0 !

! Stem final on Imperative Verbs
!К4:0 !

! Diminutive in ке and не нэ Н1е
Н1:к
Н1:н

! Some verbs need a marker for vowel placement
!Used before Conjuntive ending
!V1:е !
!V1:э !2012-03-13 look to О1:
!V1:о !
! I2 and U2 adjectives and nouns need this with their "t" and "s"
!V2:0 

TLoss:0
SLoss:0
KLoss:0
VLoss:0
%^WLoss:0
NoVLoss:0

%- !Hyphen with constructions like "-buti"
%>
# 
;


Sets

Vows = а о у ы э я ё ю и е 
       А О У Ы Э Я Ё Ю И Е ;
verbStemVows = а о э я ё е 
	       А О Э Я Ё Е ;
verbStemVowStrong = а о э я ё е 
		    А О Э Я Ё Е ;
noGlideVow = а о у ы э 
	     А О У Ы Э ;
glideVow = я ё ю и е 
	   Я Ё Ю И Е ;
BTV = а о у я ё ю 
      А О У Я Ё Ю ; ! Back Trigger Vowels
FTV = ы и е э 
      Ы И Е Э ; ! Front Trigger Vowels

Cns = б в г д ж з й к л м н п р с т ф х ц ч ш щ 
      Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
NPC = б в г д ж з к л м н п р с т ф х ц ч ш щ 
      Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ; ! Nonpalatal Consonants
CnsVoiced = б в г д ж з й л м н р 
	    Б В Г Д Ж З Й Л М Н Р ;
CnsVoicedDent = д з л н р 
	      	Д З Л Н Р ;
CnsVoicedNonDent = б в г ж й м 
		   Б В Г Ж Й М ;
CnsVoiceless = к п с т ф х ц ч ш щ 
	       К П С Т Ф Х Ц Ч Ш Щ ;
CnsVoicelessDent = с т ц 
		   С Т Ц ;
CnsVoicelessNonDent = к п ф х ч ш щ 
		      К П Ф Х Ч Ш Щ ;
CnsNonDent = б в г ж к м п ф х ч ш щ 
	     Б В Г Ж К М П Ф Х Ч Ш Щ ;
CnsDent = д з л н р с т ц 
	  Д З Л Н Р С Т Ц  ;
CnsDentNoL = д з н р с т ц 
	     Д З Н Р С Т Ц  ;

Letters = Vows Cns ь ъ Ь Ъ ;

Definitions

! Front Trigger Consonants (less ль)
FTC = [ й: | д ь: | з ь: | н ь: | р ь: | с ь: | т ь: | ц ь: ] ;

! All Consonants
AC = [ FTC | л ь: | NPC ] ;

! Left Front Context Soft
LFCS = [ :FTV AC* CnsNonDent | :FTV AC* FTC |  BTV FTC AC* CnsNonDent  | FTC | й: | л ь: ] ;

! Left Front Context Hard
LFCH = [ :FTV AC* CnsDent | BTV FTC  AC* CnsDent+ | [ BTV л ь [ к с ] ] ] ;

! Front Trigger
FT = [ LFCS | LFCH | л ь: | :FTV ] ; 
! This should be followed by a morpheme boundary marker %>

LFCHMore =  [ :FTV | FTC | л ь: ]  %> [ %{ОЕЭØ%}: н з | с ( т ) ] ;
! [ LFCS | LFCH | :FTV ]  \[ #: | :BTV | BTV: ]* %> [ [ %{ОЕЭØ%}:е | %{ОЕЭØ%}:0 ] м [ с | с т ] | [ %{ОЕЭØ%}:е | %{ОЕЭØ%}:0 ] ( н ) з | з | с | с т ] ;
!



! Left Back Context
LBC = [# [Cns: (ь) ]* BTV NPC* | :BTV \[ #: | :FTV | FTV: | ь: | й: ]* |BTV л ь [ ф | т | д ] ]  ;
!   [ :BTV ( %> %{ОЕЭØ%}:0 ) NPC* ( %> )  :NPC+ |BTV л ь [ ф | т | д ] ]  ;


NoFront = [ %>  | :BTV | BTV: | :NPC | О1: | %{ОЕЭØ%}: ]* ;

!LBCCns = LBC \[ #: | FT | :FTV | FTC | л ь: | ь: | й: ]* :NPC+ ;

PossX = [ м | н Ь2: | т Ь2: | н т Ь2: | ( н ) з О1: | н О1: к | м О1: к | н к | ( н ) с т | с к ] ;

CaseX = [ с ь: | [ н | с ] т ь | [ н | с ] т е н ь | н е н ь | д: О1: | с ( т ) О1: | з | с | в | к с | Г2: а | в: т О1: м О1: |  ш к а | н е: | н е: к | н ь: ] ;

CasePossX = [ т е [ т | н ] ь | н с т [ О1: | э н ь ] | з %> %{ОЕЭØ%}: PossX ] ;

InfX = [ м с | м О1: | м А1: | м д О1: | м г а | м с О1: | м с т О1: | з ь: ] ;

ConjX = [ ( з ) м а к | ( з ) м а м | ( з ) м и з ь | н з а т | с с О1: | з О1: | с т | з т | К1: Ь2: | К3: | д О1: | (в: ( О1: ) ) л ь: ( т ь | с е ) | (в: ( О1: ) ) л и [[ н | т | т ( и | е ) н ] ь | д е | н [ е | и ] к | н к | з ь: | к | я | ( н ) з е | н з [ е | и ] т ь | [ м | д ] [ и | е ] з ь | м [ и | е ] [ м | к ]  ] | т с ( т ) ь   ] ;
Back =  :* [ :BTV \[ :FTV | :й | :ь  ]* ( л ь ) [NPC]]  ;
! Word-final elements
WFE = [ [ PossX | CaseX | CasePossX  | ConjX | т н е: ] [  #:  | %> [  Г1: | :Cns ] |  %- ]  | InfX |  #: | %> [  Г1: | :Cns ] |  %- ] ;

SIVV = [ PossX | InfX | м с (т) О1: | м (к) а | в | н е н ь | т е н ь | т е т ь | н с т э н ь | н с т О1: | н з а | н т е н ь | с т е н ь | л ь | л и | к с | ш к а | з т | з с | з а | з м | с | з  %>  %{ОЕЭØ%}: | т ь: | н т ь: | н ь: | в:  О1:  л | т %> с ь | т %> с т ь | К3: | д О1: | с ь ] ;
! Suffix-Initial Variable Vowel



Rules


!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
!! __@RULENAME@__  
 X:Y <=> [ Cns | Vows ] _  %> [А2:я|А2:а [ н | т ]| Ы2:] ;
!                   Cns  _ К4:0 %>  Ы2: ;
          where X in (а о э ы у я ё е и ю)
                Y in (0 0 0 0 0 0 0 0 0 0) matched ;

!€ цёра%>А2н
!€ цёр00ан


!

! Deverbal noun in ОмА
"A1:o"
!! __@RULENAME@__  
А1:о <=>  [ а |я ] %> %{ОЕЭØ%}:0  м  _ ;

"Y2:yi"
!! __@RULENAME@__  
Ы2:и <=> [ я:0 | ё:0 | е:0 | ю:0 | ь: | й: | CnsNonDent (noGlideVow:0)]  %> _ ;



"O1:ye"
О1:е <=> [ LFCS | FTC | л ь: ] %> _ [ ( %> ) WFE | %> %{ОЕЭØ%}:0 | т %> с | %> :Cns ] ;
         [ [ LFCS | л ь: | й:0 ] %> | :FTV ( %> ) ] [ ( %{ОЕЭØ%}:0 | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э ) д: | [ %{ОЕЭØ%}:0 | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э ] м ( д ) | в: т [ О1:е | О1:э ] м ] _ [  #: |  %- | %> [  Г1: | :Cns | %{ОЕЭØ%}:0 ] ]  ;
         [ LFCS | FTC | л ь: | :FTV ] %> [в:0 | в ] т _ м О1: ;
         FT %>  [%{ОЕЭØ%}:0 | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э ][в:0 | в ] _ л ь ;
         FT %> ( %{ОЕЭØ%}:0 | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э )  м  _ к  ;
         [  LFCS | :FTV] %> ( %{ОЕЭØ%}:0 | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э )  н  _ к  ;
        LFCS  %> _ [ #: | %> [ Г1: | :Cns | %{ОЕЭØ%}:0 ] ] ; !вере, икеле
!€# сай%>О1%>дО1
!€0 са00е0де
	 [ LFCS | FTV | FTC | л ь: ] [ м | м б | т ь: | д ь: | к | г | в ш ] %> _ [ #: | %> [ %{ОЕЭØ%}:0 | с | т | н | ш | к | д | :в ] ] ; ! Пильге


!
"O1:e"
!! __@RULENAME@__  
О1:э <=>   :FTV %> [ с ( т ) | %{ОЕЭØ%}:0 ( н )  з | %{ОЕЭØ%}:0 м [ с | с т ] ] _ [  #: |  %- | %> [ Г1: | %{ОЕЭØ%}:0 | :Cns ] ]  ;
          LFCH %> [ с ( т ) | [ %{ОЕЭØ%}:э |  %{ОЕЭØ%}:0 ] з | %{ОЕЭØ%}:э н   з | %{ОЕЭØ%}:э м [ с | с т ] ] _ [  #: |  %- | %> [ Г1: | %{ОЕЭØ%}:0 | :Cns ] ]  ;
          [ :FTV %> %{ОЕЭØ%}:0 | %{ОЕЭØ%}:э | %{ОЕЭØ%}:е ] [ н   з | м [ с | с т ] ] _ [  #: |  %- | %> [ Г1: | %{ОЕЭØ%}:0 | :Cns ] ]  ;
          LFCS  %> [ с ( т ) | %{ОЕЭØ%}:е  н  з |  [ %{ОЕЭØ%}:е | %{ОЕЭØ%}:0 ]  з | [ %{ОЕЭØ%}:е | %{ОЕЭØ%}:0 ]  м [ с | с т ] ] _ [  #: |  %- | %> [ Г1: | %{ОЕЭØ%}:0 | :Cns ] ]  ;
          # [ м о | т о | с о | м и | т ы | с ы ] н ь:  %> к с %> _  Cns ;
          LFCH %> ( д: ) _ [  #: |  %- | %> [ Г1: | :Cns | %{ОЕЭØ%}:0 ] ]  ;
          LFCH %> в: т _ м О1:  ;
          LFCH %> н _ к  ;
! LFCHMore _ [  #: |  %- | %> [ Г1: | %{ОЕЭØ%}:0 | :Cns ] ]  ;
!          FT %> [ %{ОЕЭØ%}: ( н ) з | с ( т ) ] _ [ # |  %> [ :Cns | Г1: ]] ;
!2013-04-16!  [ LFCH | [[ LFCH | LFCS ] %> | :FTV ( %> ) ] з  ] %> _ [ ( %> ) WFE | %> %{ОЕЭØ%}:0 | %> :Cns ]; ! move to O2:e


"O1:o"
!! __@RULENAME@__  
О1:о <=> LBC _ [  #: | %- | %> [ Г1: | :Cns | %{ОЕЭØ%}:0 ] ] ;
        [ LBC  %> | :BTV ( %> ) ] [ д: | ( с с:0 %> ) с ( т ) | [ %{ОЕЭØ%}:о | %{ОЕЭØ%}:0 ] ( н )  з | [ %{ОЕЭØ%}:о | %{ОЕЭØ%}:0 ]  м ( д | с | с т ) | в: т О1:о м ] _ [  #: | %- | %> [ Г1: | :Cns | %{ОЕЭØ%}:0 ] ] ;
         [ LBC  %> | :BTV ( %> ) ] в: т _ м О1: ;
        [ LBC  %> | :BTV ( %> ( %{ОЕЭØ%}:0 )) ] [ м | н ] _ к ;
        LBC  %> _ [ #: | %> [ Г1: | :Cns | %{ОЕЭØ%}:0 ] ] ; ! ало
         [ LBC - [ :* в ]  | :BTV ] %> [%{ОЕЭØ%}:0 |%{ОЕЭØ%}:о] в _ л ь ;
	 #: Cns*  BTV [ [п|т|к|ц|ч] | м б | [в|с|ш] т | [р|л|н] д | [с|ш] к | [в|р|л|н] г | [т|д|р|л] м | в ш ] %> _ [ #: | %> [ %{ОЕЭØ%}:0 | с | т | н | ш | к | д | :в ] ] ; ! пандо


"O1:0"
!! __@RULENAME@__  
О1:0 <=>   _  %> [А2:а|А2:я] ;
         :Vows :0* в: _ л  и ;
!          #: Cns* Vows Letters* :Vows в %^WLoss:0  %> %{ОЕЭØ%}: в: _ л  и ;
!:BTV в %^WLoss:0  %> _ в: ( О1: ) л ;

"O2:ye"
%{ОЕЭØ%}:е <=>  [ LFCS | й:0 ] %> _ [ SIVV | к ш н | # | %> Г1: ] ;

"O2:e"
!! __@RULENAME@__  
%{ОЕЭØ%}:э <=>  [ LFCH | О1:е %> з ] %> _ [ SIVV | к ш н | # | %> Г1: ] ;
!         [ LFCH | FT %> ( %{ОЕЭØ%}: ) з  ] %> _ SIVV ;

"O2:o"
!! __@RULENAME@__  
%{ОЕЭØ%}:о <=>
         # [Cns: (ь) ]* BTV NPC+   %> _ [ SIVV | к ш н | # | %> Г1: ] ;
         :BTV \[ #: | :FTV | FTV: | ь: | й: ]* NPC+   %> _ [ SIVV | к ш н | # | %> Г1: ] ;
!         LBC  %> _ [ SIVV | # | %> Г1: ] ;
	 #:  Letters* BTV л ь [ ф | т | д ] %> _ [ SIVV | к ш н | # | %> Г1: ] ;
! [ LBC | :BTV ( %> ) з ] %> _ [ ( %> )  WFE | т %> с | %> :Cns ] ;
       	     :BTV в %^WLoss:0  %> _ в: ( О1: ) л ;
!€ эряв%>%^WLoss%>%{ОЕЭØ%}вО1ль
!€ эряв00000оль

"O2:0"
!! __@RULENAME@__  
%{ОЕЭØ%}:0 <=>   :Vows %> _ [ SIVV | # | %> Г1: ] ;


!!Changes in the suffix vowels
!"A2:ya"
!А2:я <=> [ FTC | :FTV | ь | :FTV NPC | :FTV Cns* CnsNonDent | LFCS :FTV ( (н) т ( Ь2: ) | н Ь2: ) ]  %> т _ [ д | н | м ] ;
!2013-03-16! [ FTC | :FTV | ь | :FTV NPC | :FTV Cns* CnsNonDent | LFCS :FTV ( (н) т ( Ь2: ) | н Ь2: ) ]  %> [ т | TLoss:0 ( %> ) т:0 ] _  [ д | н | м ] ;
!         [ CnsDent [  ь:0 |я:0 | ё:0 | е:0 | и:0 | ю:0 ]| Vows [ я:0 | ё:0 | е:0 | ю:0 | й:0 ] | [ LFCS ( :FTV ) %>  | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э ] ( (н) т  Ь2:0  | н Ь2: )  ]  %>  _ [ т | н ] ;


!HERE
"A2:a"
!! __@RULENAME@__  
А2:а <=>  [ LBC | :BTV ] %> т  _  [ д | н ] ;
! [[ :BTV | LBC ] %> т | %> ( %{ОЕЭØ%}: ) м с %> т ] _  [ д | н ] ;
!2013-03-16! [[ :BTV | LBC ] %> [ т | TLoss:0 ( %> ) т:0 ] | %> ( %{ОЕЭØ%}: ) м с %> т ] _  [ д | н ] ;
         [CnsDent ( э:0 | ы:0 | а:0 | О1: | о:0 | у:0 )|  в: т О1: м  О1:0 | CnsNonDent ( Vows:0 ) | [ LBC ( :BTV ) %>  |  %{ОЕЭØ%}:о ]  ((н) т Ь2:0 | н Ь2: )  ] %>  _  [ т | н ] ;



"j:0"
!! __@RULENAME@__  
й:0 <=> Vows _ %> [  Ы2: | А2:я | %{ОЕЭØ%}:е | %{ОЕЭØ%}:0 | О1:е |О1:0 ] ;
! [  %> Ы2:и | %> [ А2:я | %{ОЕЭØ%}:е [ %> д | :Cns | К3:0 ]] ] ;
! € сай>О1>дО1: са00е0де

!
!18.04.2007 Adding "U2:" and contemplating environment "%> т н _"
!
"Е3:э always" 
!! __@RULENAME@__ %> т н _ 2013-02-23
Е3:э <=     [ CnsDent ]  %>  т н _ [ #: | %- | %> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"Е3:э sometimes" 
!! __@RULENAME@__ %> т н _ 2013-02-23
Е3:э =>     [ CnsDent | BTV Cns* CnsNonDent ]  %>  т н _ [ #: | %- | %> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"ye:e always" 
!! __@RULENAME@__  
е:э <=>
          CnsDent (О1:0) %>  :н _ [[  #: | %-| %> Г1:0 а:я]| н ь: | к] ;
!          [[[Back - [ :* Vows]] %> | CnsDent]  %> ] т н _ [ #: |  %- |%> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];


!"ye:e sometimes"
!е:э =>
!          [CnsDent|CnsNonDent] ( О1:0 ) %>  н _ [[  #: | %- | %> Г1:0 а:я ] | н ь: | к ] ;
!          [ CnsDent | CnsNonDent | м:0 ]  ( О1:0 ) %> ( VLoss:0 ) ( TLoss:0 ) т н _ [ #: | %- | %> [ Г1:0 а:я | т | л | Г2:в а | в | д | к | н | с | ш ]];


! вазнэ, верьгизнэ, ривезьке
"Н1:н"
!! __@RULENAME@__  
Н1:н <=> [ в | д | й | л | м | н | р ] ( ь: ) %> _ е:  ;

"Н1:к"
!! __@RULENAME@__  
Н1:к <=> [ б | г | к | п | с | т | ф | х | ц | ч | ш | щ ] ( ь: ) %> _ е:  ;

"a:ya"
!! __@RULENAME@__  
а:я  <=> :Vows ((%>) К3:0 )  %>  Г1:0 _  ;


!Changes in suffix consonants
"v:0"
!! __@RULENAME@__  
в:0 <=> [ :Cns  ( ь: ) ] %> _ т [О1:о|О1:е|О1:э] м [О1:о|О1:е|О1:0] ;
        #: Cns* Vows Letters* :Vows в %^WLoss:0 %> %{ОЕЭØ%}: _ ( О1: ) л  ;

!Depricated due to 
!ERROR: Label not found in 'EQV_LABELS' (114) **
!Solved in several contexts by declaring both lexical
!and surface forms in the alphabet.
"G1:0"
!! __@RULENAME@__  
Г1:0 <=> [ :Vows ((%>) К3:0 )  %> | К1: Ь2:0 %> KLoss:0 | :к %> KLoss:0 ]  _ а:я ;

"G1:g"
!! __@RULENAME@__  
Г1:г <=> [CnsVoicedDent (:ь|Ь2:0)| й | [ CnsVoicedNonDent - г ] ( :ь )] %>  _ а ;

"G1:k"
!! __@RULENAME@__  
Г1:к <=>  [ CnsVoicelessDent ( :ь | Ь2:0 | Ь2:ь ) | CnsVoicelessNonDent | г | К1: Ь2: | К4:0 ]  %> _ а  ;
          

!Prolative
"G2:g"
!! __@RULENAME@__  
Г2:г <=> [ CnsVoicedDent ( :ь )| [ CnsVoicedNonDent - г ] ( :ь ) ] %> _ а ;

"G2:v"! 
Г2:в <=> [ :Vows (:0) %> | :Vows ] _ а [ #: | %- |%> [ Г1:0 |  %{ОЕЭØ%}:  SIVV |т | н | м | с | д: | в | к с | л ] ];


"G2:k"
!! __@RULENAME@__  
Г2:к <=>  [ CnsVoicelessDent ( :ь )| CnsVoicelessNonDent ( :ь )| г ] %> _ а [ #: |  %- |%> [ Г1:0 | %{ОЕЭØ%}: SIVV | т | н | с | д: | в | к с | л ] ] ;
           Vows CnsVoiceless  %> VLoss:0 _ а ;

! 2012-08-07
"G4:g" !Imperative Clitics
Г4:г  <=> з ь %> _ а ;


"G4:0"
!! __@RULENAME@__  
Г4:0  <=>
           [ К1: Ь2: | к ] %> _ а ;
  [О1:е|О1:о] %> _ а: ;

"G4:k"
!! __@RULENAME@__  
Г4:к  <=>  #: [и л я|у ж о|а з ё|а д я|а й д а] %> _ а  ;


!"m:0" 
!м:0 <=> 
! BTV  _ У2:0  %>  VLoss: [ т | т н е:э ] ;
!        т (ь) _ [ У2: ]  %>  VLoss: [с (т) [О1:э|О1:о|О1:0]|с [  #: | %-| %> [Г1:0|Г1:к]]] ;



!! imperative suffix
"K1:t"
!! __@RULENAME@__  
К1:т <=>   Cns ( ь: )  %> _ Ь2: ;



! Suffix final hard vs soft
"U4:y"
!! __@RULENAME@__  
Ь2:ь <=>  [ [ [ LFCS  %> | :FTV  %> ( %{ОЕЭØ%}:0 ) | ь:  %> ] | %{ОЕЭØ%}:е | %{ОЕЭØ%}:э | О1:е | О1:э ] [ н | ( н | в ) т ] |  [ LFCS | ь: | :FTV ] %> К1:т ]  _  [ #: | %- | %>  Г1: ] ;


!!!TLoss sloppy
!"t:0"
!т:0 <=> [ [ т | К1:т ] ( Ь2: ) %> TLoss:0 | т У2:0 %> VLoss:0 TLoss:0 ] ( %> ) _ ;

!!SLoss s s > s = s 0 0 s
"s:0"
!! __@RULENAME@__  
с:0 <=> с _ %>  с ;
! с _ %> SLoss:0 с ;

!€ класс%>с
!€ клас00с


!Ablative d/t
"d:t"
!! __@RULENAME@__  
д:т  <=> [ CnsVoiceless | д | г] (ь:0)  %> _ [О1:е|О1:э|О1:о] ;
!€# кедь%>дО1
!€0 кед00те
!         т [ И2:0 ] %> VLoss:0 _ [О1:е|О1:э|О1:о] ;
!Word final Vowels


"y:0"
!! __@RULENAME@__  
ь:0 <=> Cns _ %^Ь2ZERO: ;
!!кель+N+Sg+Gen+Def: __language__
!€# кель%^Ь2ZERO%>енть
!€0 кел000енть
     CnsDent  _  %> [ А2:я | Ы2: | %{ОЕЭØ%}:е | О1:е ];
              CnsDentNoL _ %> [в:0 т О1:е м [О1:е|О1:0] | К1: | Н1:н | н | д: | ц е: [ #: | %- | %> ] | т | с (т) [ ь: | е: ]] ;



!"U2:0"
!У2:0 <=>  _ %> VLoss:0 [ ( TLoss:0 ) т [ Ь2:0 | Ь2:ь | А2:а ] | ( TLoss:0 ) т н е: |с (т) [О1:о|О1:0]| с [ %> | #: | %- ] ] ;
!          _ %> VLoss:0 д: [ О1:о ] ;

!"U2:o"
!У2:о  <=>  _ [ %> [ (н) т Ь2:0 | н Ь2:0 | т н е: | с ь: | с (т) [ О1:о | О1:0 ] | с [ %> |  #: |  %- ] | з %> %{ОЕЭØ%}: | л  |  (н) з [ О1:о | О1:0 ] | ш к а | к с | н | в | Г2:в | д | с т [ %> |  #: | %- ] ] | Г1:0|  #: | %- ] ;


!Disallow

"Disallow TLoss after non-t"
!! __@RULENAME@__  
TLoss:0 /<= [[ :* Letters ] - [ :* :т ] ] %> _ ;

"Disallow KLoss after non-k"
!! __@RULENAME@__  
KLoss:0 /<= [[ :* Letters ] - [ :* :к ] ] %> _ ;

"Disallow SLoss after non-s"
!! __@RULENAME@__  
SLoss:0 /<= [[ :* Letters ] - [ :* :с ] ] %> _ ;

"Disallow %^WLoss after non-v"
!! __@RULENAME@__  
%^WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %> _ ;

"Disallow Н1:н after Letters"
!! __@RULENAME@__  
Н1:н /<= Letters _ ;
