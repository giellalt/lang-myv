!! !!!The Erzya morphophonological/twolc rules file 

Alphabet
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я  
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

ҥ Ҥ і І ѳ Ѳ ! Pre-Soviet 1930s letters 

! Nominal stem vowels that alternate 
! work with "+VLoss" and "+NoVLoss" tags
!
!У2:о
!У2:0
!И2:е
!И2:ь
!И2:0

!! !!Special letters in the root that might be useful in dialect research and etymology later
Ь3:0  !!= * __@CODE@__ арсемс:арсе  arśems  vs арсемс:арЬ3се  aŕśems 
Ӓ3:э  !!= * __@CODE@__ эрямс:Ӓ3ря 
Ӓ4:е  !!= * __@CODE@__ пелемс:пӒ4ль

%^Ь2ZERO:0  !!= * __@CODE@__ removes stem-final soft sign

!Suffix final variant vowel for hard vs soft
!Ь2:ь !
Ь2:0 !

! verbal stem consonant that appear in PretI 3sg
!Т1:0 !

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !

! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
!А2:а !А2
А2:я !А2
!%{иы%}:и !
%{иы%}:ы !

! derivational suffix protovowel  "mo" and "me"
О1:е !
О1:э !
О1:о !
О1:0 !

! variation in э о 0 inessive, elative; this is the hard/broad s
%{оэØ%}:о 
%{оэØ%}:э
%{оэØ%}:0

! derivational suffix protovowel  аволь аволинь
%{ое%}:е
%{ое%}:о


! derivation suffix-initial archivowel 
%{оеэØ%}:е
%{оеэØ%}:э
%{оеэØ%}:о
%{оеэØ%}:0

! suffix-internal archivowel
%{оеэ%}:е	  !! вт%{оеэ%}мО1
%{оеэ%}:э
%{оеэ%}:о


! stem-final archivowels панго/тинге
%{ОØ%}:0 !!≈ * _@CODE@_ Stem-final archiphoneme панго
%{ЕØ%}:ь !!≈ * _@CODE@_ Stem-final archiphoneme тинге
%{ЕØ%}:0 !!≈ * _@CODE@_ Stem-final archiphoneme тинге

Е3:е ! тне тнэ

! clitic proto consonant
Г1:г !
Г1:к !
Г1:0 !



! Prolative Ga
Г2:г !
Г2:к !
Г2:в !


! Imperative clitic proto consonant
Г4:г !
Г4:к !
Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
К1:к !
К1:т !

!

! connegative suffix rendering vowel
! панемс пань%>%{оеэØ%}К3 : пан00е0
К3:0 !

! Stem final on Imperative Verbs
!К4:0 !

! Diminutive in ке and не нэ Н1е
Н1:к
Н1:н

! Harmony
 %{frontHard%}:0   !!= @CODE@ — front harmony hard
 %{frontSoft%}:0   !!= @CODE@ — front harmony soft
 %{back%}:0   !!= @CODE@ — back harmony
 %{backHard%}:0   !!= @CODE@ — back harmony


! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <


! Some verbs need a marker for vowel placement
!Used before Conjuntive ending
!V1:е !
!V1:э !2012-03-13 look to О1:
!V1:о !
! I2 and U2 adjectives and nouns need this with their "t" and "s"
!V2:0 

TLoss:0
SLoss:0
KLoss:0
VLoss:0
%^WLoss:0
NoVLoss:0

%^NoLinkVow:0 !!= @CODE@ — No linking vowel is used only after consonants for error

— ! emdash
%- !Hyphen with constructions like "-buti"
%>
%<
#
« »

;


Sets

Vows = а о у ы э я ё ю и е 
       А О У Ы Э Я Ё Ю И Е ;
verbStemVows = а о э я ё е 
	       А О Э Я Ё Е ;
verbStemVowStrong = а о э я ё е 
		    А О Э Я Ё Е ;
noGlideVow = а о у ы э 
	     А О У Ы Э ;
glideVow = я ё ю и е 
	   Я Ё Ю И Е ;
BTV = а о у я ё ю 
      А О У Я Ё Ю ; ! Back Trigger Vowels
FTV = ы и е э 
      Ы И Е Э ; ! Front Trigger Vowels

Cns = б в г д ж з й к л м н п р с т ф х ц ч ш щ 
      Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
NPC = б в г д ж з к л м н п р с т ф х ц ч ш 
      Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш ; ! Nonpalatal Consonants
CnsVoiced = б в г д ж з й л м н р 
	    Б В Г Д Ж З Й Л М Н Р ;
CnsVoicedDent = д з л н р 
	      	Д З Л Н Р ;
CnsVoicedNonDent = б в г ж й м 
		   Б В Г Ж Й М ;
CnsVoiceless = к п с т ф х ц ч ш щ 
	       К П С Т Ф Х Ц Ч Ш Щ ;
CnsVoicelessDent = с т ц 
		   С Т Ц ;
CnsVoicelessNonDent = к п ф х ч ш щ 
		      К П Ф Х Ч Ш Щ ;
CnsNonDent = б в г ж к м п ф х ч ш щ 
	     Б В Г Ж К М П Ф Х Ч Ш Щ ;

CnsHardNonDent = ж ч ш 
	       	 Ж Ч Ш ;
CnsDent = д з л н р с т ц 
	  Д З Л Н Р С Т Ц  ;
CnsDentNoL = д з н р с т ц 
	     Д З Н Р С Т Ц  ;

Letters = Vows Cns ь ъ Ь Ъ ;

HarmDummies = %{back%} %{backHard%}  %{frontSoft%} %{frontHard%}  ;

Definitions

! Front Trigger Consonants (less ль)
FTC = [ й: | д ь: | з ь: | н ь: | р ь: | с ь: | т ь: | ц ь: ] ;

! All Consonants
AC = [ FTC | л ь: | NPC | щ ] ;

! Left Front Context Soft
LFCS = [ :FTV [ %> | :0 | AC ]* CnsNonDent | :FTV [ %> | :0 | AC ]* FTC |  BTV FTC AC* CnsNonDent  | FTC | л ь: ] ;

! Left Front Context Hard
LFCH = [ :FTV AC* CnsDent | [LFCS ( %{frontHard%}: | %{frontSoft%}: ) | :FTV AC* ( %{frontHard%}: | %{frontSoft%}: )] %> з | BTV FTC  AC* CnsDent+ | [ BTV л ь [ к с ] ] ] ;

! Front Trigger
FT = [ LFCS | LFCH | л ь: | :FTV ] ; 
! This should be followed by a morpheme boundary marker %>

LFCHMore =  [ :FTV | FTC | л ь: ]  %> [ %{оеэØ%}: н з | с ( т ) ] ;
! [ LFCS | LFCH | :FTV ]  \[ # | :BTV | BTV: ]* %> [ [ %{оеэØ%}:е | %{оеэØ%}:0 ] м [ с | с т ] | [ %{оеэØ%}:е | %{оеэØ%}:0 ] ( н ) з | з | с | с т ] ;
!


! LEFT BACK TRIGGER CONTEXT
BackTriggerContextCns = [ :BTV [ %> | %{back%}: | %{backHard%}: | :NPC | %{оеэØ%}:0 ]* NPC ] ;
![ :BTV [ %> | :NPC | %{оеэØ%}:0 ]* NPC ] ;

! Left Back Context
LBC = [# [Cns: (ь) ]* :BTV NPC* | :BTV \[ # | :FTV | FTV: | ь: | й: ]* |:BTV л ь [ ф | т | д ] ]  ;
!   [ :BTV ( %> %{оеэØ%}:0 ) NPC* ( %> )  :NPC+ |BTV л ь [ ф | т | д ] ]  ;


NoFront = [ %>  | :BTV | BTV: | :NPC | О1: | %{оеэØ%}: ]* ;

LBCCns = [ :BTV [ \[ # | :FTV | FTV: | ь: | й: | :Vows ]* :NPC |:BTV %> CnsDent |  л ь [ ф | т | д ] ]]  ;
!LBCCns = LBC \[ # | FT | :FTV | FTC | л ь: | ь: | й: ]* :NPC+ ;

PossX = [ м | н Ь2: | т Ь2: | н т Ь2: | ( н ) з %{оэØ%}: | н %{оеэ%}: к | м О1: к | н к | ( н ) с т | с к ] ;

CaseX = [ с ь: | [ н | с ] т ь | [ н | с ] т е н ь | н е н ь | д: %{оеэ%}: | с ( т ) О1: | з | с | в | к с | Г2: а | в: т %{оеэ%}: м О1: |  ш к а | н е: | н е: к | н ь: ] ;

CasePossX = [ т е [ т | н ] ь | н с т [ О1: | э н ь ] | з %> %{оеэØ%}: PossX ] ;

InfX = [ м с | м О1: | м А1: | м д О1: | м г а | м с О1: | м с т О1: | з ь: ] ;

ConjX = [ ( з ) м а к | ( з ) м а м | ( з ) м и з ь | н з а т | с с О1: | з О1: | с т | з т | К1: Ь2: | К3: | д О1: | (в: ( О1: ) ) л ь: ( т ь | с е ) | (в: ( О1: ) ) л и [[ н | т | т ( и | е ) н ] ь | д е | н [ е | и ] к | н к | з ь: | к | я | ( н ) з е | н з [ е | и ] т ь | [ м | д ] [ и | е ] з ь | м [ и | е ] [ м | к ]  ] | т с ( т ) ь   ] ;
Back =  :* [ :BTV \[ :FTV | :й | :ь  ]* ( л ь ) [NPC]]  ;
! Word-final elements
WFE = [ [ PossX | CaseX | CasePossX  | ConjX | т н е: ] [  #  | %> [  Г1: | Г4: | :Cns ] |  %- ]  | InfX |  # | %> [  Г1: | Г4: | :Cns ] |  %- ] ;

SIVV = [ PossX | InfX | м с (т) %{оэØ%}: | м (к) а | в | н е н ь | т е н ь | т е т ь | н с т э н ь | н с т %{оэØ%}: | н з а | н т е н ь | с т е н ь | л ь | л и | к с | ш к а | з т | з с | з а | з м | с | з  %>  %{оеэØ%}: | т ь: | н т ь: | н ь: | в:  О1:  л | т %> с ь | т %> с т ь | К3: | д О1: | с ь ] ;
! Suffix-Initial Variable Vowel



Rules


!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
!! __@RULENAME@__  
 X:Y <=> [ Cns | Vows ] _ ( HarmDummies: )  %> [А2:я|А2:а [ н | т ]| %{иы%}:] ;
!                   Cns  _ К4:0 %>  %{иы%}: ;
          where X in (а о э ы у я ё е и ю)
                Y in (0 0 0 0 0 0 0 0 0 0) matched ;

!!€ цёра%>А2н
!!€ цёр0%>ан


!

! Deverbal noun in ОмА
"A1:o"
!! __@RULENAME@__  
А1:о <=>  [ а |я ] ( HarmDummies: ) %> %{оеэØ%}:0  м  _ ;
!!€ яка%>%{оеэØ%}мА1
!!€ яка%>0мо
!!$ яка%>%{оеэØ%}мА1
!!$ яка%>0ма

"Y2:yi"
!! __@RULENAME@__  
%{иы%}:и <=> [ я:0 | ё:0 | е:0 | ю:0 | ь: | й: | CnsNonDent (noGlideVow:0)] ( HarmDummies: ) %> _ ;


"%{оеэ%}:е"
!! __@RULENAME@__
%{оеэ%}:е <=> 
         [ LFCS | FTC | л ь: | :FTV ] (%{frontSoft%}:) %> [в:0 | в ] т _ м О1: ;
         :FTV (%{frontSoft%}: ) %> %{оеэØ%}:0  н  _ к  ;
         [ %{оеэØ%}:е | %{оеэØ%}:э ]  н  _ к  ;
!! неемс+V+TV+Ger+Ill+PxPl1: --see/nähdä--
!!€ ней%>%{оеэØ%}мО1%>%{оеэØ%}з%>%{оеэØ%}н%{оеэ%}к
!!€ не0%>еме%>0з%>энек

"%{оеэ%}:о"
!! __@RULENAME@__
%{оеэ%}:о <=> 
         [ LBC (%{back%}: |%{backHard%}: )  %> | :BTV ( %> ) ] в: т _ м О1: ;
!! псака+N+SP+Abe+PxSg3+Der+Der/MWN+N+SP+Tra+Indef: __cat/kissa__
!!€ псака%>втО1мО1%>%{оеэØ%}нз%{оэØ%}%>кс
!!€ псака%>втомо%>0нзо%>кс
        [ LBC (%{back%}: |%{backHard%}: ) %> | :BTV (%{back%}: |%{backHard%}: ) ( %> ( %{оеэØ%}:0 | %{оеэØ%}:о )) ] [ м | н ] _ к ;


"%{оеэ%}:э"
!! __@RULENAME@__
%{оеэ%}:э <=> 
          LFCH (%{frontHard%}:) %> в: т _ м О1:  ;
          LFCH (%{frontHard%}:) %> н _ к  ;


"%{оеэØ%}:0"
!! __@RULENAME@__
%{оеэØ%}:0 <=> :Vows (HarmDummies:) %> _  ;
               :Cns (HarmDummies:) %> %^NoLinkVow: _  ;
               в (HarmDummies:) %>  %^WLoss: %> _ в:0 ( %{ое%}: ) л ;
"%{оеэØ%}:е"
!! __@RULENAME@__
%{оеэØ%}:е <=> LFCS ( %{frontSoft%}: ) %> _  ;
!! панемс+V+TV+Ind+ConNeg: __drive/ajaa__
!!€ пань%>%{оеэØ%}К3
!!€ пан0%>е0

"%{оеэØ%}:э"
!! __@RULENAME@__
%{оеэØ%}:э <=> LFCH ( %{frontHard%}: ) %> _ ;
               LFCS (%{frontSoft%}:) %> з %> _ ;
!! кев+N+SP+Ill+PxSg2: __rock/kivi__
!!€ кӒ4в%{frontSoft%}%>з%>%{оеэØ%}тЬ2
!!€ кев0%>з%>эть
!!€ кӒ4в%{frontSoft%}%>%{оеэØ%}з%>%{оеэØ%}тЬ2
!!€ кев0%>ез%>эть

!   LFCH (%{frontHard%}:) %> [ с ( т ) | [ %{оеэØ%}:э |  %{оеэØ%}:0 ] з | %{оеэØ%}:э н   з | %{оеэØ%}:э м [ с | с т ] ] _ [  # |  %- | %> [ Г1: | %{оеэØ%}:0 | :Cns ] ]  ;
!  [LFCS  (%{frontSoft%}:) %>  с ( т ) |[ :FTV %> %{оеэØ%}:0 | %{оеэØ%}:э | %{оеэØ%}:е ] [ (н)   з | м [ с | с т ] ]] _ [  # |  %- | %> [ Г1: | %{оеэØ%}:0 | :Cns ] ]  ;

"%{оеэØ%}:о"
!! __@RULENAME@__
%{оеэØ%}:о <=> BackTriggerContextCns ( %{back%}: | %{backHard%}: )  %> _  ;
!  LBCCns ( %{back%}: | %{backHard%}: )  %> _ [в|з|к|л|м|н|с|т|ш] ;
      [:BTV|LBC] (%>) з %> _ [в|з|к|л|м|н|с|т|ш] ;
!! ков+N+SP+Ill+PxSg2: __moon/kuu__
!!€ ков%{back%}%>з%>%{оеэØ%}тЬ2
!!€ ков0%>з%>от
!!€ ков%{back%}%>%{оеэØ%}з%>%{оеэØ%}тЬ2
!!€ ков0%>оз%>от



"O1:ye"
О1:е <=>

 [ LFCS | FTC | л ь: ] (%{frontSoft%}:) %> _ [ ( %> ) WFE | %> %{оеэØ%}:0 | т %> с | %> :Cns ] ;
         [ [ LFCS | л ь: | й:0 ] (%{frontSoft%}:) %> | :FTV ( %> ) ] [ ( %{оеэØ%}:0 | %{оеэØ%}:е | %{оеэØ%}:э ) д: | [ %{оеэØ%}:0 | %{оеэØ%}:е | %{оеэØ%}:э ] м ( д ) | в: т [ %{оеэ%}:е | %{оеэ%}:э ] м ] _ [  # |  %- | %> [  Г1: | Г4: | :Cns | %{оеэØ%}:0 ] ]  ;
        LFCS  (%{frontSoft%}:) %> _ [ # | %> [ Г1: | :Cns | %{оеэØ%}:0 ] ] ; !вере, икеле

!!€ сай%{frontSoft%}%>О1%>дО1
!!€ са00%>е%>де
	 [ LFCS | :FTV | FTC | л ь: ] [ м | м б | т ь: | д ь: | к | г | в ш ] %> _ [ # | %> [ %{оеэØ%}:0 | с | т | н | ш | к | д | :в ] ] ; ! Пильге
!how does this work 2019-03-29

!
"O1:e"
!! __@RULENAME@__  
О1:э <=>  
          # [ м о | т о | с о | м и | т ы | с ы ] н ь:  %> к с %> _  Cns ;
          LFCH (%{frontHard%}:) %> ( д: ) _ [  # |  %- | %> [ Г1: | :Cns | %{оеэØ%}:0 ] ]  ;


"O1:o"
!! __@RULENAME@__  
О1:о <=> LBC _ [  # | %- | %> [ Г1: | Г4: | :Cns | %{оеэØ%}:0 ] ] ;
        [ LBC  %> | :BTV ( %> ) ] [ д: | [ %{оеэØ%}:о | %{оеэØ%}:0 ]  м ( д ) | в: т О1:о м ] _ [  # | %- | %> [ Г1: | :Cns | %{оеэØ%}:0 ] ] ;
        LBC  %> _ [ # | %> [ Г1: | :Cns | %{оеэØ%}:0 ] ] ; ! ало
	 # Cns*  BTV [ [п|т|к|ц|ч] | м б | [в|с|ш] т | [р|л|н] д | [с|ш] к | [в|р|л|н] г | [т|д|р|л] м | в ш ] %> _ [ # | %> [ %{оеэØ%}:0 | с | т | н | ш | к | д | :в ] ] ; ! пандо


"%{оэØ%}:e"
!! __@RULENAME@__  
 %{оэØ%}:э <=>   :FTV (%{frontHard%}:|%{frontSoft%}:) %> [ с ( т ) | %{оеэØ%}:0 ( н )  з | %{оеэØ%}:0 м [ с | с т ] ] _ [  # |  %- | %> [ Г1: | %{оеэØ%}:0 | :Cns ] ]  ;
          LFCH (%{frontHard%}:) %> [ с ( т ) | [ %{оеэØ%}:э |  %{оеэØ%}:0 ] з | %{оеэØ%}:э н   з | %{оеэØ%}:э м [ с | с т ] ] _ [  # |  %- | %> [ Г1: | %{оеэØ%}:0 | :Cns ] ]  ;
          [LFCS  (%{frontSoft%}:) %>  с ( т ) |[ :FTV %> %{оеэØ%}:0 | %{оеэØ%}:э | %{оеэØ%}:е ] [ (н)   з | м [ с | с т ] ]] _ [  # |  %- | %> [ Г1: | %{оеэØ%}:0 | :Cns ] ]  ;



"%{оэØ%}:o"
!! __@RULENAME@__  
%{оэØ%}:о <=>
        [ LBC  %> | :BTV ( %> ) ] [ ( с с:0 %> ) с ( т ) | [ %{оеэØ%}:о | %{оеэØ%}:0 ] ( н )  з | [ %{оеэØ%}:о | %{оеэØ%}:0 ]  м ( д | с | с т ) ] _ [  # | %- | %> [ Г1: | :Cns | %{оеэØ%}: ] ] ;

!! псака+N+SP+Abe+PxSg3+Der+Der/MWN+N+SP+Tra+Indef: __cat/kissa__
!!€ псака%>втО1мО1%>%{оеэØ%}нз%{оэØ%}%>кс
!!€ псака%>втомо%>0нзо%>кс
!! псака+N+SP+Gen+PxSg3+Der+Der/MWN+N+Sg+Gen+Def
!!€ псака%>%{оеэØ%}нз%{оэØ%}%>%{оеэØ%}нть
!!€ псака%>0нзо%>0нть

"%{оэØ%}:0"
!! __@RULENAME@__
%{оэØ%}:0 <=>   _  %> А2: ;


"O1:0"
!! __@RULENAME@__  
О1:0 <=>   _  %> [А2:а|А2:я] ;


"%{ое%}:е"
!! __@RULENAME@__
%{ое%}:е <=> FT _ ;
         :FTV в (%{frontSoft%}:|%{frontHard%}:) %>  %^WLoss: %> %{оеэØ%}:0 в:  _ л  ;
         [ :FTV ( (%{frontSoft%}:|%{frontHard%}:) %>  %{оеэØ%}:0 ) ] в _ л  ;
         FT (%{frontSoft%}:|%{frontHard%}:) %> ( %{оеэØ%}:0 | %{оеэØ%}:е | %{оеэØ%}:э )  м  _ к  ;


"%{ое%}:о"
!! __@RULENAME@__
%{ое%}:о <=> LBC _ ;
          :BTV в ( %{back%}: ) %> %^WLoss: %> %{оеэØ%}:0 в: _ л  ;
          :BTV ( ( %{back%}: | %{backHard%}: ) %> ( %{оеэØ%}:0 ) )  в _ л  ;
!         [ LBC - [ :* в ]  | :BTV ] %> [%{оеэØ%}:0 |%{оеэØ%}:о] в _ л ь ;
         LBC (%{back%}:|%{backHard%}:) %> ( %{оеэØ%}:0 | %{оеэØ%}:о )  м  _ к  ;

!!Changes in the suffix vowels
!"A2:ya"
!А2:я <=> [ FTC | :FTV | ь | :FTV NPC | :FTV Cns* CnsNonDent | LFCS :FTV ( (н) т ( Ь2: ) | н Ь2: ) ]  %> т _ [ д | н | м ] ;
!2013-03-16! [ FTC | :FTV | ь | :FTV NPC | :FTV Cns* CnsNonDent | LFCS :FTV ( (н) т ( Ь2: ) | н Ь2: ) ]  %> [ т | TLoss:0 ( %> ) т:0 ] _  [ д | н | м ] ;
!         [ CnsDent [  ь:0 |я:0 | ё:0 | е:0 | и:0 | ю:0 ]| Vows [ я:0 | ё:0 | е:0 | ю:0 | й:0 ] | [ LFCS ( :FTV ) %>  | %{оеэØ%}:е | %{оеэØ%}:э ] ( (н) т  Ь2:0  | н Ь2: )  ]  %>  _ [ т | н ] ;


!HERE
"A2:a"
!! __@RULENAME@__  
А2:а <=>  [ LBC | :BTV ] %> т  _  [ д | н ] ;
   %{оэØ%}: %> _ [ д | н | т] ;
         [CnsDent ( э:0 | ы:0 | а:0 | О1: | о:0 | у:0 )|  в: т %{оеэ%}: м  О1:0 | CnsNonDent ( Vows:0 ) | [ LBC ( :BTV ) %>  |  %{оеэØ%}:о ]  ((н) т Ь2:0 | н Ь2: )  ] %>  _  [ т | н ] ;

"j:0"
!! __@RULENAME@__  
й:0 <=> :Vows _  ( %{frontSoft%}: ) %> [  %{иы%}: | А2:я | %{оеэØ%}:е ] ;
! Vows _ %> [  %{иы%}: | А2:я | %{оеэØ%}:е | %{оеэØ%}:0 | О1:е |О1:0 ] ;
!!€ сай%>%{оеэØ%}%>дО1
!!€ са0%>е%>де

!
!18.04.2007 Adding "U2:" and contemplating environment "%> т н _"
!
"Е3:э always" 
!! __@RULENAME@__ %> т н _ 2013-02-23
Е3:э <=     [ CnsDent ] (%{backHard%}:|%{frontHard%}:)  %>  т н _ [ # | %- | %> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"Е3:э sometimes" 
!! __@RULENAME@__ %> т н _ 2013-02-23
Е3:э =>     [ CnsDent | BTV Cns* CnsNonDent | CnsHardNonDent ] (%{backHard%}:|%{back%}:|%{frontHard%}:)  %>  т н _ [ # | %- | %> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];

"ye:e always" 
!! __@RULENAME@__  
е:э <=>
          CnsDent (О1:0) %>  :н _ [[  # | %-| %> Г1:0 а:я]| н ь: | к] ;
!          [[[Back - [ :* Vows]] %> | CnsDent]  %> ] т н _ [ # |  %- |%> [ Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];


!"ye:e sometimes"
!е:э =>
!          [CnsDent|CnsNonDent] ( О1:0 ) %>  н _ [[  # | %- | %> Г1:0 а:я ] | н ь: | к ] ;
!          [ CnsDent | CnsNonDent | м:0 ]  ( О1:0 ) %> ( VLoss:0 ) ( TLoss:0 ) т н _ [ # | %- | %> [ Г1:0 а:я | т | л | Г2:в а | в | д | к | н | с | ш ]];


! вазнэ, верьгизнэ, ривезьке
"Н1:н"
!! __@RULENAME@__  
Н1:н <=> [[ в | д | й | л | м | н | р ] ( ь: ) | Vows: ] %> _ е:  ;
         .#. [в | л | п ] а з %> _ е: ; 
"Н1:к"
!! __@RULENAME@__  
Н1:к <=> [ б | г | к | п | с | т | ф | х | ц | ч | ш | щ ] ( ь: ) %> _ е:  ;
         [ [Vows] - [ а ] ] з ( ь ) %> _ е: ;
         [[Cns] - [в | л | п ]] а з ( ь ) %> _ е: ;

"а: и Dimin"
!! __@RULENAME@__
 а:ы <=> [ д | з | л | н | р | с | т | ц ]  _ %> Н1: е ;

"о: ы Dimin"
!! __@RULENAME@__
 о:ы <=> [ д | з | л | н | р | с | т | ц ]  _ %> Н1: е ;

"у: и Dimin"
!! __@RULENAME@__
 у:ы <=> [ д | з | л | н | р | с | т | ц ]  _ %> Н1: е ;


"э: и Dimin"
!! __@RULENAME@__
 э:ы <=> [ д | з | л | н | р | с | т | ц ]  _ %> Н1: е ;

"а: и Dimin"
!! __@RULENAME@__
 а:и <=> [ б | в | г | ж | к | м | п | ф | х | ч | ш | щ ] _ %> Н1: е ;

"о: и Dimin"
!! __@RULENAME@__
 о:и <=> [ б | в | г | ж | к | м | п | ф | х | ч | ш | щ ] _ %> Н1: е ;


"у: и Dimin"
!! __@RULENAME@__
 у:и <=> [ б | в | г | ж | к | м | п | ф | х | ч | ш | щ ] _ %> Н1: е ;

"я: и Dimin"
!! __@RULENAME@__
 я:и <=>  _ %> Н1: е ;

"ё: и Dimin"
!! __@RULENAME@__
 ё:и <=>  _ %> Н1: е ;

"ю: и Dimin"
!! __@RULENAME@__
 ю:и <=>  _ %> Н1: е ;

"е: и Dimin"
!! __@RULENAME@__
 е:и <=>  [ Cns | Vows ] Cns   _ %> Н1: е ;
   .#. [[ Cns ] - [ п ]]  _ %> Н1: е ;
          
"a:ya"
!! __@RULENAME@__  
а:я  <=> :Vows ((%>) К3:0 )  %> [ Г1:0 | Г4:0 ] _  ;



!Changes in suffix consonants
"v:0"
!! __@RULENAME@__  
в:0 <=> [ :Cns  ( ь: | :0) ] %> _ т [%{оеэ%}:о|%{оеэ%}:е|%{оеэ%}:э] м [О1:о|О1:е|О1:0] ;
    в %> %^WLoss: %> %{оеэØ%}: _ %{ое%}: л  ;

!Depricated due to 
!ERROR: Label not found in 'EQV_LABELS' (114) **
!Solved in several contexts by declaring both lexical
!and surface forms in the alphabet.
"G1:0"
!! __@RULENAME@__  
Г1:0 <=> [ :Vows ((%>) К3:0 )  %> | К1: Ь2:0 %> KLoss:0 | :к %> KLoss:0 ]  _ а:я ;

"G1:g"
!! __@RULENAME@__  
Г1:г <=> [CnsVoicedDent (:ь|Ь2:0)| й | [ CnsVoicedNonDent - г ] ( :ь )] %>  _ а ;

"G1:k"
!! __@RULENAME@__  
Г1:к <=>  [ CnsVoicelessDent ( :ь | Ь2:0 | Ь2:ь ) | CnsVoicelessNonDent | г | К1: Ь2: | К4:0 ]  %> _ а  ;
          

!Prolative
"G2:g"
!! __@RULENAME@__  
Г2:г <=> [ CnsVoicedDent ( :ь )| [ CnsVoicedNonDent - г ] ( :ь ) ] %> _ а ;

"G2:v"! 
Г2:в <=> [ :Vows (:0) %> | :Vows ] _ а [ # | %- |%> [ Г1:0 |  %{оеэØ%}:  SIVV |т | н | м | с | д: | в | к с | л | А2: ] ];


"G2:k"
!! __@RULENAME@__  
Г2:к <=>  [ CnsVoicelessDent ( :ь )| CnsVoicelessNonDent ( :ь )| г ] %> _ а [ # |  %- |%> [ Г1:0 | %{оеэØ%}: SIVV | т | н | с | д: | в | к с | л ] ] ;
           Vows CnsVoiceless  %> VLoss:0 _ а ;

! 2012-08-07
"G4:g" !Imperative Clitics
Г4:г  <=> з ь %> _ а ;


"G4:0"
!! __@RULENAME@__  
Г4:0  <=>
           [ К1: Ь2: | к ] %> _ а ;
  [О1:е|О1:о|и|ы] %> _ а: ;

"G4:k"
!! __@RULENAME@__  
Г4:к  <=>  # [и л я|у ж о|а з ё|а д я|а й д а] %> _ а  ;


!"m:0" 
!м:0 <=> 
! BTV  _ У2:0  %>  VLoss: [ т | т н е:э ] ;
!        т (ь) _ [ У2: ]  %>  VLoss: [с (т) [О1:э|О1:о|О1:0]|с [  # | %-| %> [Г1:0|Г1:к]]] ;



!! imperative suffix
"K1:t"
!! __@RULENAME@__  
К1:т <=>   Cns ( ь: ) (%{back%}:|%{backHard%}:|%{frontSoft%}:|%{frontHard%}:) %> _ Ь2: [ # | %- ] ;

!! ливтемс+V+TV+Imprt+ScSg2+Clt/Га: __set out/laittaa esille__
!!€ ливть%>К1Ь2%>Г4а
!!€ ливть%>к0%>0а

"K1:к"
!! __@RULENAME@__  
К1:к <=> Vows (%{back%}:|%{backHard%}:|%{frontSoft%}:|%{frontHard%}:) %> _ Ь2: [ # | %- ]  ;



! Suffix final hard vs soft
"U4:y"
!! __@RULENAME@__  
Ь2:ь <=>
 [ FTC | л ь: ] ( %{frontSoft%}: ) %>  К1:т  _  [ # | %- | %>  Г1: ] ;
[ LFCS | :FTV | ь: ]  ( %{frontSoft%}: ) %>  т  _  [ # | %- | %>  Г1: ] ;
  [  :FTV ( %{frontSoft%}: )  %>  %{оеэØ%}:0 | %>  [ %{оеэØ%}:е | %{оеэØ%}:э ]] [ н | ( н | в ) т ]  _  [ # | %- | %>  Г1: ] ;

!#  [ [ [ LFCS   %> | :FTV  %> ( %{оеэØ%}:0 ) | ь:  %> ] | %{оеэØ%}:е | %{оеэØ%}:э | О1:е | О1:э ] [ н | ( н | в ) т ] |  [ LFCS | ь: | :FTV ] %> К1:т ]  _  [ # | %- | %>  Г1: ] ;
!! пильге+N+Pl+Nom+Indef __leg; foot/jalka__
!!€ пильг%{frontSoft%}%>тЬ2
!!€ пильг0%>ть
!! валдо+N+Pl+Nom+Indef __light/valo__
!!$ валд%{backHard%}%>тЬ2
!!$ валд0%>ть

"U4:0"
!! __@RULENAME@__  
 Ь2:0 <=>
 [ LFCH (%{frontHard%}:) | :FTV (HarmDummies:) | LBCCns  (%{backHard%}:|%{back%}:) | :BTV (%{back%}:) ] %> К1:  _  [ # | %- | %>  Г1: ] ;
 [ LFCH (%{frontHard%}:) | LBCCns (%{backHard%}:|%{back%}:) | :BTV (%{back%}:) ] %> т  _  [ # | %- | %>  Г1: ] ;

#  [  LFCH | :BTV ] (%{frontHard%}: | %{back%}: | %{backHard%}: ) %> [ %{оеэØ%}:0 | %{оеэØ%}:о ] [ н | ( н | в ) т ] _  [ # | %- | %>  Г1: ] ;

 LBC (%{backHard%}: | %{back%}: ) %> [ %{оеэØ%}:0 |%{оеэØ%}:о ] [ н | ( н | в ) т ]  _  [ # | %- | %>  Г1: ] ;
 

!! валдо+N+Pl+Nom+Indef __light/valo__
!!€ валд%{backHard%}%>тЬ2
!!€ валд0%>т0



!!!TLoss sloppy
!"t:0"
!т:0 <=> [ [ т | К1:т ] ( Ь2: ) %> TLoss:0 | т У2:0 %> VLoss:0 TLoss:0 ] ( %> ) _ ;

!!SLoss s s > s = s 0 0 s
"s:0"
!! __@RULENAME@__  
с:0 <=> с _ %>  с ;
! с _ %> SLoss:0 с ;

!!€ класс%>с
!!€ клас0%>с


!Ablative d/t
"d:t"
!! __@RULENAME@__  
д:т  <=> [ CnsVoiceless | д | г] (ь:0)  %> _ [О1:е|О1:э|О1:о] ;
!!€ кедь%>дО1
!!€ кед0%>те
!         т [ И2:0 ] %> VLoss:0 _ [О1:е|О1:э|О1:о] ;
!Word final Vowels


"y:0"
!! __@RULENAME@__  
ь:0 <=> Cns _ %^Ь2ZERO: ;
!!кель+N+Sg+Gen+Def: __language__
!!€ кель^Ь2ZERO%>енть
!!€ кел00%>енть
     CnsDent  _  %> [ А2:я | %{иы%}: | %{оеэØ%}:е | О1:е ];
    CnsDentNoL _ ( %{frontSoft%}: ) %> [в:0 т %{оеэ%}:е м [О1:е|О1:0] | К1: Ь2: | Н1:н | н | д: | ц е: [ # | %- | %> ] | т | с (т) [ ь: | е: ]] ;



!"U2:0"
!У2:0 <=>  _ %> VLoss:0 [ ( TLoss:0 ) т [ Ь2:0 | Ь2:ь | А2:а ] | ( TLoss:0 ) т н е: |с (т) [О1:о|О1:0]| с [ %> | # | %- ] ] ;
!          _ %> VLoss:0 д: [ О1:о ] ;

!"U2:o"
!У2:о  <=>  _ [ %> [ (н) т Ь2:0 | н Ь2:0 | т н е: | с ь: | с (т) [ О1:о | О1:0 ] | с [ %> |  # |  %- ] | з %> %{оеэØ%}: | л  |  (н) з [ О1:о | О1:0 ] | ш к а | к с | н | в | Г2:в | д | с т [ %> |  # | %- ] ] | Г1:0|  # | %- ] ;


!Disallow

"Disallow TLoss after non-t"
!! __@RULENAME@__  
TLoss:0 /<= [[ :* Letters ] - [ :* :т ] ] %> _ ;

"Disallow KLoss after non-k"
!! __@RULENAME@__  
KLoss:0 /<= [[ :* Letters ] - [ :* :к ] ] %> _ ;

"Disallow SLoss after non-s"
!! __@RULENAME@__  
SLoss:0 /<= [[ :* Letters ] - [ :* :с ] ] %> _ ;

"Disallow %^WLoss after non-v"
!! __@RULENAME@__  
%^WLoss:0 /<= [ [ Letters ] - [ в ] ] %> _ %> %{оеэØ%}: в: ;
!%^WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %> _ ;

"Disallow Н1:н after Letters"
!! __@RULENAME@__  
Н1:н /<= Letters _ ;

"Disallow з where not following l, n or r"
!! __@RULENAME@__
 з:з /<=  \[л | р | н ] (ь:) %> _ %> %{оеэØ%}:  ;




!!OLD

!"Disallow %{ОØ%} where not T, sO, stO, s"
!! __@RULENAME@__
! %{ОØ%}:0 /<= _ %> \[ т Ь2: | т н Е3: | с ( т ) %{оэØ%}: | с [ %> | .#. ] ] ;
!! чувто+N+Pl+Nom+Def: __tree/puu__
!!€ чувт%{ОØ%}%>тнЕ3
!!€ чувт0%>тнэ

!"Disallow %{ЕØ%}:0 where not T, sO, stO, s"
!!! __@RULENAME@__
! %{ЕØ%}:0 /<= _ %> \[ т Ь2: | т н Е3: | с ( т ) %{оэØ%}: | с [ %> | .#. ] ] ;

!"Disallow %{ОØ%}:ь where not T, sO, stO, s"
!!! __@RULENAME@__
! %{ЕØ%}:ь /<= _ %> \[ т Ь2: | с ( т ) %{оэØ%}: | с [ %> | .#. ] ] ;

!! веле+N+SP+Tra+PxSg2
!!€ веле%>%{оеэØ%}кс%>%{оеэØ%}тЬ2
!!€ веле%>0кс%>эть

!! псака+N+SP+Abe+PxSg2+Clt/Cop+Prt2+ScPl3+Clt/Gak
!!€ псака%>втО1мО1%>%{оеэØ%}тЬ2%>%{оеэØ%}линек%>Г1ак
!!€ псака%>втомо%>0т0%>олинек%>как
!! ош+N+SP+Ill+PxSg2
!!€ ош%>%{оеэØ%}з%>%{оеэØ%}тЬ2
!!€ ош%>оз%>от0
!       	     :BTV в %^WLoss:0  %> _ в: ( О1: ) л ;
!!€ эряв%>^WLoss%>{ОЕЭØ}вО1ль
!!€ эряв%>0%>00оль

!! псака+N+SP+Abe+PxSg3+Der+Der/MWN+N+SP+Tra+Indef: __cat/kissa__
!!€ псака%>вт%{оеэ%}мО1%>%{оеэØ%}нз%{оэØ%}%>кс
!!€ псака%>втомо%>0нзо%>кс

!! веле+N+SP+Tra+PxSg2+Clt/Cop+Prt2+ScPl3: __village/kylä__
!!€ веле%>%{оеэØ%}кс%>%{оеэØ%}тЬ2%>%{оеэØ%}льть
!!€ веле%>0кс%>эт0%>ельть

"Disallow %^NoLinkVow after vowel"
!! __ @RULENAME@ __                                                                                                                                                                           
%^NoLinkVow:0 /<= :Vows  (HarmDummies:) %> _ ;
