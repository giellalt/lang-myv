! -*- encoding: utf-8-unix -*-
Alphabet
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я  
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я



! verbal stem vowels
!these are used with verbs like:
! "кадомс", "молемс" and  "пезэмс"
У1:0 !
И1:0 !
И1:ь !
Ы1:0 !

! Nominal stem vowels that alternate 
! work with "+VLoss" and "+NoVLoss" tags
!
У2:0
У2:о
И2:0 
И2:е 
И2:ь 

!!!!!!
!!кедИ3
!!покшУ3
!!пикшЫ3
У3:0 !
И3:0 !
Ы3:0 !

!!Suffixes
! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
А2:я !А2
А2:а !А2
Ы2:ы !
Ы2:и !

! mid vowel harmony vowel
! Initial -Ом
! Medial втомо втеме тэме
! final  "mo" and "me"
О1:0 !
О1:е !
О1:э !
О1:о !

! clitic proto consonant Гак
Г1:0 !
Г1:к !
Г1:г !



! Prolative Га
Г2:г !
Г2:в !
Г2:к !



!!!!
!!!Questionable
!Suffix final variant vowel for hard vs soft
У4:0 !
У4:ь !

! verbal stem consonant that appear in PretI 3sg
Т1:т !
Т1:0 !

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
А1:а !
А1:о !

! Imperative clitic proto consonant
Г4o:г !
Г4:к !
Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
К1:к !
К1:т !

!

! connegative suffix rendering vowel
К3:0 !

! Stem final on Imperative Verbs
К4:0 !



! I2 and U2 adjectives and nouns need this with their "t" and "s"
!V2:0 !!Reassess this 2011-03-07

VLoss:0
NoVLoss:0

%- !Hyphen with constructions like "-buti"
%^:0
!Trying to cope with 


;


Sets
vows = а о у ы э я ё ю и е А О У Ы Э Я Ё Ю И Е ;
verbStemVows = а о э я ё е А О Э Я Ё Е ;

noGlideVow = а о у ы э А О У Ы Э ;
glideVow = я ё ю и е Я Ё Ю И Е ;
subseqBack = а о у я ё ю А О У Я Ё Ю ;
subseqFront = ы и е э Ы И Е Э ;

Cons = б в г д ж з й к л м н п р с т ф х ц ч ш щ Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
ConsBack = б в г д ж з к л м н п р с т ф х ц ч ш щ Б В Г Д Ж З К Л М Н П Р С Т Ф Х Ц Ч Ш Щ ;
ConsVoiced = б в г д ж з й л м н р Б В Г Д Ж З Й Л М Н Р ;
ConsVoicedDent = д з л н р Д З Л Н Р ;
ConsVoicedNonDent = б в г ж й м Б В Г Ж Й М ;
ConsVoiceless = к п с т ф х ц ч ш щ К П С Т Ф Х Ц Ч Ш Щ ;
ConsVoicelessDent = с т ц С Т Ц ;
ConsVoicelessNonDent = к п ф х ч ш щ К П Ф Х Ч Ш Щ ;
ConsNonDent = б в г ж к м п ф х ч ш щ Б В Г Ж К М П Ф Х Ч Ш Щ ;
ConsDent = д з л н р с т ц Д З Л Н Р С Т Ц  ;
ConsDentNoL = д з н р с т ц Д З Н Р С Т Ц  ;

Letters = vows Cons ь ъ Ь Ъ ;

Definitions
BackHarmSeg = [ \[ :subseqFront | :й | :ь | й: | ь:  | И1: | И2: | И3: | Ы1: | Ы3: | .#. ] ] ;
!!2011-03-07!SubseqBackHarm = [А2:а|а| О1:о|У2:о  | о |а:я| у| А2:я|я| ё| ю] ;
!BackStem =  [ :subseqBack | А2: | У3: | У2: | У1: ]  ConsBack*  (  л ь  ConsBack )   ;

!BackStem = U3, U1
BackStem =  :subseqBack [ ConsBack+ | л ь  ConsBack ];

Back = BackStem BackHarmSeg* [ BackHarmSeg -  :vows  ] ;

FinalBack = BackStem BackHarmSeg* [ BackHarmSeg - [ :vows | %^:0 ] ] ;


FronHarmSeq = [ \:subseqBack ]+  ;

!+++!FrontSoftStem is the same as И3:!!
FrontSoftStem =  [ [ :subseqFront | й ] Cons* ConsNonDent | :subseqFront ConsDent ь ConsNonDent | :subseqBack ConsDentNoL ь ConsNonDent | ь: | й: ] ;

!2011-03-08 this copes with сэван
FrontHardStemForA2 = [ [ :subseqFront | й ] Cons* ConsNonDent | :subseqFront ConsDent ь ConsNonDent | :subseqBack ConsDentNoL ь ConsNonDent ] ;

!+++!FrontHardStem is the same as Ы3: !!
FrontHardStem = [ [ :subseqFront | й ] Cons* | :subseqFront Cons* ConsDent ь Cons* | :subseqBack Cons* ConsDentNoL ь Cons* ] ConsDent ;


FrontHard = [ [ :subseqFront | :й | ConsDentNoL :ь ] \[ :subseqBack ]+ | л ь Cons+ | subseqFront л ь ] ConsDent ;

FrontSoft = [ [ :subseqFront | й ] \[ :subseqBack | У3 :| У2: ]+ | ConsDentNoL ь ConsNonDent | subseqFront л ь ConsNonDent | й: ] ;


Front = [ FrontHard | FrontSoft | [ :subseqFront | FrontSoftStem | :ь | :й И3:0 | :й И2:0 ]] ;

!2011-03-08
FollowsO1 = [ м | н [ У4:0 | У4:ь | ь: ] | т [ У4:0 | У4:ь | ь: ]  | (н) з [ О1:0 | О1:о | О1:э ] | н [ О1:о | О1:е ] к | н к | с т [ %^:0 | .#. ] | с ь: | н т [ ь: | е ] | к с | ш к а: | с (т) О1: | с [ %^:0 | .#. ] | в О1: л | в | л | К3: | н с т | з %^:0 [ О1:о | О1:э ]  ] ;

!
Poss =  [ м | н [ У4:0 | У4:ь | ь: ] | т [ У4:0 | У4:ь | ь: ]  | (н) з [ О1:0 | О1:о | О1:э ] | н [ О1:о | О1:е ] к | н к | с т [ %^:0 [ Г1:0 | Г1:к ] а: | О1: л | (т) А2:а | .#. ] ] ;


!Poss-1pl =  %^:0  н [ О1:о | О1:е | О1:э ] к ;
!Poss = (О1:е | О1:э | О1:о) [ м | н [ У4:0 | У4:ь | ь: ] | т [ У4:0 | У4:ь | ь: ]  | (н) з [ О1:0 | О1:о | О1:э ] | н [ О1:о | О1:е | О1:э ] к | н к | с т [ %^:0 [ Г1:0 | Г1:к ] а: | .#. ] ] ;


Rules

!Changes in the stem vowels before "10", "20", "30" and "60"
"VowelDeletionBeforePredVowels"
[ а:0 | я:0 | о:0 | ё:0 | э:0 | е:0 | ы:0 | и:0 | у:0 | ю:0 ] <=> Cons _  %^:0 [ А2:я | А2:а [ н | т ] | Ы2: ] ;


!!Consonant Stem Verbs lose stem vowel
"verbStemVowWeakU1:0"
  У1:0 => ConsBack _ %^:0 [ А2: [ н | т ] | т А2: [ д | н ] | К1: | Ы2: | с [ (т) ь: |  [ ы | а ] ] | О1:о ] ;


!!Consonant Stem Verbs lose stem vowel
"verbStemVowWeakI1:0"
  Ы1:0 <=> [ ConsDent | vows ] _ %^:0 [ А2: [ н | т ] | т А2: [ д | н ] | К1: | Ы2: | с [ (т) ь: |  [ ы | а ] ] | О1:э ] ;


!!Consonant Stem Verbs with Palatalized Dental lose stem vowel
"YI1:0"
       И1:0 <=> [ Cons | vows | й: ] _ %^:0 [ А2: [ н | т ] | Ы2: | О1:е ];
      .#. :* [ Cons - л ]  _ %^:0 [ т А2: [ д | н ] | К1: ] ;
      .#. :* [ Cons - л | vows ]   _ %^:0 с (т) ь: ;
       [ ConsNonDent | й: | vows ] _ %^:0 с [ ы | а ] ;
                й:0 _ %^:0 [ Ы2: | О1:е ] ;

"YI1:y"
И1:ь <=>  ConsDent  _ %^:0  [ Г1: | Г2: ] а  ;
      .#.   :* л  _ %^:0 [ т А2:я [ д | н ] | К1: | с (т) ь: ] ;


!+++++!!!?????!This need to be changed to %^:0 О1:о К3:0 ;
!"U1:o, YI1:ye"
![ У1:о | И1:е ] <=> [ Cons | vows ] _ %^:0 К3:0 ;

"I1:e"
Ы1:э <=> ConsDent _ %^:0 К3:0 ;
!


!2011-03-08 we might remove this in favor of "FrontSoftStem"
!Vowels in Nouns and Adjectives 
!2006-12-02
!"YI3:y"
!И3:ь <=>  ConsDent  _ [ %^:0  [ Г1:г | Г1:к | Г2:г | Г2:к | с [ %^:0 | .#. ] | с (т) О1: | :ConsNonDent ] | .#. ] ;
!      .#.   :* л  _ [ %^:0 (в:0) :Cons | .#. ] ;


"y:0"
ь:0 <=>        ConsDent  _  %^:0 [ О1:е :Cons | А2:я | Ы2:и ];
              ConsDentNoL _ %^:0 [ в:0 т О1:е м [ О1:е | О1:0 ] | н | д: | :т | А2:я | с (т) [ ь: | е: ] ] ;




!!Changes in stem Consonants
"T1:t"
Т1:т <=> [ е | о ]  _ %^:0 с (т) ь: ;

"A1:o"
А1:о <=>  [ а | я ] %^:0 О1:0  м  _ ;

"Y2:yi"
Ы2:и <=> [ я:0 | ё:0 | е:0 | ь:0 | FrontSoftStem | ConsNonDent [ noGlideVow:0 | И1:0 | У1:0 ] ]  %^:0 _ ;


"O1:ye"
О1:е <=> [ FrontSoftStem | FrontHardStem |  :subseqFront ] %^:0 О1:  н _ к ;
!![ И3:0 | ь: |  :subseqFront ] %^:0 (О1:е | О1:э)   н _ к ;
!1Pl
         %^:0 [ в | в:0 ] т [ :е | :э ]  м  _  [ .#. | %^:0 [ Г1:0 | О1: ] ] ;
!Abessive carative final vowel
         [ ь: | И3:0 | И3:ь | :subseqFront ] %^:0 [ в:0 | в ] т _ м [ О1:0 | О1:е ] ;
! Abessive interim vowel
          [ И3:0 | И2:е | :subseqFront ] %^:0 д:  _ [ %^:0 [ Г1:0 |  О1: ] | .#. ] ;
!Ablative
          [ FrontSoftStem | ь: | И3:0 ] %^:0  _  [ FollowsO1 | .#. ] ;
! Suffix-initial mid vowel
         И1:0 %^:0 _ [ з | м | в | н | л | с | К3: | к ] ;
!Verb1



"O1:0"
О1:0 <=> :vows %^:0  _ ;
          _ %^:0 А2: ;

!
"O1:o"
О1:о <=> [ Back | BackStem %^:0 О1:о ] н _ к ;
![ У3:0 %^:0 (О1:о) | [ У2:о | а | я | о | ё | у | ю ] %^:0 ] н _ к ;
!1Pl
         FinalBack  _ [ .#. | %^:0 [ Г1:0 | О1: ] ] ;
!          %^:0 [ в | в:0 ] т О1:  м  _  [ .#. | %^:0 [ Г1:0 | Poss | н т ] ] ;
!Abessive carative final vowel
         Back ConsBack _ Cons ;
!         [ У3:0 | а | я | :о | ё | у | ю ] %^:0 [ в:0 | в ] т _ м [ О1:0 | О1:о ] ;
! Abessive interim vowel
!!This will require something for secondary declension of the translative
         У1:0 %^:0 _ [ з | м | в | н | л | с | К3: | к ] ;
!Verb1

         [ У3:0 | :о | а | я | у | ю ] %^:0  д:  _ [ %^:0 [ Г1:0 |  Poss | н т | л ] | .#. ] ;
!Ablative
!!This will require something for secondary declension of the translative
[ Back ConsBack | BackStem ] %^:0  _ [ ( %^:0 О1: ) FollowsO1 | .#. ] ;
      [ BackStem | [ %^:0 О1:о | BackStem %^:0 ] з ] %^:0  _  [ FollowsO1 ] ;
! Suffix-initial mid vowel
! Possive suffix, Possessive-Illative
!!This will require something for secondary declension of the translative


! Suffix-final mid vowel
          [ У3:0 | :о | а | я | у | ю | ё ] %^:0 с (т) _  [ .#. | %^:0 [ Г1:0 | Poss | т | н | с | Г2:в | д: | л ] ] ;
!Inessive and Elative
         [ [ :о | а | я | у | ю | ё ] %^:0 | %^:0 О1:о ] (н) з _ [ .#. | %^:0 [ Г1:0 | л | т | н | с | Г2:в | д: ] ] ;
! 3Sg



!
"O1:e"
О1:э <=>
         FrontHardStem %^:0  н _ к ;
!1Pl 
!!No suffix-initial mid vowel
         FrontHardStem %^:0 в:0 т _ м [ О1:0 | О1:е ] ;
! Abessive interim vowel
!!This will require something for secondary declension of the translative
         FrontHardStem  %^:0 д:  _ [ %^:0 [ Г1:0 |  Poss | н т | л ] | .#. ] ;
!Ablative
!!This will require something for secondary declension of the translative
          [ FrontHardStem | [ %^:0 [ О1:е | О1:э ] | FrontHardStem %^:0 ] з ] %^:0  _  [ FollowsO1 | з %^:0 ] ;
! Suffix-initial mid vowel
! Possive suffix, Possessive-Illative
!!This will require something for secondary declension of the translative

! Suffix-final mid vowel
         [ FrontSoftStem | FrontHardStem | :subseqFront ] %^:0 с (т)  _  [ .#. | %^:0 [ Г1:0 | Poss | т | н | с | Г2:в | д: | л ] ] ;
!Inessive and Elative

         [ [ :subseqFront ] %^:0 О1:0 | %^:0 [ О1:е | О1:э ] ] (н) з _ [ .#. | %^:0 [ Г1:0 | О1: л | т | О1: н | с | Г2:в | д: ] ] ;

        FrontHardStem %^:0 _ FollowsO1 ;
!
         Ы1:0 %^:0 _ [ з | м | в | н | л | с | К3: | к ] ;
!Verb1


!Changes in the suffix vowels
"A2:ya"
А2:я <=>
         [ [ subseqFront | й | ь ][ ConsDent - л ] | И2:0 | [ ConsDent - л ] [ И1:0 | И2:0 | ь:0 ] | л И1:ь | :subseqFront | й (И1:0 | И2:0) | И3:0 ] %^:0 т _  [ д | н ] ;
 [ ConsDent [ И2:0 | И3:0 ] | ConsDent [ ь:0 | я:0 | ё:0 | е:0 ] | я:0 | ё:0  ]  %^:0  _ [ т | н ] ;
         vows [ (й:) И2:0 | й: И3:0 ]  %^:0  _ [ т | н ] ;

!Changes in the suffix vowels
!HERE
"A2:a"
А2:а <=> [ FrontHard | FrontHardStemForA2 |ConsDent [ э:0 | Ы1:0 | Ы3:0 ] | [ в: т О1: м | с ( т ) | з ] О1:0 | ConsNonDent [ е:0 | И3:0 | Ы3:0 | И2:0 | И1:0  ] | BackStem | а:0 | о:0 | У1:0 | У2:0 | У3:0 | Ы1:0  | Ы3:0 | м | BackHarmSeg :0* т У4:0 ] %^:0  _  [ т | н ] ;

!![ ConsDent [ э:0 | Ы1:0 | Ы3:0 ] | [ в: т О1: м | с ( т ) | з ] О1:0 | ConsNonDent [ е:0 | И3:0 | Ы3:0 | И2:0 ] | BackStem | а:0 | о:0 | У1:0 | У2:0 | У3:0 | Ы1:0  | Ы3:0 | м | BackHarmSeg :0* т У4:0 ] %^:0  _  [ т | н ] ;
         [ :subseqBack | У1:0 | У2:0 | У3:0 | Ы1:0  | Ы3:0 ] %^:0 т _  [ д | н ] ;



"j:0"
й:0 <=> vows _ [ И2:е  | И1:0 %^:0 Ы2:и | (И1:0 | И2:0 | И3:0)  %^:0 [ А2:я | О1:е :Cons ] ] ;


!
!18.04.2007 Adding "U2:" and contemplating environment "%^:0 т н _"
!
"ye:e always"
е:э <=
          ConsDent (У3:0 | Ы3:0 | О1:0) %^:0  н _ [ [ .#. | %^:0 Г1:0 а:я ] | н ь: | к ] ;
          [ [ [ BackStem - [ :* vows ] ] %^:0 | У3:0 | ConsDent Ы3:0 ]  %^:0 | У2:0 %^:0 VLoss:0 ] т н _ [ .#. | %^:0 [ Г1:0 а:я | т | л | Г2:в а | в | д | к | н | с | ш ] ];


"ye:e sometimes"
е:э =>
          [ ConsDent | ConsNonDent ] (У3:0 | Ы3:0 | О1:0) %^:0  н _ [ [ .#. | %^:0 Г1:0 а:я ] | н ь: | к ] ;
          [ ConsDent | ConsNonDent | м:0 ]  (У3:0 | Ы3:0 | О1:0 | У2:0) %^:0 (VLoss:0) т н _ [ .#. | %^:0 [ Г1:0 а:я | т | л | Г2:в а | в | д | к | н | с | ш ] ];


"a:ya"
а:я  <=> :vows  %^:0 Г1:0 _  ;


!Changes in suffix consonants
"v:0"
в:0 <=> [ [ Cons - л ] (У3:0 | И3:0 | Ы3:0 ) | л [ И3:ь | У3:0 ] ] %^:0 _ т [ О1:о | О1:е | О1:э ] м [ О1:о | О1:е | О1:0 ] ;

!!!Depricated due to 
!!ERROR: Label not found in 'EQV_LABELS' (114) **
!!Solved in several contexts by declaring both lexical
!!and surface forms in the alphabet.
"G1:0 sometimes"
Г1:0 <= :vows  %^:0 _ а:я ;

"G1:0 always"
Г1:0 => [ :vows | :к :0* ]  %^:0 _ а: ;

"G1:g"
Г1:г  <=> [ ConsVoicedDent ( :ь | У4:0 | У3:0 | И3:0 | Ы3:0) | й И3:0 | [ ConsVoicedNonDent - г ] ( :ь | У3:0 | И3:0 | Ы3:0) ] %^:0 _ а ;

"G1:k sometimes"
Г1:к  <=  [ ConsVoicelessDent ( :ь | И3:0 | У3:0 | Ы3:0 | У4:0 | У4:ь | О1:0) | [ ConsVoicelessNonDent - к ] (У3:0 | И3:0 | Ы3:0) | г (У3:0 | И3:0 | Ы3:0) | К4:0 ] %^:0 _ а  ;

"G1:k always"
Г1:к  =>  [ ConsVoicelessDent ( :ь | И3:0 | У3:0 | Ы3:0 | У4:0 | У4:ь | О1:0) | ConsVoicelessNonDent (У3:0 | И3:0 | Ы3:0) | г (У3:0 | И3:0 | Ы3:0) | К4:0 ] :0* %^:0 _ а  ;
          

!Prolative
"G2:g"
Г2:г  <=> [ ConsVoicedDent ( :ь | И3: | У3:0 | Ы3:0) | [ ConsVoicedNonDent - г ] ( :ь | И3: | У3:0 | Ы3:0) ] %^:0 _ а ;

"G2:v"! 
Г2:в  <=> [ :vows ( :0) %^:0 | У2:о %^:0 | И2:е %^:0 ] _ а [ .#. | %^:0 [ Г1:0 | Poss | т | н | с | д: | в | к с | л ] ];


"G2:k"
Г2:к  <=>  [ ConsVoicelessDent ( :ь | И3: | У3:0 | Ы3:0) | ConsVoicelessNonDent ( :ь | И3: | У3:0 | Ы3:0) | г (И3:0 | У3:0) ] %^:0 _ а [ .#. | %^:0 [ Г1:0 | Poss | т | н | с | д: | в | к с | л ] ] ;
           vows ConsVoiceless [ У2:0 | И2:0 ] %^:0 _ а ;

!!Depricated
"G3:g" !Imperative Clitics
Г4:г  <=> з ь %^:0 _ а ;
!!
!!!
"G4:0"
Г4:0  <=>
           К1: %^:0 _ а ;
  [ О1:е | О1:о ] %^:0 _ а: ;
!
!
"G4:k"
Г4:к  <=>  .#. [ и л я | у ж о | а з ё | а д я | а й д а ] %^:0 _ а  ;


"m:0" 
м:0 <=> 
 subseqBack  _ У2:0  %^:0  VLoss: [ т | т н е:э ] ;
        т (ь) _ [ У2: | И2: ]  %^:0  VLoss: [ с (т) [ О1:э | О1:о | О1:0 ] | с [ .#. | %^:0 [ Г1:0 | Г1:к ] ] ] ;



!! imperative suffix
"K1:t"
К1:т <=>  [ И1:0 | И1:ь | У1:0 | Ы1:0 ] %^:0 _  ;



! Suffix final hard vs soft
!Removing "Ы1:0" 17.04.2007
!Adding "И2:0" 18.04.2007
"U4:y"
У4:ь  <=>  
[ [ %^:0 [ О1:е | О1:э ] |  :subseqFront ] %^:0  [ т | н ] | FrontSoftStem  %^:0 т | [ :ь | И3:0 | ь:0 | :subseqFront ] %^:0 т | [ И1:ь | И1:0 | :subseqFront ] %^:0 :т | И2:е %^:0 т ]  _  [ .#. | %^:0 [ Г1:г | Г1:к ] ] ;


!Ablative d/t
"d:t"
д:т  <=> [ ConsVoiceless | д | г ] [ ь:0 | И3:0 | Ы3:0 | У3:0 ]  %^:0 _ [ О1:е | О1:э | О1:о | О1:0 ] ;
         т [ У2:0 | И2:0 ] %^:0 VLoss:0  _ [ О1:е | О1:э | О1:о ] ;
!Word final Vowels



"U2:0"
У2:0 <=>  _ %^:0 VLoss:0 [ т [ У4:0 | н е: | А2:а ] | с (т) [ О1:о | О1:0 ] | с [ %^:0 |  .#. ] | д:т [ О1:о | О1:е | О1:э ]  ] ;
          _ %^:0  А2:а ;


"U2:o"
У2:о  <=>  _ [ %^:0 [ т [ У4:0 | У4:ь ] | т н е: | с ь: | с (т) [ О1:о | О1:0 ] | с [ %^:0 |  .#. ] | л  ] | [ (н) з [ О1:о | О1:0 ] | ш к а | к с | н | в | Г2:в | д | с т [ %^:0 |  .#. ] | Г1:0 ] |  .#. ] ;


"YI2:y"
И2:ь  <=>  ConsDent _ %^:0 VLoss:0 [ с (т) [ О1:э | О1:0 ] | с [ %^:0 |  .#. ] ] ;

"YI2:ye"
И2:е  <=>   _ [ %^:0 [ т [ У4:0 | У4:ь ] | т н е | с ь: | с (т) [ О1:э | О1:0 | ь: ] | с [ %^:0 |  .#. ] | л ] | [ (н) з [ О1:э | О1:0 ] | ш к а | к с | н | в | д | с т [ %^:0 |  .#. ] | Г1:0 ] |  .#. ] ;


"YI2:0 always"
И2:0 <=> Cons _ %^:0 VLoss:0 т [ У4:0 | У4:ь | н е: | А2:я [ д | н ] ] ;
         ConsNonDent: _ %^:0 VLoss:0 [ с (т) [ О1:э | О1:0 ] | с [ %^:0 |  .#. ] ] ;
           т _  %^:0 VLoss:0 д:т [ О1:е | О1:0 ] ;
         vows ConsVoiceless _ %^:0 VLoss:0 Г2:к а ;
         [ ConsBack | й:0 ] _ %^:0  А2:я ;


!Disallow

"Disallow vows Y3"
У3:0 /<= vows _ ;  

"Disallow vows consequtive O1"
О1:0 /<= :vows %^:0 О1:  %^:0 _ ;  

