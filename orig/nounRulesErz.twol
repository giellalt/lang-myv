! -*- encoding: utf-8-unix -*-
Alphabet
а б в г д е ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я  
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я


! verbal stem vowels
!these are used with verbs like:
! "кадомс", "молемс" and  "пезэмс"
!У1: !
!И1: !
!Ы1: !

! Nominal stem vowels that alternate 
! work with "+VLoss" and "+NoVLoss" tags
!
У2:
И2: 

!!!!!!
!!кедИ3
!!покшУ3
!!пикшЫ3
У3:0 !
И3:0 !
Ы3:0 !

!Suffix final variant vowel for hard vs soft
У4:0 !

! verbal stem consonant that appear in PretI 3sg
!Т1:0 !

! derivational suffix protovowel with "o" and "a" 
!realisation  in deverbal noun ending
!А1:а !

! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
А2: !А2
Ы2:ы !

! derivational suffix protovowel  "mo" and "me"
О1: !

! clitic proto consonant
Г1: !



! Prolative Ga
Г2: !


! Imperative clitic proto consonant
!Г4:г !
!Г4:к !
!Г4:0 !
!Replace these with "G1" but apply stem final "K4" to
!




! imperative suffix
!К1:к !

!

! connegative suffix rendering vowel
!К3:0 !

! Stem final on Imperative Verbs
!!К4:0 !


! Some verbs need a marker for vowel placement
!Used before Conjuntive ending
V1: !
! I2 and U2 adjectives and nouns need this with their "t" and "s"
!V2:0 

VLoss:0
NoVLoss:0

%- !Hyphen with constructions like "-buti"
%^:0

;


Sets
vows = а о у ы э я ё ю и е ;
verbStemVows = а о э я ё е ;
verbStemVowStrong = а о э я ё е ;
noGlideVow = а о у ы э ;
glideVow = я ё ю и е ;
subseqBack = а о у я ё ю ;
subseqFront = ы и е э ;

Cons = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
ConsBack = б в г д ж з к л м н п р с т ф х ц ч ш щ ;
ConsVoiced = б в г д ж з й л м н р ;
ConsVoicedDent = д з л н р ;
ConsVoicedNonDent = б в г ж й м ;
ConsVoiceless = к п с т ф х ц ч ш щ ;
ConsVoicelessDent = с т ц ;
ConsVoicelessNonDent = к п ф х ч ш щ ;
ConsNonDent = б в г ж к м п ф х ч ш щ ;
ConsDent = д з л н р с т ц  ;
ConsDentNoL = д з н р с т ц  ;

Letters = vows Cons ь ъ ;

Definitions
SubseqBackHarm = [А2:а|а| О1:о|У2:о | V1:о | о |а:я| у| А2:я|я| ё| ю] ;
Back = .#. :* [SubseqBackHarm \[:subseqFront|:й|:ь|Ы3:|И3:|И2: ]* ( л ь ) [ConsBack]]  ;

FrontHard = .#. :* [[subseqFront|:й|ConsDentNoL :ь ] \[SubseqBackHarm|У3:|У2:]+|л ь Cons+|subseqFront л ь] ConsDent ;

FrontSoft = .#. :* [[subseqFront|й] \[SubseqBackHarm|У3:|У2:]+| ConsDentNoL ь ConsNonDent|subseqFront л ь ConsNonDent|й:] ;
!Removing  ~SubseqBackHarm+

Front = [FrontHard | FrontSoft |.#. :*  [:subseqFront|:ь|:й И3:0 |:й И2:0]] ;
!2007.04.11
!didn't accept istoriya, ideya
!accepted aziya, liya [:* - SubseqBackHarm*]] ;


Poss = (V1:е|V1:э|V1:о) [м | н [У4:0|У4:ь|ь:] | т [У4:0|У4:ь|ь:]  | (н) з [О1:0|О1:о|О1:э] | н [О1:о|О1:е|О1:э] к | н к | с т [ %^:0 [Г1:0|Г1:к] а:| #:]] ;


Rules


!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
 X:Y <=> Cons _  %^:0 [А2:я|А2:а [ н | т ]| Ы2:] ;
!                                           Cons  _ К4:0 %^:0  Ы2: ;
          where X in (а о э я ё е)
                Y in (0 0 0 0 0 0) matched ;

!!Consonant Stem Verbs lose stem vowel
!"verbStemVowWeakU1:0"
!  У1:0 <=> ConsBack _ %^:0 [А2: [ н | т ]|т А2: [ д | н ]|К1:|Ы2:| с [(т) ь: |  [ы | а ] ]| V1:о] ;


!!Consonant Stem Verbs lose stem vowel
!"verbStemVowWeakI1:0"
!  Ы1:0 <=> [ConsDent|vows] _ %^:0 [А2: [ н | т ]|т А2: [ д | н ]|К1:|Ы2:| с [(т) ь: |  [ы | а ] ]| V1:] ;


!!Consonant Stem Verbs with Palatalized Dental lose stem vowel
!"YI1:0"
!       И1:0 <=> [Cons|vows|й:] _ %^:0 [А2: [ н | т ]| Ы2: | V1:е];
!      .#. :* [Cons - л]  _ %^:0 [т А2: [ д | н ]| К1:] ;
!      .#. :* [Cons - л|vows]   _ %^:0 с (т) ь: ;
!       [ConsNonDent|й:|vows] _ %^:0 с [ы |а ] ;
!                й:0 _ %^:0 [Ы2:|V1:е] ;

!"YI1:y"
!И1:ь <=>  ConsDent  _ %^:0  [Г1: | Г2: ] а  ;
!      .#.   :* л  _ %^:0 [т А2:я [ д | н ]| К1: |с (т) ь:] ;


!"U1:o, YI1:ye"
![У1:о| И1:е] <=> [Cons|vows] _ %^:0 К3:0 ;

!"I1:e"
!Ы1:э <=> ConsDent _ %^:0 К3:0 ;
!
!Vowels in Nouns and Adjectives 
!2006-12-02
"YI3:y"
И3:ь <=>  ConsDent  _ [%^:0  [Г1:г|Г1:к|Г2:г|Г2:к|с [%^:0 |#: ] | с (т) О1:э |:ConsNonDent]| #:] ;
      .#.   :* л  _ [%^:0 (в:0) :Cons|#:] ;




!!Changes in stem Consonants
!"T1:t"
!Т1:т <=> [ е | о ]  _ %^:0 с (т) ь: ;

!"A1:o"
!А1:о <=>  [ а |я ] %^:0  м  _ ;

"Y2:yi"
Ы2:и <=> [ я:0 | ё:0 | е:0 |ConsNonDent noGlideVow:0]  %^:0 _ ;


"O1:ye"
О1:е <=> [[FrontSoft|FrontHard|И3:] %^:0 | [в|в:0] т [ :е| :э]| И2:0 %^:0 V1:е в т [ :е| :э] ] м  _  [ #: |%^:0 [Г1:0 | Poss |н т ]] ;
         [[FrontSoft] %^:0 (V1:э)| [И3:0| И2:0] %^:0 V1:е (%^:0)|Ы3:0 %^:0 V1:э (%^:0)] н _ к ;
         [[subseqFront| И3:] %^:0| И2:0 %^:0 VLoss:0 |%^:0 [V1:е|V1:э]] д:  _ [%^:0 [Г1:0|  Poss |н т ]| #: ] ;
         [[subseqFront| И3:|е] %^:0 |И2:0 %^:0 V1:е] [в:0 | в ] т _ м [О1:0|О1:е] ;

!!2008.09.12 dealing with О1:о
!
"O1:o"
О1:о <=> [[.#. :* Back|У3:] %^:0 | [в|в:0] т О1:о| У2:0 %^:0 V1:о в т О1:о ] м  _  [ #: |%^:0 [Г1:0 | Poss |н т ]] ;
         [[Back|subseqBack| У3:] %^:0|%^:0 V1:о] з _ %^:0  [м | н У4:0 | т У4:0  | н з О1:о | н О1:о к | н к | с т] [ %^:0 [Г1:0|Г1:г|Г1:к] а:| #:] ;

          [.#. [:* Back] - [:* [vows|й]] %^:0 (V1:о)|subseqBack %^:0 | У3:0 %^:0 V1:о (%^:0) | О1:о  %^:0 ]  н _ к ;
         [[subseqBack| У3:] %^:0 | У2:0 %^:0 VLoss:0 |%^:0 V1:о] д:  _ [%^:0 [Г1:0|  Poss |н т ]| #: ] ;
         [[subseqBack| У3:] %^:0 |У2:0 %^:0 V1:о] в: т _ м [О1:0|О1:о] ;
         [[У3:|SubseqBackHarm] %^:0|V1:о |У2: %^:0 VLoss:0] с (т) _  [ #: |%^:0 [Г1:0 | Poss |т | н| с |Г2:в |д: |л ]] ;
         [[SubseqBackHarm] %^:0|%^:0 V1:о] (н) з _ [ #: |%^:0 [Г1:0|л| т | н| с |Г2:в |д: ] ] ;



"O1:0"
О1:0 <=>   _  %^:0 [А2:а|А2:я] ;
         к с _ [#: |%^:0 А2:а ] ;


!2007-5-14 changing "[И3:|Ы3:|Front]" to "[И3:|Ы3:|:subseqFront|й|ь]"
!to cope with "церькова"
!
"O1:e"
О1:э <=>
         ConsDent Ы3:0  %^:0 д:  _ [%^:0 [Г1:0 |  Poss| н т ] | #: ] ;
         [[И3:|Ы3:|:subseqFront|й|ь] %^:0|V1:е |V1:э |И2: %^:0 VLoss:0] с (т) _  [ #: |%^:0 [Г1:0 | Poss |т | н| с |Г2:в |д: |л ]] ;
         [[[:subseqFront] %^:0|%^:0 V1:е|%^:0 V1:э] (н)| ConsVoiced [И3:0 | И3:ь| Ы3:0 ] %^:0 ] з _ [ #: |%^:0 [Г1:0|л] ] ;
         ConsDent Ы3:0  %^:0 в:0 т _ м [О1:0|О1:е] ;
         [subseqFront %^:0| ConsVoiced [И3:0 | И3:ь| Ы3:0 ] %^:0 |%^:0 V1:е|%^:0 V1:э] з _ %^:0 [м | н У4:ь | т У4:ь  | н з О1:э | н О1:е к | н к | с т] [ %^:0 [Г1:0|Г1:г|Г1:к] а:| #:] ;



!Changes in the suffix vowels
"A2:ya"
А2:я <=>
         [[subseqFront|й|ь][ConsDent - л]|И2:0|[ConsDent - л] [И1:0| И2:0|ь:0]|л И1:ь | :subseqFront | й (И1:0|И2:0)|И3:0] %^:0 т _  [ д | н ] ;
 [ ConsDent [И2:0|И3:0]|ConsDent [ь:0 |я:0 | ё:0 | е:0]|я:0 | ё:0  ]  %^:0  _ [ т | н ] ;
         vows [(й:) И2:0 |й: И3:0 ]  %^:0  _ [ т | н ] ;

!Changes in the suffix vowels
!HERE
"A2:a"
А2:а <=> [ConsDent [э:0|Ы1:0|Ы3:0]| [ в: т О1: м | с ( т )| з ] О1:0 |ConsNonDent [е:0|И3:0|Ы3:0|И2:0]|Back |а:0|о:0|У1:0|У2:0|У3:0|Ы1:0| Ы2:0|Ы3:0|м | SubseqBackHarm :0* т У4:0 ] %^:0  _  [ т | н ] ;
         [:subseqBack|У1:0|У2:0|У3:0|Ы1:0| Ы2:0|Ы3:0] %^:0 т _  [ д | н ] ;



"j:0"
й:0 <=> vows _ [И2:е  |И3:0  %^:0 Ы2:и|(И2:0|И3:0)  %^:0 [А2:я |V1:е :Cons] ] ;


!
!18.04.2007 Adding "U2:" and contemplating environment "%^:0 т н _"
!
"ye:e always"
е:э <=
          ConsDent (У3:0|Ы3:0|О1:0) %^:0  н _ [[ #:|%^:0 Г1:0 а:я]| н ь: | к] ;
          [[[Back - [:* vows]] %^:0| У3:0|ConsDent Ы3:0]  %^:0| У2:0 %^:0 VLoss:0] т н _ [ #: |%^:0 [Г1:0 а:я |т| л|Г2:в а | в | д | к | н | с | ш ]];


"ye:e sometimes"
е:э =>
          [ConsDent|ConsNonDent] (У3:0|Ы3:0|О1:0) %^:0  н _ [[ #:|%^:0 Г1:0 а:я]|н ь:|к ] ;
          [ConsDent|ConsNonDent|м:0]  (У3:0|Ы3:0|О1:0|У2:0) %^:0 (VLoss:0) т н _ [ #: | %^:0 [Г1:0 а:я | т | л | Г2:в а | в | д | к | н | с | ш ]];


"a:ya"
а:я  <=> :vows  %^:0 Г1:0 _  ;


!Changes in suffix consonants
"v:0"
в:0 <=> [[Cons - л] (У3:0 |И3:0 |Ы3:0 )|л [И3:ь|У3:0]] %^:0 _ т [О1:о|О1:е|О1:э] м [О1:о|О1:е|О1:0] ;

!!!Depricated due to 
!!ERROR: Label not found in 'EQV_LABELS' (114) **
!!Solved in several contexts by declaring both lexical
!!and surface forms in the alphabet.
"G1:0 sometimes"
Г1:0 <= :vows  %^:0 _ а:я ;

"G1:0 always"
Г1:0 => [:vows | :к :0*]  %^:0 _ а: ;

"G1:g"
Г1:г  <=> [ConsVoicedDent (:ь|У4:0|У3:0|И3:0|Ы3:0)|й И3:0 |[ConsVoicedNonDent - г] (:ь|У3:0|И3:0|Ы3:0)] %^:0 _ а ;

"G1:k sometimes"
Г1:к  <=  [ConsVoicelessDent (:ь|И3:0|У3:0|Ы3:0|У4:0|У4:ь|О1:0)|[ConsVoicelessNonDent - к] (У3:0|И3:0|Ы3:0) | г (У3:0|И3:0|Ы3:0) |К4:0] %^:0 _ а  ;

"G1:k always"
Г1:к  =>  [ConsVoicelessDent (:ь|И3:0|У3:0|Ы3:0|У4:0|У4:ь|О1:0)|ConsVoicelessNonDent (У3:0|И3:0|Ы3:0) | г (У3:0|И3:0|Ы3:0) |К4:0] :0* %^:0 _ а  ;
          

!Prolative
"G2:g"
Г2:г  <=> [ConsVoicedDent (:ь|И3:|У3:0|Ы3:0)|[ConsVoicedNonDent - г ] (:ь|И3:|У3:0|Ы3:0)] %^:0 _ а ;

"G2:v"! 
Г2:в  <=> [:vows (:0) %^:0 | У2:0 %^:0 V1:о|И2:0 %^:0 V1:е ] _ а [ #: |%^:0 [Г1:0 | Poss |т | н| с |д: |в |к с| л ] ];


"G2:k"
Г2:к  <=>  [ ConsVoicelessDent (:ь|И3:|У3:0|Ы3:0)|ConsVoicelessNonDent (:ь|И3:|У3:0|Ы3:0)| г (И3:0|У3:0)] %^:0 _ а [ #: |%^:0 [Г1:0 | Poss |т | н| с |д: |в |к с| л ] ] ;
           vows ConsVoiceless [У2:0 | И2:0 ] %^:0 _ а ;

!!Depricated
!!"G3:g" !Imperative Clitics
!!Г4:г  <=> з ь %^:0 _ а ;
!!
!!!
!!"G4:0"
!!Г4:0  <=>
!!           К1: %^:0 _ а ;
!!  [О1:е|О1:о] %^:0 _ а: ;
!
!
!!"G3:k"
!!Г4:к  <=>  #: [и л я|у ж о|а з ё|а д я|а й д а] %^:0 _ а  ;


"m:0" 
м:0 <=> 
 subseqBack  _ У2:0  %^:0  VLoss: [ т| т н е:э ] ;
        т (ь) _ [У2:|И2:]  %^:0  VLoss: [с (т) [О1:э|О1:о|О1:0]|с [#:| %^:0 [Г1:0|Г1:к]]] ;



!! imperative suffix
!"K1:t"
!К1:т <=>  [ И1:0 |И1:ь| У1:0 | Ы1:0 ] %^:0 _  ;



! Suffix final hard vs soft
!Removing "Ы1:0" 17.04.2007
!Adding "И2:0" 18.04.2007
"U4:y"
У4:ь  <=>  [[И3:0|И3:ь| И1:0 | :subseqFront ] %^:0 (V1:е|V1:э) [ :т|н]|И2:0 %^:0 [VLoss:0|V1:е] т| Ы3:0  %^:0 V1:э т]  _  [#: |%^:0 [Г1:г|Г1:к]] ;


!Ablative d/t
"d:t"
д:т  <=> [ ConsVoiceless | д | г] [ь:0|И3:0|Ы3:0|У3:0]  %^:0 _ [О1:е|О1:э|О1:о] ;
         т [ У2:0 | И2:0 ] %^:0 VLoss:0 _ [О1:е|О1:э|О1:о] ;
!Word final Vowels


"y:0"
ь:0 <=>        ConsDent  _  %^:0 V1:е :Cons ;
              ConsDentNoL _ %^:0 [в:0 т О1:е м [О1:е|О1:0] | н|д:|т|А2:я|с (т) [ь:|е:]] ;

"V1:ye"
V1:е <=> ConsDent: ь:0 %^:0 _ :Cons ;
         [FrontSoft FrontHard]  %^:0 _ :Cons;
       [[Cons|й:|vows]  И1:0|[Cons|й:] И2:0|И3:0] %^:0  _ ;


"V1:e"
V1:э <=> FrontHard %^:0 _ :Cons ;
         ConsDent [Ы1:0|Ы3:0] %^:0  _ ;


"V1:o"
V1:о <=> Back ( У4:0 ) %^:0 _ :Cons ;
         Cons [У1:0|У2:0|У3:0]  %^:0  _ ;



"U2:0"
У2:0 <=>  _ %^:0 VLoss:0 [т [У4:0|У4:ь] |т н е: |с (т) [О1:о|О1:0]| с [ %^:0 |  #: ] ] ;
         _ %^:0 [V1:о| (т) [А2:я|А2:а]| т н е:э ] ;
        т  _ %^:0 VLoss:0 д:т [ О1:о | О1:е | О1:э ] ;

"U2:o"
У2:о  <=>  _ [%^:0 NoVLoss:0 [т [У4:0|У4:ь]|т н е: |с ь:|с (т) [О1:о|О1:0]| с [ %^:0 |  #: ]| л  ]| [(н) з [О1:о|О1:0]|ш к а|к с | н|в|Г2:в|д | с т [ %^:0 |  #: ]] |%^:0  Г1:0|  #:] ;


"YI2:0 sometimes"
И2:0 <= Cons _ %^:0 (VLoss:0) т [У4:0|У4:ь |н е: |А2:я [д|н]] ;
         ConsNonDent: _ %^:0 (VLoss:0) [с (т) [О1:э|О1:0]| с [ %^:0 |  #: ] ] ;
         [ConsBack|й:0] _ %^:0 [V1:е| А2:я|А2:а] ;
           т _  %^:0 VLoss:0 д:т [О1:е|О1:э|О1:о] ;

"YI2:0 always"
И2:0 => Cons _ %^:0 (VLoss:0) т [У4:0|У4:ь |н е: |А2:я [д|н]] ;
         ConsNonDent: _ %^:0 (VLoss:0) [с (т) [О1:э|О1:0]| с [ %^:0 |  #: ] ] ;
         [ConsBack|й:0] _ %^:0 [V1:е| А2:я|А2:а] ;
           т _  %^:0 VLoss:0 д:т [О1:е|О1:э|О1:о] ;
         vows ConsVoiceless _ %^:0 (VLoss:0) Г2:к а ;

"YI2:y"
И2:ь  <=>  ConsDent _ %^:0 VLoss:0 [с (т) [О1:э|О1:0]| с [ %^:0 |  #: ] ] ;


"YI2:ye"
И2:е  <=>   _ [%^:0 NoVLoss:0 [т [У4:0|У4:ь]|т н е|с ь:|с (т) [О1:э|О1:0] |с (т) ь:| с [ %^:0 |  #: ] | л ]| [(н) з [О1:э|О1:0]|ш к а|к с | н|в|д | с т [ %^:0 |  #: ]] |%^:0  Г1:0|  #: ] ;


!Disallow

"Disallow vows Y3"
У3:0 /<= vows _ ;  

