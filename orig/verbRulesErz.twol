! -*- coding: utf-8-unix -*-
Alphabet
а б в г д е  ё ж з и й к л м н о п р с т у ф х ц ч ш щ ъ ы ь э ю я  
А Б В Г Д Е Ё Ж З И Й К Л М Н О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я


! verbal stem vowels
!these are used with verbs like:
! "кадомс", "молемс" and  "пезэмс"
У1:0 !
И1:0 !
Ы1:0 !


!Suffix final variant vowel for hard vs soft
У4:0 !

! verbal stem consonant that appear in PretI 3sg
Т1:0 !

! conjugational suffix protovowels 
!showing "a" vs "я" and "ы" vs "и" alternation
А2:а !А2
Ы2:ы !

! derivational suffix protovowel  "mo" and "me"
О1:о !

! clitic proto consonant
Г1: !





! imperative suffix
К1:к !

!

! connegative suffix rendering vowel
К3:0 !



! Some verbs need a marker for vowel placement
!Used before Conjuntive ending
V1: !


%- !Hyphen with constructions like "-buti"
%^:0

;


Sets
vows = а о у ы э я ё ю и е ;
verbStemVows = а о э я ё е У1 И1 Ы1 ;
verbStemVowStrong = а о э я ё е ;
verbStemVowWeak = У1 И1 Ы1 ;
noGlideVow = а о у ы э ;
glideVow = я ё ю и е ;
subseqBack = а о у я ё ю ;
subseqFront = ы и е э ;

Cons = б в г д ж з й к л м н п р с т ф х ц ч ш щ ;
ConsBack = б в г д ж з к л м н п р с т ф х ц ч ш щ ;
ConsVoiced = б в г д ж з й л м н р ;
ConsVoicedDent = д з л н р ;
ConsVoicedNonDent = б в г ж й м ;
ConsVoiceless = к п с т ф х ц ч ш щ ;
ConsVoicelessDent = с т ц ;
ConsVoicelessNonDent = к п ф х ч ш щ ;
ConsNonDent = б в г ж к м п ф х ч ш щ ;
ConsDent = д з л н р с т ц  ;
ConsDentNoL = д з н р с т ц  ;

Letters = vows Cons ь ъ ;

Definitions
SubseqBackHarm =  [А2:а|а| О1:о|У2:о|V1:о| о |а:я| у| А2:я|я| ё| ю] ;
Back = .#. :* [SubseqBackHarm \[:subseqFront|:й|:ь|Ы1:|И1: ]* ( л ь ) [ConsBack]]  ;

FrontHard = .#. :* [[subseqFront|:й|ConsDentNoL :ь ] \[SubseqBackHarm|У1:]+|л ь Cons+|subseqFront л ь] ConsDent ;


FrontSoft = .#. :* [[subseqFront|й] \[SubseqBackHarm|У1:]+| ConsDentNoL ь ConsNonDent|subseqFront л ь ConsNonDent|й:] ;

Front = [FrontHard | FrontSoft |.#. :*  [:subseqFront|:ь |:й И1:0]] ;


Poss = (V1:) [ м | н [ У4: | ь: ] | т [ У4: | ь: ]  | (н) з О1: | н О1: к | н к | с т [ %^:0 Г1: | .#. ] ] ;


Rules

!Changes in the stem vowels before "10", "20", "30" and "60"
"verbStemVowStrong:0"
 X:Y  <=> [Cons | vows ] _ (Т1:0) %^:0 [ А2: [ н | т ] | Ы2:[ д | з | к | л | м | н | т | я | [ #: | %+:0 Г1:0 а:я ] ] ] ;
          where X in (а о э я ё е)
                Y in (0 0 0 0 0 0) matched ;


"YI1:y"
И1:ь <=>  ConsDent  _ %^:0  [ с [ ы | а ] | Г1: ] ;
      .#.   :* л  _ %^:0 [ т: А2:я [ д | н ] | К1: | с (т) ь: ] ;


!Changes in stem Consonants
"T1:t"
Т1:т <=> [ е | о ]  _ %^:0 с (т) ь: ;

"Y2:yi"
Ы2:и <=> [ я:0 | ё:0 | е:0 (Т1:0) | И1:0 | ConsNonDent [ noGlideVow:0 | У1:0 ] ]  %^:0 _ ;


"O1:ye"
О1:е <=> [ subseqFront %^:0 | %^:0 :subseqFront ] в: _ л ;
         [ subseqFront %^:0 | %^:0 :subseqFront ] д:  _  [ #: | %^:0 Г1: ] ;




"O1:e"
О1:э <=> [ subseqFront (Т1:0) %^:0 | %^:0 :subseqFront ] [ с (с) | з (с | з) ]  _  [ #: | %^:0 Г1: ] ;


"t:d old orth sometimes"
т:д <= ConsVoiced [У1: | И1: | Ы1: ] _ А2: ;

!"t:d old orth always"
!т:д => ConsVoiced [У1: | И1: | Ы1: ] _ А2: ;



!Changes in the suffix vowels
"A2:ya"
А2:я <=>
! changing left environment "I2:0" to "I1:0" 19.04.2007
         [ [ subseqFront | й | ь ][ ConsDent - л ] | И1:0 | [ ConsDent - л ] [ И1:0 | И2:0 | ь:0 ] | л И1:ь | :subseqFront (Т1:0) | й (И1:0) ] %^:0 т: _  [ д | н | м ] ;
 ConsDent [ И1:0  | ь:0 | я:0 | ё:0 | е:0 (Т1:0) ]  %^:0  _ [ д | т | н | м ] ;
         vows [ Ы1:0 | (й:) И1:0 | я:0 | е:0 ]  %^:0  _ [ т | н | м ] ;


"j:0"
й:0 <=> vows _ И1:0 %^:0 [ V1:е | А2:я | Ы2:и ] ;



"a:ya"
а:я  <=> :vows  %^:0 (К3:0 %^:0 ) Г1:0 _  ;
[ [ И1:0 | Ы1:0 ] %^:0  :т У4: | ь:0 ] %+:0  _  я ;
          Front :0*  К1:т У4:0 %^:0 _ ;

!clitics in "ая"
"y:0" 
ь:0  <=> _  %+:0  а:я  я ;


!!Verb-only! I might need this for Conj
!Changes in suffix consonants
!"v:0"
!в:0 <=> Cons %^:0 _ т О1: м О1: ;


"G1:0 sometimes"
Г1:0 <= :vows (:0)* %^:0 _ а:я ;

"G1:0 always"
Г1:0 => [:vows | :к ] (:0)* %^:0 _ а: ;

"G1:g"
Г1:г  <=> [ ConsVoicedDent (:ь | У4:) | [ ConsVoicedNonDent - г ] (:ь) ] %^:0 _ а ;

"G1:k sometimes"
Г1:к  <=  [ ConsVoicelessDent (:ь | У4:) | ConsVoicelessNonDent - [ к | К1:к ] | г | К1:т У4: ] %^:0 _ а  ;

"G1:k always"
Г1:к  =>  [ ConsVoicelessDent (:ь | У4:) | ConsVoicelessNonDent :0*  | г | К1:т У4: ] %^:0 _ а  ;


!!Depricated
!!"G3:g" !Imperative Clitics
!!Г4:г  <=> з ь %^:0 _ а ;
!!
!!!
!!"G4:0"
!!Г4:0  <=>
!!           К1: %^:0 _ а ;
!!  [ О1:е | О1:о ] %^:0 _ а: ;
!
!а
!!"G3:k"
!!Г4:к  <=>  #: [ и л я | у ж о | а з ё | а д я | а й д а ] %^:0 _ а  ;



! imperative suffix
"K1:t"
К1:т <=> [  Cons | #: Cons* vows т ] [ И1:0 | И1:ь | У1:0 | Ы1:0 ] %^:0 _ У4: ;


!! imperative suffix
!"K1:0"
!К1:0 <=> Cons* vows Cons+ т [ И1:0 | И1:ь | У1:0 | Ы1:0 ] %^:0 _ У4: ;



! Suffix final hard vs soft
"U4:y"
У4:ь  <=>   [ И1: | Front ]  %^:0  :т  _  [ #: | %^:0 Г1:к ] ;

! Suffix final hard
"U4:0"
У4:0  <=>   [[ У1: | Back ]  %^:0  :т| К1:к ]  _  [ #: | %^:0 Г1:к ] ;
               К1: _ %^:0  а: ;



!!!!!!!!!!!!!!!!!!!!!!!!
!!!!Word final Vowels



"V1:ye"
V1:е <=> ConsDent: ь:0 %^:0 _ :Cons ;
         [ FrontSoft Fronthard ]  %^:0 _ :Cons;
       [ Cons | й: | vows ]  И1:0 %^:0  _ ;


"V1:e"
V1:э <=> FrontHard %^:0 _ :Cons ;
         ConsDent Ы1:0 %^:0  _ ;


"V1:o"
V1:о <=> .#. [ :* Back ] - [ :* [ vows | й ] ] %^:0 _ :Cons ;
         Cons У1:0 %^:0  _ ;


